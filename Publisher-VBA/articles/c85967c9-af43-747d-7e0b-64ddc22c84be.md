
# ShapeRange Object (Publisher)

 **Last modified:** July 28, 2015

Represents a shape range, which is a set of shapes on a document. A shape range can contain as few as one shape or as many as all the shapes in the document. You can include whichever shapes you want â€” chosen from among all the shapes in the document or all the shapes in the selection â€” to construct a shape range. For example, you could construct a  **ShapeRange** collection that contains the first three shapes in a document, all the selected shapes in a document, or all the freeform shapes in a document.

 **Note**  Most operations that you can do with a  ** [Shape](666cb7f0-62a8-f419-9838-007ef29506ee.md)** object, you can also do with a **ShapeRange** object that contains only one shape. Some operations, when performed on a **ShapeRange** object that contains more than one shape, will cause an error. This section describes how to:


- Return a set of shapes.
    
- Return a  **ShapeRange** object within a selection or range.
    
- Align, distribute, and group shapes in a  **ShapeRange** object.
    

## Example

Use  **Shapes.Range**(index), where index is the index number of the shape or an array that contains index numbers of shapes, to return a  **ShapeRange** collection that represents a set of shapes in a publication. You can use Visual Basic's **Array** function to construct an array of index numbers. The following example sets the fill pattern for shapes one through three on the active publication.


```
Sub ChangeFillPattern() 
    ActiveDocument.Pages(1).Shapes.Range(Array(1, 2, 3)) _ 
        .Fill.PresetGradient Style:=msoGradientDiagonalDown, _ 
        Variant:=1, PresetGradientType:=msoGradientHorizon 
End Sub
```

Although you can use the  ** [Range](f9ef5314-21f1-378f-1552-fcd4e46f841d.md)** method to return any number of shapes, it is simpler to use the ** [Item](f316bbac-b0be-0281-585b-c32dcb709b66.md)** method if you want to return only a single member of the collection. For example, **Shapes**(1) is simpler than  **Shapes.Range**(1).

Use  **Selection.ShapeRange**(index), where index is the index number of the shape, to return a  **Shape** object that represents a shape within a selection. The following example selects the first two shapes on the first page of the active publication and then sets the fill for the first shape in the selection.




```
Sub ChangeFillForShapeRange() 
    ActiveDocument.Pages(1).Shapes.Range(Array(1, 2)).Select 
    Selection.ShapeRange(1).Fill.ForeColor.RGB = RGB(255, 0, 0) 
End Sub
```

This example selects all the shapes on the first page of the active publication, then adds and formats text in the second shape in the range.




```
Sub SelectShapesOnPageOne() 
    ActiveDocument.Pages(1).Shapes.Range.Select 
    With Selection.ShapeRange(2).TextFrame.TextRange 
        .Text = "Shape Number 2" 
        .ParagraphFormat.Alignment = pbParagraphAlignmentCenter 
        .Font.Size = 25 
    End With 
End Sub
```

Use the  ** [Align](ef522d47-3fc7-cfca-5b9a-44ff020f8b31.md)** , ** [Distribute](a145fb46-d7b6-bc3c-b7fd-cdb892fda179.md)** , or ** [ZOrder](2043f78c-ab83-e719-c3b5-5d75edcf1593.md)** method to position a set of shapes relative to each other or relative to the document. This example specifies a shape range and left-aligns and vertically distributes the shapes on the page.




```
Sub AlignDistibuteShapes() 
    Dim rngShapes As ShapeRange 
    Set rngShapes = ActiveDocument.Pages(1).Shapes.Range 
 
    With rngShapes 
        .Align AlignCmd:=msoAlignLefts, RelativeTo:=msoFalse 
        .Distribute DistributeCmd:=msoDistributeVertically, RelativeTo:=msoTrue 
    End With 
End Sub
```

Use the  ** [Group](ca3e011f-72ea-904e-da3f-cac7fe24341d.md)** , ** [Regroup](29342a78-9425-2356-963c-36a62a7f3091.md)** , or ** [Ungroup](253a366c-7317-14e7-2668-191eccec6cb8.md)** method to create and work with a single shape formed from a shape range. The ** [GroupItems](d37c75cd-a651-51d1-42c7-59879ccbbf1d.md)** property for a **Shape** object returns the ** [GroupShapes](dd723f99-25a9-81cc-1d16-eb7dcd651c5e.md)** object, which represents all the shapes that were grouped to form one shape. This example specifies a shape range and left-aligns and vertically distributes the shapes on the page.




```
Sub GroupShapes() 
    Dim rngShapes As ShapeRange 
    Set rngShapes = ActiveDocument.Pages(1).Shapes.Range 
    rngShapes.Group 
 
    rngShapes(1).Fill.OneColorGradient _ 
        Style:=msoGradientFromCenter, _ 
        Variant:=2, Degree:=1 
End Sub
```

