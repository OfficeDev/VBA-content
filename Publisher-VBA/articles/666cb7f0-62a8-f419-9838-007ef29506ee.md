
# Shape Object (Publisher)

 **Last modified:** July 28, 2015

Represents an object in the drawing layer, such as an AutoShape, freeform, OLE object, ActiveX control, or picture. The  **Shape** object is a member of the ** [Shapes](52e069a6-d54b-a11a-1cba-96174329cb02.md)** collection, which includes all the shapes on a page or in a selection.

 **Note**  There are three objects that represent shapes: the  **Shapes** collection, which represents all the shapes on a document; the ** [ShapeRange](c85967c9-af43-747d-7e0b-64ddc22c84be.md)** collection, which represents a specified subset of the shapes on a document (for example, a **ShapeRange** object could represent shapes one and four on the document, or it could represent all the selected shapes on the document); the **Shape** object, which represents a single shape on a document. If you want to work with several shape at the same time or with shapes within the selection, use a **ShapeRange** collection. This section describes how to:


- Return an existing shape on a document.
    
- Return a shape or shapes within a selection.
    
- Return a newly created shape.
    
- Work with a group of shapes.
    
- Format a shape.
    
- Use other important shape properties.
    

## Example

Use  ** [Shapes](52e069a6-d54b-a11a-1cba-96174329cb02.md)** (index), where index is the name or the index number, to return a single **Shape** object. The following example horizontally flips shape one on the active document.


```
Sub FlipShape() 
    ActiveDocument.Pages(1).Shapes(1).Flip FlipCmd:=msoFlipHorizontal 
End Sub
```

The following example horizontally flips the shape named "Rectangle 1" on the active document.




```
Sub FlipShapeByName() 
    ActiveDocument.Pages(1).Shapes("Rectangle 1") _ 
        .Flip FlipCmd:=msoFlipHorizontal 
End Sub
```

Each shape is assigned a default name when it is created. For example, if you add three different shapes to a document, they might be named "Rectangle 2," "TextBox 3," and "Oval 4." To give a shape a more meaningful name, set the  **Name** property of the shape.

Use  **Selection.ShapeRange**(index), where index is the name or the index number, to return a  **Shape** object that represents a shape within a selection. The following example sets the fill for the first shape in the selection, assuming that the selection contains at least one shape.




```
Sub FillSelectedShape() 
    Selection.ShapeRange(1).Fill.ForeColor.RGB = RGB(255, 0, 0) 
End Sub
```

The following example sets the fill for all the shapes in the selection, assuming that the selection contains at least one shape.




```
Sub FillAllSelectedShapes() 
    Dim shpShape As Shape 
    For Each
```




```
shpShape In Selection.ShapeRange 
       
```




```
shpShape.Fill.ForeColor.RGB = RGB(Red:=255, Green:=0, Blue:=0) 
    Next shpShape 
End Sub
```

To add a  **Shape** object to the collection of shapes for the specified document and return a **Shape** object that represents the newly created shape, use one of the following methods of the **Shapes** collection: ** [AddCallout](bbf5f913-fcf0-b700-0c7e-9f0bdc7c6aea.md)** , ** [AddConnector](fd1ef969-7960-2555-e355-9804c86f6c01.md)** , ** [AddCurve](888a35cb-190d-4058-e0d7-a848d77ba920.md)** , ** [AddLabel](5a803aa2-d37f-6da1-7d8b-58ee2dcd8146.md)** , ** [AddLine](43df8878-5640-875f-06e0-37e1feb47b78.md)** , ** [AddOLEObject](c454f9cb-2005-5e55-80a7-6dfbe9c109e5.md)** , ** [AddPolyline](d49fb2bc-4df5-fff8-c741-2c0d35413fc5.md)** , ** [AddShape](500d8cb3-f066-fdb6-09ac-b03c7822e8bd.md)** , ** [AddTextBox](38494902-61d5-2017-819e-248b2b7bc0d1.md)** or ** [AddTextEffect](21af82f1-d507-3c16-72df-bde1b5e00717.md)** . The following example adds a rectangle to the active document.




```
Sub AddNewShape() 
    ActiveDocument.Pages(1).Shapes.AddShape Type:=msoShapeRectangle, _ 
        Left:=400, Top:=72, Width:=100, Height:=200 
End Sub
```

Use  ** [GroupItems](9194f43b-bd8a-76a9-aa8c-17544d052d47.md)** (index), where index is the shape name or the index number within the group, to return a **Shape** object that represents a single shape in a grouped shape. Use the ** [Group](ca3e011f-72ea-904e-da3f-cac7fe24341d.md)** or ** [Regroup](29342a78-9425-2356-963c-36a62a7f3091.md)** method to group a range of shapes and return a single **Shape** object that represents the newly formed group. After a group has been formed, you can work with the group the same way you work with any other shape. This example adds three shapes to the active publication, groups the shapes, and sets the fill color for each of the shapes in the group




```
Sub WorkWithGroupShapes() 
 
    With ActiveDocument.Pages(1).Shapes 
        .AddShape Type:=msoShapeIsoscelesTriangle, Left:=100, _ 
            Top:=72, Width:=100, Height:=100 
        .AddShape Type:=msoShapeIsoscelesTriangle, Left:=250, _ 
            Top:=72, Width:=100, Height:=100 
        .AddShape Type:=msoShapeIsoscelesTriangle, Left:=400, _ 
            Top:=72, Width:=100, Height:=100 
        .SelectAll 
 
        With Selection.ShapeRange 
            .Group 
            .GroupItems(1).Fill.ForeColor _ 
                .RGB = RGB(Red:=255, Green:=0, Blue:=0) 
            .GroupItems(2).Fill.ForeColor _ 
                .RGB = RGB(Red:=0, Green:=255, Blue:=0) 
            .GroupItems(3).Fill.ForeColor _ 
                .RGB = RGB(Red:=0, Green:=0, Blue:=255) 
        End With 
    End With 
End Sub
```

Use the  ** [Fill](ff1b8d02-150e-e023-2f0a-b1608cc99644.md)** property to return the ** [FillFormat](0a5d4f7a-c42a-28ad-c86d-ac9828a3b874.md)** object, which contains all the properties and methods for formatting the fill of a closed shape. The ** [Shadow](cfb908ae-ef1d-9539-1f82-2693cbe38d97.md)** property returns the ** [ShadowFormat](b23ab92e-5e49-8d8d-69d5-93d391a9edb2.md)** object, which you use to format a shadow. Use the ** [Line](3d53f917-87ad-159d-65c3-e6fdfa72b15e.md)** property to return a ** [LineFormat](9c973f5a-b2d2-78b1-24c3-350f1ba4c2ab.md)** object, which contains properties and methods for formatting lines and arrows. The ** [TextEffect](187b55f8-9593-6a00-61e6-dbcf5c56b987.md)** property returns the ** [TextEffectFormat](672d0ef0-cbcd-05ef-9aa5-b986c7b045ac.md)** object, which you use to format WordArt. The ** [Callout](e0682bb4-1129-fa58-b28c-46d7ce2fad0c.md)** property returns the ** [CalloutFormat](1f54aba3-3872-e668-fe76-1966d1a62cca.md)** object, which you use to format line callouts. The ** [TextWrap](e641d9a5-5b63-06d0-a0c3-d3feb1910159.md)** property returns the ** [WrapFormat](b6f80d40-2043-6944-3ed8-f26635c7fa4d.md)** object, which you use to define how text wraps around shapes. The ** [ThreeD](e3430bb2-2f2a-14a6-8eb4-98a29a96ad1c.md)** property returns the ** [ThreeDFormat](11d57330-c99e-5aa9-d47c-2c5d2846ed4d.md)** object, which you use to create 3-D shapes. You can use the ** [PickUp](12b59235-db2d-b451-de8e-9e8df6bfeb1c.md)** and ** [Apply](711c72b6-3618-be0b-fb72-9f68fdbcc4a8.md)** methods to transfer formatting from one shape to another.



Use the  ** [SetShapesDefaultProperties](3f7d7143-3a08-6ff4-c28e-86598212a876.md)** method for a **Shape** object to set the formatting for the default shape for the document. New shapes inherit many of their attributes from the default shape.

Use the  ** [Type](bb712dd4-5d81-10e0-9b4c-4af6a09a3c71.md)** property to specify the type of shape: freeform, AutoShape, OLE object, callout, or linked picture, for instance. Use the ** [AutoShapeType](f469dc31-a620-5561-ce57-fbff8a5536c0.md)** property to specify the type of AutoShape: oval, rectangle, or balloon, for instance.



Use the  ** [Width](0b7c5b57-1968-dabb-1e19-9f1d450cea7f.md)** and ** [Height](2796ae7e-f4b9-4d79-ff98-d5807286b41e.md)** properties to specify the size of the shape.



Use  ** [TextFrame](fc654905-d56b-9a6c-28fa-4b54bf2a8686.md)** and ** [TextRange](31aa92d1-852f-3742-defa-94485411bcc3.md)** properties to return the ** [TextFrame](95e88f5a-b3dc-272e-7c1d-5282c97ae11e.md)** and ** [TextRange](566f240b-d2a6-8cb3-9eb7-68328d6c28bd.md)** objects, respectively, which contain all the properties and methods for inserting and formatting text within shapes and publications and linking the text frames together. The following example adds a text box to the first page of the active publication, then adds text to it and formats the text.




```
Sub CreateNewTextBox() 
    With ActiveDocument.Pages(1).Shapes.AddTextbox( _ 
        Orientation:=pbTextOrientationHorizontal, Left:=100, _ 
        Top:=100, Width:=200, Height:=100).TextFrame.TextRange 
        .Text = "This is a textbox." 
        With .Font 
            .Name = "Stencil" 
            .Bold = msoTrue 
            .Size = 30 
        End With 
    End With 
End Sub
```

