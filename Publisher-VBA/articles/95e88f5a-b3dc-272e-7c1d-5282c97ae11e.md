
# TextFrame Object (Publisher)

 **Last modified:** July 28, 2015

Represents the text frame in a  ** [Shape](666cb7f0-62a8-f419-9838-007ef29506ee.md)** object. Contains the text in the text frame and the properties that control the margins and orientation of the text frame.

## Example

Use the  ** [TextFrame](fc654905-d56b-9a6c-28fa-4b54bf2a8686.md)** property to return the **TextFrame** object for a shape. The ** [TextRange](44a8395e-81dc-7d06-f068-89f77a889f5e.md)**property returns a  ** [TextRange](566f240b-d2a6-8cb3-9eb7-68328d6c28bd.md)** object that represents the range of text inside the specified text frame. The following example adds text to the text frame of shape one in the active publication, and then formats the new text.


```
Sub AddTextToTextFrame() 
 With ActiveDocument.Pages(1).Shapes(1).TextFrame.TextRange 
 .Text = "My Text" 
 With .Font 
 .Bold = msoTrue 
 .Size = 25 
 .Name = "Arial" 
 End With 
 End With 
End Sub
```


 **Note**  Some shapes do not support attached text (lines, freeforms, pictures, and OLE objects, for example). If you attempt to return or set properties that control text in a text frame for those objects, an error occurs.

Use the  ** [HasTextFrame](faf9514a-438b-ad12-a830-ed34cea8ba03.md)**property to determine whether the shape has a text frame and use the  ** [HasText](f8d1c660-c3f1-e835-adc3-114e6611de98.md)**property to determine whether the text frame contains text, as shown in the following example.




```
Sub GetTextFromTextFrame() 
 Dim shpText As Shape 
 
 For Each shpText In ActiveDocument.Pages(1).Shapes 
 If shpText.HasTextFrame = msoTrue Then 
 With shpText.TextFrame 
 If .HasText Then MsgBox .TextRange.Text 
 End With 
 End If 
 Next 
End Sub
```

Text frames can be linked together so that the text flows from the text frame of one shape into the text frame of another shape. Use the  ** [NextLinkedTextFrame](5ba08ab5-8515-4efe-59a3-79a11f6a7c4e.md)**and  ** [PreviousLinkedTextFrame](00947ec3-fcff-4451-491b-5b7748ccb74e.md)**properties to link text frames. The following example creates a text box (a rectangle with a text frame) and adds some text to it. It then creates another text box and links the two text frames together so that the text flows from the first text frame into the second one.




```
Sub LinkTextBoxes() 
 Dim shpTextBox1 As Shape 
 Dim shpTextBox2 As Shape 
 
 Set shpTextBox1 = ActiveDocument.Pages(1).Shapes.AddTextbox _ 
 (msoTextOrientationHorizontal, 72, 72, 72, 36) 
 shpTextBox1.TextFrame.TextRange.Text = _ 
 "This is some text. This is some more text." 
 
 Set shpTextBox2 = ActiveDocument.Pages(1).Shapes.AddTextbox _ 
 (msoTextOrientationHorizontal, 72, 144, 72, 36) 
 shpTextBox1.TextFrame.NextLinkedTextFrame = shpTextBox2 _ 
 .TextFrame 
End Sub
```

