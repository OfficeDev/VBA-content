
# TextRange Object (Publisher)

 **Last modified:** July 28, 2015

Contains the text that is attached to a shape, in addition to properties and methods for manipulating the text. This topic describes how to: 

- Return the text range in any shape you specify.
    
- Return a text range from the selection.
    
- Return particular characters, words, lines, sentences, or paragraphs from a text range.
    
- Insert text, the date and time, or the page number into a text range.
    

## Example

Use the  ** [TextRange](44a8395e-81dc-7d06-f068-89f77a889f5e.md)** property of the ** [TextFrame](95e88f5a-b3dc-272e-7c1d-5282c97ae11e.md)** object to return a **TextRange** object for any shape you specify. Use the ** [Text](13584812-307a-c32b-ca8f-27869728b64e.md)** property to return the string of text in the **TextRange** object. The following example adds a rectangle to the active publication and sets the text it contains.


```
Sub AddTextToShape() 
    With ActiveDocument.Pages(1).Shapes.AddShape(Type:=msoShapeRectangle, _ 
        Left:=72, Top:=72, Width:=250, Height:=140) 
        .TextFrame.TextRange.Text = "Here is some test text" 
    End With 
End Sub
```

Because the  **Text** property is the default property of the **TextRange** object, the following two statements are equivalent.




```
ActiveDocument.Pages(1).Shapes(1).TextFrame _ 
    .TextRange.text = "Here is some test text" 
ActiveDocument.Pages(1).Shapes(1).TextFrame _ 
    .TextRange = "Here is some test text"
```

Use the  ** [HasTextFrame](8a3b4f3b-3282-686b-f4fe-abf2d7677b3e.md)** property to determine whether a shape has a text frame, and use the ** [HasText](f8d1c660-c3f1-e835-adc3-114e6611de98.md)** property to determine whether the text frame contains text.

Use the  **TextRange** property of the **Selection** object to return the currently selected text. The following example copies the selection to the Clipboard.




```
Sub CopyAndPasteText() 
    With ActiveDocument 
        .Selection.TextRange.Copy 
        .Pages(1).Shapes(1).TextFrame.TextRange.Paste 
    End With 
End Sub
```

Use one of the following methods to return a portion of the text of a  **TextRange** object: ** [Characters](e851767e-12b2-ad77-071b-9d27bbf0d637.md)** , ** [Lines](56862090-b2ff-403b-d016-e37108d5ccc1.md)** , ** [Paragraphs](895c32cf-cdbe-74b0-ab47-6ae63d1bdea0.md)** , or ** [Words](df812db2-98ca-848b-7922-6905cb71124c.md)** . The following example formats the second word in the first shape on the first page of the active publication. For this example to work, the specified shape must contain text.




```
Sub FormatWords() 
    With ActiveDocument.Pages(1).Shapes(1).TextFrame _ 
            .TextRange.Words(2).Font 
        .Bold = msoTrue 
        .Size = 15 
        .Name = "Text Name" 
    End With 
End Sub
```

Use one of the following methods to insert characters into a  **TextRange** object: ** [InsertAfter](f647be29-68c7-b221-adf1-fa233583e74e.md)** , ** [InsertBefore](b0e4355b-b1bc-ae78-08ad-000d577fd7db.md)** , ** [InsertDateTime](1d02471a-f22b-7dad-bcbb-40af3a04d198.md)** , ** [InsertPageNumber](f71d3b40-0263-93fa-d7e3-d815b90f71f7.md)** , or ** [InsertSymbol](607d12da-5a2d-4e0e-b45e-92275ce97bab.md)** . This example inserts a new line with text after any existing text in the first shape on the first page of the active publication.




```
Sub InsertNewText() 
    Dim intCount As Integer 
    With ActiveDocument.Pages(1).Shapes(1).TextFrame _ 
            .TextRange 
        For intCount = 1 To 3 
            .InsertAfter vbLf &amp; "This is a test." 
        Next intCount 
    End With 
End Sub
```

