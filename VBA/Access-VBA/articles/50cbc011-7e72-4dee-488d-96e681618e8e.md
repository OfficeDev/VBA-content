
# Recordset.Clone Method (DAO)

 **Last modified:** June 29, 2011

 _ **Applies to:** Access 2013 | Access 2016_

 **In this article**
[Syntax](#sectionSection0)
[Remarks](#sectionSection1)
[Example](#sectionSection2)


Creates a duplicate  **[Recordset](9774232c-e6da-175b-fc7f-ed2ab7908fa0.md)** object that refers to the original **Recordset** object.

## Syntax
<a name="sectionSection0"> </a>

 _expression_. **Clone**

 _expression_ A variable that represents a **Recordset** object.


### Return Value

Recordset


## Remarks
<a name="sectionSection1"> </a>

Use the  **Clone** method to create multiple, duplicate **Recordset** objects. Each **Recordset** can have its own current record. Using **Clone** by itself doesn't change the data in the objects or in their underlying structures. When you use the **Clone** method, you can share bookmarks between two or more **Recordset** objects because their bookmarks are interchangeable.

You can use the  **Clone** method when you want to perform an operation on a **Recordset** that requires multiple current records. This is faster and more efficient than opening a second **Recordset**. When you create a **Recordset** with the **Clone** method, it initially lacks a current record. To make a record current before you use the **Recordset** clone, you must set the **[Bookmark](c4b1c2d9-668e-e365-544c-efb4ae4efcc9.md)** property or use one of the **[Move](338f7e86-6997-b80a-fc7a-a395d10b4a62.md)** methods, one of the **[Find](5fcf78cd-7d2c-2e47-14e5-996f2e14ff51.md)** methods, or the **[Seek](ef83d909-c962-b016-7d33-36eacdc25c2c.md)** method.

Using the  **[Close](9b1a77cb-da12-24d6-892f-a56be103d51d.md)** method on either the original or duplicate object doesn't affect the other object. For example, using **Close** on the original **Recordset** doesn't close the clone.




 **Note**  




## Example
<a name="sectionSection2"> </a>

This example uses the  **Clone** method to create copies of a **Recordset** and then lets the user position the record pointer of each copy independently.


```vb
Sub CloneX() 
 
   Dim dbsNorthwind As Database 
   Dim arstProducts(1 To 3) As Recordset 
   Dim intLoop As Integer 
   Dim strMessage As String 
   Dim strFind As String 
 
   Set dbsNorthwind = OpenDatabase("Northwind.mdb") 
 
   ' If the following SQL statement will be used often,  
   ' creating a permanent QueryDef will result in better 
   ' performance. 
   Set arstProducts(1) = dbsNorthwind.OpenRecordset( _ 
      "SELECT ProductName FROM Products " &; _ 
      "ORDER BY ProductName", dbOpenSnapshot) 
 
   ' Create two clones of the original Recordset. 
   Set arstProducts(2) = arstProducts(1).Clone 
   Set arstProducts(3) = arstProducts(1).Clone 
 
   Do While True 
 
      ' Loop through the array so that on each pass, the  
      ' user is searching a different copy of the same  
      ' Recordset. 
      For intLoop = 1 To 3 
 
         ' Ask for search string while showing where the 
         ' current record pointer is for each Recordset. 
         strMessage = _ 
            "Recordsets from Products table:" &; vbCr &; _ 
            "  1 - Original - Record pointer at " &; _ 
            arstProducts(1)!ProductName &; vbCr &; _ 
            "  2 - Clone - Record pointer at " &; _ 
            arstProducts(2)!ProductName &; vbCr &; _ 
            "  3 - Clone - Record pointer at " &; _ 
            arstProducts(3)!ProductName &; vbCr &; _ 
            "Enter search string for #" &; intLoop &; ":" 
         strFind = Trim(InputBox(strMessage)) 
         If strFind = "" Then Exit Do 
 
         ' Find the search string; if there's no match, jump 
         ' to the last record. 
         With arstProducts(intLoop) 
            .FindFirst "ProductName >= '" &; strFind &; "'" 
            If .NoMatch Then .MoveLast 
         End With 
 
      Next intLoop 
 
   Loop 
 
   arstProducts(1).Close 
   arstProducts(2).Close 
   arstProducts(3).Close 
   dbsNorthwind.Close 
 
End Sub
```

