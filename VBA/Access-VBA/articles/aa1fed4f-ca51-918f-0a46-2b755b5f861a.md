
# Recordset.RecordCount Property (DAO)

 **Last modified:** June 29, 2011

 _ **Applies to:** Access 2013 | Access 2016_

 **In this article**
[Syntax](#sectionSection0)
[Remarks](#sectionSection1)
[Example](#sectionSection2)


Returns the number of records accessed in a  **[Recordset](9774232c-e6da-175b-fc7f-ed2ab7908fa0.md)** object, or the total number of records in a table-type **Recordset** object. or **[TableDef](715146b6-c62a-abff-28ee-e6bbe3c08adf.md)** object. Read-only **Long**.

## Syntax
<a name="sectionSection0"> </a>

 _expression_. **RecordCount**

 _expression_ A variable that represents a **Recordset** object.


## Remarks
<a name="sectionSection1"> </a>

Use the  **RecordCount** property to find out how many records in a **Recordset** or **TableDef** object have been accessed. The **RecordCount** property doesn't indicate how many records are contained in a dynaset-, snapshot-, or forward-only-type **Recordset** object until all records have been accessed. Once the last record has been accessed, the **RecordCount** property indicates the total number of undeleted records in the **Recordset** or **TableDef** object. To force the last record to be accessed, use the **[MoveLast](fc0f7a33-1f55-9f5b-b00d-1b81f49b1c3e.md)** method on the **Recordset** object. You can also use an SQL **Count** function to determine the approximate number of records your query will return.




 **Note**  Using the  **MoveLast** method to populate a newly opened **Recordset** negatively impacts performance. Unless it is necessary to have an accurate **RecordCount** as soon as you open a **Recordset**, it's better to wait until you populate the **Recordset** with other portions of code before checking the **RecordCount** property.

As your application deletes records in a dynaset-type  **Recordset** object, the value of the **RecordCount** property decreases. However, records deleted by other users aren't reflected by the **RecordCount** property until the current record is positioned to a deleted record. If you execute a transaction that affects the **RecordCount** property setting and you subsequently roll back the transaction, the **RecordCount** property won't reflect the actual number of remaining records.

The  **RecordCount** property of a snapshot- or forward-only-type **Recordset** object isn't affected by changes in the underlying tables.

A  **Recordset** or **TableDef** object with no records has a **RecordCount** property setting of 0.

Using the  **[Requery](a5d66eb5-499c-4133-f6c3-c7a1619a8a11.md)** method on a **Recordset** object resets the **RecordCount** property just as if the query were re-executed.


## Example
<a name="sectionSection2"> </a>

This example demonstrates the  **RecordCount** property with different types of **Recordsets** before and after they're populated.


```vb
Sub RecordCountX() 
 
 Dim dbsNorthwind As Database 
 Dim rstEmployees As Recordset 
 
 Set dbsNorthwind = OpenDatabase("Northwind.mdb") 
 
 With dbsNorthwind 
 ' Open table-type Recordset and show RecordCount 
 ' property. 
 Set rstEmployees = .OpenRecordset("Employees") 
 Debug.Print _ 
 "Table-type recordset from Employees table" 
 Debug.Print " RecordCount = " &; _ 
 rstEmployees.RecordCount 
 rstEmployees.Close 
 
 ' Open dynaset-type Recordset and show RecordCount 
 ' property before populating the Recordset. 
 Set rstEmployees = .OpenRecordset("Employees", _ 
 dbOpenDynaset) 
 Debug.Print "Dynaset-type recordset " &; _ 
 "from Employees table before MoveLast" 
 Debug.Print " RecordCount = " &; _ 
 rstEmployees.RecordCount 
 
 ' Show the RecordCount property after populating the 
 ' Recordset. 
 rstEmployees.MoveLast 
 Debug.Print "Dynaset-type recordset " &; _ 
 "from Employees table after MoveLast" 
 Debug.Print " RecordCount = " &; _ 
 rstEmployees.RecordCount 
 rstEmployees.Close 
 
 ' Open snapshot-type Recordset and show RecordCount 
 ' property before populating the Recordset. 
 Set rstEmployees = .OpenRecordset("Employees", _ 
 dbOpenSnapshot) 
 Debug.Print "Snapshot-type recordset " &; _ 
 "from Employees table before MoveLast" 
 Debug.Print " RecordCount = " &; _ 
 rstEmployees.RecordCount 
 
 ' Show the RecordCount property after populating the 
 ' Recordset. 
 rstEmployees.MoveLast 
 Debug.Print "Snapshot-type recordset " &; _ 
 "from Employees table after MoveLast" 
 Debug.Print " RecordCount = " &; _ 
 rstEmployees.RecordCount 
 rstEmployees.Close 
 
 ' Open forward-only-type Recordset and show 
 ' RecordCount property before populating the 
 ' Recordset. 
 Set rstEmployees = .OpenRecordset("Employees", _ 
 dbOpenForwardOnly) 
 Debug.Print "Forward-only-type recordset " &; _ 
 "from Employees table before MoveLast" 
 Debug.Print " RecordCount = " &; _ 
 rstEmployees.RecordCount 
 
 ' Show the RecordCount property after calling the 
 ' MoveNext method. 
 rstEmployees.MoveNext 
 Debug.Print "Forward-only-type recordset " &; _ 
 "from Employees table after MoveNext" 
 Debug.Print " RecordCount = " &; _ 
 rstEmployees.RecordCount 
 rstEmployees.Close 
 
 .Close 
 End With 
 
End Sub
```

