
# Fields.Delete Method (DAO)

 **Last modified:** March 09, 2015

 _ **Applies to:** Access 2013 | Access 2016_

 **In this article**
[Syntax](#sectionSection0)
[Remarks](#sectionSection1)
[Example](#sectionSection2)


Deletes a  **[Field](47282ce2-9b49-ccf9-ad37-c4bb25cfd037.md)** from the **[Fields](4be3ba07-20c1-d958-c1b8-7dd8b4731f60.md)** collection.

## Syntax
<a name="sectionSection0"> </a>

 _expression_. **Delete**( ** _Name_** )

 _expression_ A variable that represents a **Fields** object.


### Parameters



|**Name**|**Required/Optional**|**Data Type**|**Description**|
|:-----|:-----|:-----|:-----|
| _Name_|Required|**String**|The field to delete.|

## Remarks
<a name="sectionSection1"> </a>

The deletion of a stored object occurs immediately, but you should use the  **Refresh** method on any other collections that may be affected by changes to the database structure.


## Example
<a name="sectionSection2"> </a>

This example uses either the  **Append** method or the **Delete** method to modify the **Fields** collection of a **TableDef**. The AppendDeleteField procedure is required for this procedure to run.


```vb
Sub AppendX() 
 
 Dim dbsNorthwind As Database 
 Dim tdfEmployees As TableDef 
 Dim fldLoop As Field 
 
 Set dbsNorthwind = OpenDatabase("Northwind.mdb") 
 Set tdfEmployees = dbsNorthwind.TableDefs!Employees 
 
 ' Add three new fields. 
 AppendDeleteField tdfEmployees, "APPEND", _ 
 "E-mail", dbText, 50 
 AppendDeleteField tdfEmployees, "APPEND", _ 
 "Http", dbText, 80 
 AppendDeleteField tdfEmployees, "APPEND", _ 
 "Quota", dbInteger, 5 
 
 Debug.Print "Fields after Append" 
 Debug.Print , "Type", "Size", "Name" 
 
 ' Enumerate the Fields collection to show the new fields. 
 For Each fldLoop In tdfEmployees.Fields 
 Debug.Print , fldLoop.Type, fldLoop.Size, fldLoop.Name 
 Next fldLoop 
 
 ' Delete the newly added fields. 
 AppendDeleteField tdfEmployees, "DELETE", "E-mail" 
 AppendDeleteField tdfEmployees, "DELETE", "Http" 
 AppendDeleteField tdfEmployees, "DELETE", "Quota" 
 
 Debug.Print "Fields after Delete" 
 Debug.Print , "Type", "Size", "Name" 
 
 ' Enumerate the Fields collection to show that the new 
 ' fields have been deleted. 
 For Each fldLoop In tdfEmployees.Fields 
 Debug.Print , fldLoop.Type, fldLoop.Size, fldLoop.Name 
 Next fldLoop 
 
 dbsNorthwind.Close 
 
End Sub 
 
Sub AppendDeleteField(tdfTemp As TableDef, _ 
 strCommand As String, strName As String, _ 
 Optional varType, Optional varSize) 
 
 With tdfTemp 
 
 ' Check first to see if the TableDef object is 
 ' updatable. If it isn't, control is passed back to 
 ' the calling procedure. 
 If .Updatable = False Then 
 MsgBox "TableDef not Updatable! " &; _ 
 "Unable to complete task." 
 Exit Sub 
 End If 
 
 ' Depending on the passed data, append or delete a 
 ' field to the Fields collection of the specified 
 ' TableDef object. 
 If strCommand = "APPEND" Then 
 .Fields.Append .CreateField(strName, _ 
 varType, varSize) 
 Else 
 If strCommand = "DELETE" Then .Fields.Delete _ 
 strName 
 End If 
 
 End With 
 
End Sub
```

