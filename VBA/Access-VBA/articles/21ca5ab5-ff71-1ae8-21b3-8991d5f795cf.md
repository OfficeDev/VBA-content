
# Recordset.Move Method (DAO)

 **Last modified:** March 09, 2015

 _ **Applies to:** Access 2013 | Access 2016_

 **In this article**
[Syntax](#sectionSection0)
[Remarks](#sectionSection1)
[Example](#sectionSection2)


Moves the position of the current record in a  **[Recordset](9774232c-e6da-175b-fc7f-ed2ab7908fa0.md)** object.

## Syntax
<a name="sectionSection0"> </a>

 _expression_. **Move**( ** _Rows_**, ** _StartBookmark_** )

 _expression_ A variable that represents a **Recordset** object.


### Parameters



|**Name**|**Required/Optional**|**Data Type**|**Description**|
|:-----|:-----|:-----|:-----|
| _Rows_|Required|**Long**|The number of rows the position will move. If rows is greater than 0, the position is moved forward (toward the end of the file). If rows is less than 0, the position is moved backward (toward the beginning of the file).|
| _StartBookmark_|Optional|**Variant**|A value identifying a bookmark. If you specify startbookmark, the move begins relative to this bookmark. Otherwise, Move begins from the current record.|

## Remarks
<a name="sectionSection1"> </a>

If you use  **Move** to position the current record pointer before the first record, the current record pointer moves to the beginning of the file. If the **Recordset** contains no records and its **[BOF](c50a0c5f-1b26-33ea-4cf2-311f9514a94a.md)** property is **True**, using this method to move backward causes an error.

If you use  **Move** to position the current record pointer after the last record, the current record pointer position moves to the end of the file. If the **Recordset** contains no records and its **[EOF](aa82c6f9-89da-1061-437c-8ffb000744b6.md)** property is **True**, then using this method to move forward causes an error.

If either the  **BOF** or **EOF** property is **True** and you attempt to use the **Move** method without a valid bookmark, a run-time error occurs.


 **Note**  


## Example
<a name="sectionSection2"> </a>

This example uses the  **Move** method to position the record pointer based on user input.


```vb
Sub MoveX() 
 
   Dim dbsNorthwind As Database 
   Dim rstSuppliers As Recordset 
   Dim varBookmark As Variant 
   Dim strCommand As String 
   Dim lngMove As Long 
 
   Set dbsNorthwind = OpenDatabase("Northwind.mdb") 
   Set rstSuppliers = _ 
      dbsNorthwind.OpenRecordset("SELECT CompanyName, " &; _ 
      "City, Country FROM Suppliers ORDER BY CompanyName", _ 
      dbOpenDynaset) 
 
   With rstSuppliers 
      ' Populate recordset. 
      .MoveLast 
      .MoveFirst 
 
      Do While True 
         ' Display information about current record and ask  
         ' how many records to move. 
         strCommand = InputBox( _ 
            "Record " &; (.AbsolutePosition + 1) &; " of " &; _ 
            .RecordCount &; vbCr &; "Company: " &; _ 
            !CompanyName &; vbCr &; "Location: " &; !City &; _ 
            ", " &; !Country &; vbCr &; vbCr &; _ 
            "Enter number of records to Move " &; _ 
            "(positive or negative).") 
 
         If strCommand = "" Then Exit Do 
 
         ' Store bookmark in case the Move doesn't work. 
         varBookmark = .Bookmark 
 
         ' Move method requires parameter of data type Long. 
         lngMove = CLng(strCommand) 
         .Move lngMove 
 
         ' Trap for BOF or EOF. 
         If .BOF Then 
            MsgBox "Too far backward! " &; _ 
               "Returning to current record." 
            .Bookmark = varBookmark 
         End If 
         If .EOF Then 
            MsgBox "Too far forward! " &; _ 
               "Returning to current record." 
            .Bookmark = varBookmark 
         End If 
      Loop 
      .Close 
   End With 
 
   dbsNorthwind.Close 
 
End Sub
```

