
# TableDef.CreateField Method (DAO)

 **Last modified:** March 09, 2015

 _ **Applies to:** Access 2013 | Access 2016_

 **In this article**
[Syntax](#sectionSection0)
[Remarks](#sectionSection1)
[Example](#sectionSection2)
[About the Contributors](#AboutContributors)


Creates a new  **[Field](47282ce2-9b49-ccf9-ad37-c4bb25cfd037.md)** object (Microsoft Access workspaces only).

## Syntax
<a name="sectionSection0"> </a>

 _expression_. **CreateField**( ** _Name_**, ** _Type_**, ** _Size_** )

 _expression_ A variable that represents a **TableDef** object.


### Parameters



|**Name**|**Required/Optional**|**Data Type**|**Description**|
|:-----|:-----|:-----|:-----|
| _Name_|Optional|**Variant**|A String that uniquely names the new  **Field** object. See the **[Name](5f4a95cd-63a3-aedf-df64-793158b2283d.md)** property for details on valid **Field** names.|
| _Type_|Optional|**Variant**| A constant that determines the data type of the new **Field** object. See the **[Type](1295ca40-78c1-bdd0-d407-e1b5be8adfd4.md)** property for valid data types.|
| _Size_|Optional|**Variant**|An  **Integer** that indicates the maximum size, in bytes, of a **Field** object that contains text. See the **[Size](15e25201-87b6-f62f-ff18-259414a47891.md)** property for valid _size_ values. This argument is ignored for numeric and fixed-width fields.|

### Return Value

Field


## Remarks
<a name="sectionSection1"> </a>

You can use the  **CreateField** method to create a new field, as well as specify the name, data type, and size of the field. If you omit one or more of the optional parts when you use **CreateField**, you can use an appropriate assignment statement to set or reset the corresponding property before you append the new object to a collection. After you append the new object, you can alter some but not all of its property settings. See the individual property topics for more details.

The  _type_ and _Size_ arguments apply only to **Field** objects in a **TableDef** object. These arguments are ignored when a **Field** object is associated with an **Index** or **Relation** object.

If  _Name_ refers to an object that is already a member of the collection, a run-time error occurs when you use the **[Append](a0e553ba-6a57-09af-3436-4f6ca3cbe561.md)** method.

To remove a  **Field** object from a **Fields** collection, use the **[Delete](a8e249e7-7526-3eff-a5cf-70cab2081970.md)** method on the collection. You can't delete a **Field** object from a **TableDef** object's **Fields** collection after you create an index that references the field.

 **Link provided by:**
![Community Member Icon](images/8b9774c4-6c97-470e-b3a2-56d8f786444c.png) The[UtterAccess](http://www.utteraccess.com) community |[About the Contributors](#AboutContributors)


- [Adding a hyperlink field to an existing table with DAO](http://www.utteraccess.com/wiki/index.php/Adding_a_hyperlink_field_to_an_existing_table_with_DAO)
    

## Example
<a name="sectionSection2"> </a>

The following example shows how to create a calculated field. The  **CreateField** method creates a field named **FullName**. The  **Expression** property is then set to the expression that calculates the value of the field.

 **Sample code provided by:** The[Microsoft Access 2010 Programmer's Reference](http://www.wrox.com/WileyCDA/WroxTitle/Access-2010-Programmer-s-Reference.productCd-0470591668.mdl) |[About the Contributors](#AboutContributors)




```vb
Sub CreateCalculatedField()
    Dim dbs As DAO.Database
    Dim tdf As DAO.TableDef
    Dim fld As DAO.Field2
    
    ' get the database
    Set dbs = CurrentDb()
    
    ' create the table
    Set tdf = dbs.CreateTableDef("tblContactsCalcField")
    
    ' create the fields: first name, last name
    tdf.Fields.Append tdf.CreateField("FirstName", dbText, 20)
    tdf.Fields.Append tdf.CreateField("LastName", dbText, 20)
    
    ' create the calculated field: full name
    Set fld = tdf.CreateField("FullName", dbText, 50)
    fld.Expression = "[FirstName] &; "" "" &; [LastName]"
    tdf.Fields.Append fld
    
    ' append the table and cleanup
    dbs.TableDefs.Append tdf
    
Cleanup:
    Set fld = Nothing
    Set tdf = Nothing
    Set dbs = Nothing
End Sub
```


## About the Contributors
<a name="AboutContributors"> </a>

UtterAccess is the premier Microsoft Access wiki and help forum. Click here to join. 

Wrox Press is driven by the Programmer to Programmer philosophy. Wrox books are written by programmers for programmers, and the Wrox brand means authoritative solutions to real-world programming problems. 

