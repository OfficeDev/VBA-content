
# Index.Unique Property (DAO)

 **Last modified:** June 30, 2011

 _ **Applies to:** Access 2013 | Access 2016_

 **In this article**
[Syntax](#sectionSection0)
[Remarks](#sectionSection1)
[Example](#sectionSection2)


Sets or returns a value that indicates whether an  **[Index](92c32cad-ec8a-1243-1d18-83f50b269ecb.md)** object represents a unique (key) index for a table (Microsoft Access workspaces only).

## Syntax
<a name="sectionSection0"> </a>

 _expression_. **Unique**

 _expression_ A variable that represents an **Index** object.


## Remarks
<a name="sectionSection1"> </a>

This property setting is read/write until the object is appended to a collection, after which it's read-only.

A unique index consists of one or more fields that logically arrange all records in a table in a unique, predefined order. If the index consists of one field, values in that field must be unique for the entire table. If the index consists of more than one field, each field can contain duplicate values, but each combination of values from all the indexed fields must be unique.

If both the  **Unique** and **[Primary](90eda1cb-cf7f-9682-9b74-81c27a37af16.md)** properties of an **Index** object are set to **True**, the index is unique and primary: It uniquely identifies all records in the table in a predefined, logical order. If the **Primary** property is set to **False**, the index is a secondary index. Secondary indexes (both key and nonkey) logically arrange records in a predefined order without serving as an identifier for records in the table.


 **Note**  




## Example
<a name="sectionSection2"> </a>

This example sets the  **Unique** property of a new **Index** object to **True**, and appends the Index to the **Indexes** collection of the Employees table. It then enumerates the **Indexes** collection of the **TableDef** and the **Properties** collection of each **Index**. The new **Index** will only allow one record with a particular combination of Country, LastName, and FirstName in the **TableDef**.


```vb
Sub UniqueX() 
 
   Dim dbsNorthwind As Database 
   Dim tdfEmployees As TableDef 
   Dim idxNew As Index 
   Dim idxLoop As Index 
   Dim prpLoop As Property 
 
   Set dbsNorthwind = OpenDatabase("Northwind.mdb") 
   Set tdfEmployees = dbsNorthwind!Employees 
 
   With tdfEmployees 
      ' Create and append new Index object to the Indexes  
      ' collection of the Employees table. 
      Set idxNew = .CreateIndex("NewIndex") 
 
      With idxNew 
         .Fields.Append .CreateField("Country") 
         .Fields.Append .CreateField("LastName") 
         .Fields.Append .CreateField("FirstName") 
         .Unique = True 
      End With 
 
      .Indexes.Append idxNew 
      .Indexes.Refresh 
 
      Debug.Print .Indexes.Count &; " Indexes in " &; _ 
         .Name &; " TableDef" 
 
      ' Enumerate Indexes collection of Employees table. 
      For Each idxLoop In .Indexes 
         Debug.Print "  " &; idxLoop.Name 
 
         ' Enumerate Properties collection of each Index  
         ' object. 
         For Each prpLoop In idxLoop.Properties 
            Debug.Print "    " &; prpLoop.Name &; _ 
               " = " &; IIf(prpLoop = "", "[empty]", prpLoop) 
         Next prpLoop 
 
      Next idxLoop 
 
      ' Delete new Index because this is a demonstration. 
      .Indexes.Delete idxNew.Name 
   End With 
 
   dbsNorthwind.Close 
 
End Sub
```

