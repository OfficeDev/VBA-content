
# Worksheet.Cells Property (Excel)

 **Last modified:** July 28, 2015

 **In this article**
 [Syntax](#sectionSection0)
 [Remarks](#sectionSection1)
 [Example](#sectionSection2)
 [About the Contributor](#AboutContributor)


Returns a  ** [Range](b8207778-0dcc-4570-1234-f130532cc8cd.md)** object that represents all the cells on the worksheet (not just the cells that are currently in use).


## Syntax
<a name="sectionSection0"> </a>

 _expression_. **Cells**

 _expression_A variable that represents a  **Worksheet** object.


## Remarks
<a name="sectionSection1"> </a>

Because the  ** [Item](f7d40273-5069-8a9d-14ee-19df225f864c.md)** property is the default property for the **Range** object, you can specify the row and column index immediately after the **Cells** keyword. For more information, see the **Item** property and the examples for this topic.

Using this property without an object qualifier returns a  **Range** object that represents all the cells on the active worksheet.


## Example
<a name="sectionSection2"> </a>

This example sets the font size for cell C5 on Sheet1 to 14 points.


```
Worksheets("Sheet1").Cells(5, 3).Font.Size = 14
```

This example clears the formula in cell one on Sheet1.




```
Worksheets("Sheet1").Cells(1).ClearContents
```

This example sets the font and font size for every cell on Sheet1 to 8-point Arial




```
With Worksheets("Sheet1").Cells.Font 
    .Name = "Arial" 
    .Size = 8 
End With
```

 **Sample code provided by:** Tom Urtis, [Atlas Programming Management](http://www.atlaspm.com/)

This example toggles a sort between ascending and descending order when you double-click any cell in the data range. The data is sorted based on the column of the cell that is double-clicked.




```
Option Explicit
Public blnToggle As Boolean

Private Sub Worksheet_BeforeDoubleClick(ByVal Target As Range, Cancel As Boolean)
    Dim LastColumn As Long, keyColumn As Long, LastRow As Long
    Dim SortRange As Range
    LastColumn = Cells.Find(What:="*", After:=Range("A1"), SearchOrder:=xlByColumns, SearchDirection:=xlPrevious).Column
    
    keyColumn = Target.Column
    
    If keyColumn <= LastColumn Then
    
        Application.ScreenUpdating = False
        Cancel = True
        LastRow = Cells(Rows.Count, keyColumn).End(xlUp).Row
        Set SortRange = Target.CurrentRegion
        
        blnToggle = Not blnToggle
        If blnToggle = True Then
            SortRange.Sort Key1:=Cells(2, keyColumn), Order1:=xlAscending, Header:=xlYes
        Else
            SortRange.Sort Key1:=Cells(2, keyColumn), Order1:=xlDescending, Header:=xlYes
        End If
    
        Set SortRange = Nothing
        Application.ScreenUpdating = True
        
    End If
End Sub
```


## About the Contributor
<a name="AboutContributor"> </a>

MVP Tom Urtis is the founder of Atlas Programming Management, a full-service Microsoft Office and Excel business solutions company in Silicon Valley. Tom has over 25 years of experience in business management and developing Microsoft Office applications, and is the co-author of "Holy Macro! It's 2,500 Excel VBA Examples." 


## See also
<a name="AboutContributor"> </a>


#### Concepts


 [Worksheet Object](182b705e-854a-81cc-a4b0-59b942de55ae.md)
#### Other resources


 [Worksheet Object Members](f8c1afea-1a1c-f5e4-37e3-52c434c8c157.md)
