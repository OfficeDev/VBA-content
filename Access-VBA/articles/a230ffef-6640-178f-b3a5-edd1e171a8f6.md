
# Module.Lines Property (Access)

 **Last modified:** July 28, 2015

 **In this article**
 [Syntax](#sectionSection0)
 [Remarks](#sectionSection1)
 [Example](#sectionSection2)


The  **Lines** property returns a string containing the contents of a specified line or lines in a standard module or a class module. Read-only **String**.


## Syntax
<a name="sectionSection0"> </a>

 _expression_. **Lines**( **_Line_**,  **_NumLines_**)

 _expression_A variable that represents a  **Module** object.


### Parameters



|**Name**|**Required/Optional**|**Data Type**|**Description**|
|:-----|:-----|:-----|:-----|
|Line|Required| **Long**|The number of the first line to return.|
|NumLines|Required| **Long**|The number of lines to return.|

## Remarks
<a name="sectionSection1"> </a>

Lines in a module are numbered beginning with 1. For example, if you read the  **Lines** property with a value of 1 for theline argument and 1 for thenumlines argument, the **Lines** property returns a string containing the text of the first line in the module.

To insert a line of text into a module, use the  ** [InsertLines](54ea5ce3-fb2a-e9c7-85ef-8861141f63ec.md)**method.


## Example
<a name="sectionSection2"> </a>

The following example deletes a specified line from a module.


```
Function DeleteWholeLine(strModuleName, strText As String) _ 
 As Boolean 
 Dim mdl As Module, lngNumLines As Long 
 Dim lngSLine As Long, lngSCol As Long 
 Dim lngELine As Long, lngECol As Long 
 Dim strTemp As String 
 
 On Error GoTo Error_DeleteWholeLine 
 DoCmd.OpenModule strModuleName 
 Set mdl = Modules(strModuleName) 
 
 If mdl.Find(strText, lngSLine, lngSCol, lngELine, lngECol) Then 
 lngNumLines = Abs(lngELine - lngSLine) + 1 
 strTemp = LTrim$(mdl.Lines(lngSLine, lngNumLines)) 
 strTemp = RTrim$(strTemp) 
 If strTemp = strText Then 
 mdl.DeleteLines lngSLine, lngNumLines 
 Else 
 MsgBox "Line contains text in addition to '" _ 
 &amp; strText &amp; "'." 
 End If 
 Else 
 MsgBox "Text '" &amp; strText &amp; "' not found." 
 End If 
 DeleteWholeLine = True 
 
Exit_DeleteWholeLine: 
 Exit Function 
 
Error_DeleteWholeLine: 
 MsgBox Err &amp; " :" &amp; Err.Description 
 DeleteWholeLine = False 
 Resume Exit_DeleteWholeLine 
End Function 

```

You could call this function from a procedure such as the following, which searches the module Module1 for a constant declaration and deletes it.




```
Sub DeletePiConst() 
 If DeleteWholeLine("Module1", "Const conPi = 3.14") Then 
 Debug.Print "Constant declaration deleted successfully." 
 Else 
 Debug.Print "Constant declaration not deleted." 
 End If 
End Sub 

```


## See also
<a name="sectionSection2"> </a>


#### Concepts


 [Module Object](e04272fa-9c29-2567-bd15-1cea38906894.md)
#### Other resources


 [Module Object Members](c2e71012-645e-b818-1247-9775f221619e.md)
