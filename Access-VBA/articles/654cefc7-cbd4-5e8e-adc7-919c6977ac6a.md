
# Allow Users to Add Items to an Unbound Combo Box

 **Last modified:** July 28, 2015

 _**Applies to:** Access 2013_

The following example uses the  **NotInList** event to add an item to a combo box.

To try this example, create a combo box called  **Colors** on a form. Set the combo box's ** [LimitToList](885ED814-6E04-B9F1-0ACB-3DED28E00F93.md)** property to **Yes**. To populate the combo box, set the combo box's  ** [RowSourceType](DD1D6EA8-5479-4BF9-3317-0B95282C7D74.md)** property to **Value List**, and supply a list of values separated by semicolons as the setting for the  ** [RowSource](1225E566-24E0-244D-09AE-E036C87F3141.md)** property. For example, you might supply the following values as the setting for this property: Red; Green; Blue.
Next, add the following event procedure to the form. Switch to Form view and enter a new value in the text portion of the combo box. 



```
Private Sub Colors_NotInList(NewData As String, _ 
        Response As Integer) 
    Dim ctl As Control 
     
    ' Return Control object that points to combo box. 
    Set ctl = Me!Colors 
    ' Prompt user to verify they want to add new value. 
    If MsgBox("Value is not in list. Add it?", _ 
         vbOKCancel) = vbOK Then 
        ' Set Response argument to indicate that data 
        ' is being added. 
        Response = acDataErrAdded 
        ' Add string in NewData argument to row source. 
        ctl.RowSource = ctl.RowSource &amp; ";" &amp; NewData 
    Else 
    ' If user chooses Cancel, suppress error message 
    ' and undo changes. 
        Response = acDataErrContinue 
        ctl.Undo 
    End If 
End Sub 

```


 **Note**  This example adds an item to an unbound combo box. When you add an item to a bound combo box, you add a value to a field in the underlying data source. In most cases you cannot simply add one field in a new record. Depending on the structure of data in the table, you probably will need to add one or more fields to fulfill data requirements. For instance, a new record must include values for any fields comprising the primary key. If you need to add items to a bound combo box dynamically, you must prompt the user to enter data for all required fields, save the new record, and then requery the combo box to display the new value. 

