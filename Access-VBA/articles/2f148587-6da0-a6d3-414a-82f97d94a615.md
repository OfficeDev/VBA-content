
# Report.ScaleTop Property (Access)

 **Last modified:** July 28, 2015

 **In this article**
 [Syntax](#sectionSection0)
 [Remarks](#sectionSection1)
 [Example](#sectionSection2)


You can use the  **ScaleTop** property to specify the units for the vertical coordinates that describe the location of the top edge of a page when the ** [Circle](4f5d24e2-75bf-3586-7e0d-0902adee61a6.md)**,  ** [Line](9e640e37-c055-3dc3-b70e-0805cdc13561.md)**,  ** [Pset](951a262b-b17b-9b95-b5f2-922d4aff9ce9.md)**, or  ** [Print](6f8523cc-7b17-ec27-e2c9-a7ae3d5a8c3f.md)**method is used while a report is previewed, printed, or its output is saved to a file. Read / write  **Single**.


## Syntax
<a name="sectionSection0"> </a>

 _expression_. **ScaleTop**

 _expression_A variable that represents a  **Report** object.


## Remarks
<a name="sectionSection1"> </a>

You can set the  **ScaleTop** property by using a macro or a [Visual Basic](3fa3677b-a779-3bc7-0f0f-827c252b3292.md)event procedure specified by a section's **OnPrint**property setting.

By using these properties and the related  **ScaleHeight** and **ScaleWidth** properties, you can set up a custom coordinate system with both positive and negative coordinates. All four of these Scale properties interact with the ** [ScaleMode](e3955e48-80bb-989e-2992-cd5a541b468b.md)**property in the following ways:


- Setting any other Scale property to any value automatically sets the  **ScaleMode** property to 0.
    
- Setting the  **ScaleMode** property to a number greater than 0 changes the **ScaleHeight** and **ScaleWidth** property settings to the new unit of measurement and sets the **ScaleLeft** and **ScaleTop** properties to 0. Also, the ** [CurrentX](3b5e7c50-ecb4-606a-6715-4d54ed912c45.md)**and  ** [CurrentY](040c0b5d-f7d6-2fa1-e34d-f69799f0b273.md)** property settings change to reflect the new coordinates of the current point.
    
You can also use the  **Scale**method to set the  **ScaleHeight**,  **ScaleWidth**,  **ScaleLeft**, and  **ScaleTop** properties in one statement.


 **Note**  The  **ScaleTop** property isn't the same as the **Top** property.


## Example
<a name="sectionSection2"> </a>

The following example uses the  **Circle** method to draw a circle and create a pie slice within the circle. Then it uses the **FillColor** and **FillStyle** properties to color the pie slice red. It also draws a line from the upper left to the center of the circle.

To try this example in Microsoft Access, create a new report. Set the  **OnPrint** property of the Detail section to [Event Procedure]. Enter the following code in the report's module, then switch to Print Preview.




```
Private Sub Detail_Print(Cancel As Integer, PrintCount As Integer) 
 
 Const conPI = 3.14159265359 
 
 Dim sngHCtr As Single 
 Dim sngVCtr As Single 
 Dim sngRadius As Single 
 Dim sngStart As Single 
 Dim sngEnd As Single 
 
 sngHCtr = Me.ScaleWidth / 2 ' Horizontal center. 
 sngVCtr = Me.ScaleHeight / 2 ' Vertical center. 
 sngRadius = Me.ScaleHeight / 3 ' Circle radius. 
 Me.Circle (sngHCtr, sngVCtr), sngRadius ' Draw circle. 
 sngStart = -0.00000001 ' Start of pie slice. 
 
 sngEnd = -2 * conPI / 3 ' End of pie slice. 
 Me.FillColor = RGB(255, 0, 0) ' Color pie slice red. 
 Me.FillStyle = 0 ' Fill pie slice. 
 
 ' Draw Pie slice within circle 
 Me.Circle (sngHCtr, sngVCtr), sngRadius, , sngStart, sngEnd 
 
 ' Draw line to center of circle. 
 Dim intColor As Integer 
 Dim sngTop As Single, sngLeft As Single 
 Dim sngWidth As Single, sngHeight As Single 
 
 Me.ScaleMode = 3 ' Set scale to pixels. 
 sngTop = Me.ScaleTop ' Top inside edge. 
 sngLeft = Me.ScaleLeft ' Left inside edge. 
 sngWidth = Me.ScaleWidth / 2 ' Width inside edge. 
 sngHeight = Me.ScaleHeight / 2 ' Height inside edge. 
 intColor = RGB(255, 0, 0) ' Make color red. 
 
 ' Draw line. 
 Me.Line (sngTop, sngLeft)-(sngWidth, sngHeight), intColor 
 
End Sub
```


## See also
<a name="sectionSection2"> </a>


#### Concepts


 [Report Object](6f77c1b4-a9ce-7caa-204c-fe0755c6f9df.md)
#### Other resources


 [Report Object Members](73370a33-1ca0-da4d-9e36-88011bc2b93e.md)
