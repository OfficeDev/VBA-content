
# Select a Range

 **Last modified:** July 28, 2015

 **In this article**
 [Selecting the Used Range](#sectionSection0)
 [Selecting a Data Range Starting at Cell A1](#sectionSection1)
 [Selecting a Data Range of Unknown Starting Location](#sectionSection2)
 [About the Contributor](#AboutContributor)


These examples show how to select the used range, which includes formatted cells that do not contain data, and how to select a data range, which includes cells that contains actual data.

 **Sample code provided by:** Tom Urtis, [Atlas Programming Management](http://www.atlaspm.com/)

## Selecting the Used Range
<a name="sectionSection0"> </a>

This example shows how to select the used range on the current sheet, which includes formatted cells that do not contain data, by using the  ** [UsedRange](f004b93c-d785-de19-1fb4-bbe0b2e9b6cd.md)** property of the ** [Worksheet](182b705e-854a-81cc-a4b0-59b942de55ae.md)** object and the ** [Select](46c12f85-fae5-15ea-3500-81ff8be49cdb.md)** method of the ** [Range](b8207778-0dcc-4570-1234-f130532cc8cd.md)** object. Then it displays the address of the range to the user.


```
Sub SelectUsedRange()
    ActiveSheet.UsedRange.Select
    MsgBox "The used range address is " &amp; ActiveSheet.UsedRange.Address(0, 0) &amp; ".", 64, "Used range address:"
End Sub
```


## Selecting a Data Range Starting at Cell A1
<a name="sectionSection1"> </a>

This example shows how to select a data range on the current sheet, starting at cell A1, and display the address of the range to the user. The data range does not include cells that are formatted that do not contain data. To get the data range, this example finds the last row and the last column that contain actual data by using the  ** [Find](d9585265-8164-cb4d-a9e3-262f6e06b6b8.md)** method of the ** [Range](b8207778-0dcc-4570-1234-f130532cc8cd.md)** object.


```
Sub SelectDataRange()
    Dim LastRow As Long, LastColumn As Long
    LastRow = Cells.Find(What:="*", After:=Range("A1"), SearchOrder:=xlByRows, SearchDirection:=xlPrevious).Row
    LastColumn = Cells.Find(What:="*", After:=Range("A1"), SearchOrder:=xlByColumns, SearchDirection:=xlPrevious).Column
    Range("A1").Resize(LastRow, LastColumn).Select
    MsgBox "The data range address is " &amp; Selection.Address(0, 0) &amp; ".", 64, "Data-containing range address:"
End Sub
```


## Selecting a Data Range of Unknown Starting Location
<a name="sectionSection2"> </a>

This example shows how to select a data range on the current sheet when you do not know the starting location, and display the address of the range to the user. The data range does not include cells that are formatted that do not contain data. To get the data range, this example finds the first and last row and column that contain actual data by using the  ** [Find](d9585265-8164-cb4d-a9e3-262f6e06b6b8.md)** method of the ** [Range](b8207778-0dcc-4570-1234-f130532cc8cd.md)** object.


```
Sub UnknownRange()
    If WorksheetFunction.CountA(Cells) = 0 Then
        MsgBox "There is no range to be selected.", , "No cells contain any values."
        Exit Sub
    Else
        Dim FirstRow&amp;, FirstCol&amp;, LastRow&amp;, LastCol&amp;
        Dim myUsedRange As Range
        FirstRow = Cells.Find(What:="*", SearchDirection:=xlNext, SearchOrder:=xlByRows).Row
        
        On Error Resume Next
        FirstCol = Cells.Find(What:="*", SearchDirection:=xlNext, SearchOrder:=xlByColumns).Column
        If Err.Number <> 0 Then
            Err.Clear
            MsgBox _
            "There are horizontally merged cells on the sheet" &amp; vbCrLf &amp; _
            "that should be removed in order to locate the range.", 64, "Please unmerge all cells."
            Exit Sub
        End If
        
        LastRow = Cells.Find(What:="*", SearchDirection:=xlPrevious, SearchOrder:=xlByRows).Row
        LastCol = Cells.Find(What:="*", SearchDirection:=xlPrevious, SearchOrder:=xlByColumns).Column
        Set myUsedRange = Range(Cells(FirstRow, FirstCol), Cells(LastRow, LastCol))
        myUsedRange.Select
        MsgBox "The data range on this worksheet is " &amp; myUsedRange.Address(0, 0) &amp; ".", 64, "Range address:"
    End If
End Sub
```


## About the Contributor
<a name="AboutContributor"> </a>

MVP Tom Urtis is the founder of Atlas Programming Management, a full-service Microsoft Office and Excel business solutions company in Silicon Valley. Tom has over 25 years of experience in business management and developing Microsoft Office applications, and is the co-author of "Holy Macro! It's 2,500 Excel VBA Examples." 

