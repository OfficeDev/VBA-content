
# Databar Object (Excel)

 **Last modified:** July 28, 2015

Represents a data bar conditional formating rule. Applying a data bar to a range helps you see the value of a cell relative to other cells.

## Remarks

All conditional formatting objects are contained within a  ** [FormatConditions](2486d4b4-605c-76d8-132a-694c0c600a81.md)** collection object, which is a child of a ** [Range](b8207778-0dcc-4570-1234-f130532cc8cd.md)** collection. You can create a data bar formatting rule by using either the ** [Add](705f9ad4-2500-6607-19c0-6abd3f214d3e.md)** or ** [AddDatabar](4e22e749-860b-4224-b983-ba6dbbc7096a.md)** methods of the **FormatConditions** collection.

You use the  ** [MinPoint](d9a0a554-50e8-24a1-e10b-c4ee4cdbc159.md)** and ** [MaxPoint](d4b149f8-c245-40fa-bb89-a32d07354679.md)** properties of the **Databar** object to set the values of the shortest bar and longest bar of a range of data. These properites return a ** [ConditionValue](a39335db-4e0a-66aa-393b-3aa7e5268c00.md)** object, with which you can specify how the thresholds are evaluated.

The  **Databar** object also provides properties that enable you to specify an axis line that is displayed when negative values are present, and to specify the color and formatting of data bars.


## Example

The following example creates a range of data and then applies a data bar to the range. You will notice that because there is an extremely low and high value in the range, the middle values have data bars that are of similiar length. To disambiguate the middle values, the sample code uses the  **ConditionValue** object to change how the thresholds are evaluated to percentiles.


```
Sub CreateDataBarCF() 
 
 Dim cfDataBar As Databar 
 
 ' Create a range of data with a couple of extreme values 
 With ActiveSheet 
 .Range("D1") = 1 
 .Range("D2") = 45 
 .Range("D3") = 50 
 .Range("D2:D3").AutoFill Destination:=Range("D2:D8") 
 .Range("D9") = 500 
 End With 
 
 Range("D1:D9").Select 
 
 ' Create a data bar with default behavior 
 Set cfDataBar = Selection.FormatConditions.AddDatabar 
 MsgBox "Because of the extreme values, middle data bars are very similar" 
 
 ' The MinPoint and MaxPoint properties return a ConditionValue object 
 ' which you can use to change threshold parameters 
 cfDataBar.MinPoint.Modify newtype:=xlConditionValuePercentile, _ 
 newvalue:=5 
 cfDataBar.MaxPoint.Modify newtype:=xlConditionValuePercentile, _ 
 newvalue:=75 
 
End Sub
```


## See also


#### Concepts


 [Excel Object Model Reference](11ea8598-8a20-92d5-f98b-0da04263bf2c.md)
#### Other resources


 [Databar Object Members](137f7e88-bb61-48a3-d2cb-76a8282cd62e.md)
