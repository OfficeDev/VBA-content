
# Application.DMax Method (Access)

 **Last modified:** July 28, 2015

 **In this article**
 [Syntax](#sectionSection0)
 [Remarks](#sectionSection1)
 [Example](#sectionSection2)
 [About the Contributors](#AboutContributors)


You can use  **DMax** function to determine maximum value in a specified set of records (a domain).


## Syntax
<a name="sectionSection0"> </a>

 _expression_. **DMax**( **_Expr_**,  **_Domain_**,  **_Criteria_**)

 _expression_A variable that represents an  **Application** object.


### Parameters



|**Name**|**Required/Optional**|**Data Type**|**Description**|
|:-----|:-----|:-----|:-----|
|Expr|Required| **String**|An expression that identifies the field for which you want to find the minimum or maximum value. It can be a string expression identifying a field in a table or query, or it can be an expression that performs  [calculation on data in that field](73c27d1c-0a3c-03e4-c17c-337133d7b316.md). In expr, you can include the name of a field in a table, a control on a form, a constant, or a function. If expr includes a function, it can be either built-in or user-defined, but not another domain aggregate or SQL aggregate function.|
|Domain|Required| **String**|A string expression identifying the set of records that constitutes the domain. It can be a table name or a query name for a query that does not require a parameter.|
|Criteria|Optional| **Variant**|An optional string expression used to restrict the range of data on which the  **DMax** function is performed. For example,criteria is often equivalent to the WHERE clause in an SQL expression, without the word WHERE. Ifcriteria is omitted, the **DMax** function evaluatesexpr against the entire domain. Any field that is included incriteria must also be a field indomain, otherwise the  **DMax** function returns a **Null**.|

### Return Value

Variant


## Remarks
<a name="sectionSection1"> </a>

For example, you could use the  **DMax** function in calculated controls on a report to display largest order amount for a particular customer.

The  **DMax** function returns the maximum value that satisfycriteria. If expr identifies numeric data, the **DMax** function returns numeric values. Ifexpr identifies string data, they return the string that is first or last alphabetically.

The  **DMax** function ignores **Null** values in the field referenced byexpr. However, if no record satisfies criteria or ifdomain contains no records, the **DMax** function returns a **Null**.

You can use the  **DMax** function to specify criteria in the Criteria row of a query, in a calculated field expression in a query, or in the Update To row of an update query.


 **Note**  You can use the  **DMax** function or the **Max** function in a calculated field expression of a totals query. If you use the **DMax** function, values are evaluated before the data is grouped. If you use the **Max** function, the data is grouped before values in the field expression are evaluated.

Use the  **DMax** function in a calculated control when you need to specify criteria to restrict the range of data on which the function is performed. For example, to display the maximum freight charged for an order shipped to California, set the **ControlSource** property of a text box to the following expression:




```
=DMax("[Freight]", "Orders", "[ShipRegion] = 'CA'")
```

If you simply want to find the minimum or maximum value of all records in domain, use the  **Min** or **Max** function.

Although you can use the  **DMin** or **DMax** function to find the minimum or maximum value from a field in a foreign table, it may be more efficient to create a query that contains the fields that you need from both tables and base your form or report on that query.

 **Link provided by:**
![Community Member Icon](../images/8b9774c4-6c97-470e-b3a2-56d8f786444c.png) The [UtterAccess](http://www.utteraccess.com) community


-  [Sequential Numbering](http://www.utteraccess.com/wiki/index.php/Sequential_Numbering)
    

## Example
<a name="sectionSection2"> </a>

The following example returns the lowest and highest values from the Freight field for orders shipped to the United Kingdom. The domain is an Orders table. The criteria argument restricts the resulting set of records to those for which ShipCountry equals UK.


```
Dim curX As Currency 
Dim curY As Currency 
 
curX = DMin("[Freight]", "Orders", "[ShipCountry] = 'UK'") 
curY = DMax("[Freight]", "Orders", "[ShipCountry] = 'UK'")
```

In the next example, the criteria argument includes the current value of a text box called OrderDate. The text box is bound to an OrderDate field in an Orders table. Note that the reference to the control isn't included in the double quotation marks (") that denote the strings. This ensures that each time the **DMax** function is called, Microsoft Access obtains the current value from the control.




```
Dim curX As Currency 
curX = DMax("[Freight]", "Orders", "[OrderDate] = #" _ 
    &amp; Forms!Orders!OrderDate &amp; "#")
```



The following examples show how to use various types of criteria with the  **DMax** function.

 **Sample code provided by:**
![Community Member Icon](../images/8b9774c4-6c97-470e-b3a2-56d8f786444c.png) The [UtterAccess](http://www.utteraccess.com) community




```
    ' ***************************
    ' Typical Use
    ' Numerical values. Replace "number" with the number to use.
    variable = DMax("[FieldName]", "TableName", "[Criteria] = number")

    ' Strings.
    ' Numerical values. Replace "string" with the string to use.
    variable = DMax("[FieldName]", "TableName", "[Criteria]= 'string'")

    ' Dates. Replace "date" with the string to use.
    variable = DMax("[FieldName]", "TableName", "[Criteria]= #date#")
    ' ***************************

    ' ***************************
    ' Referring to a control on a form
    ' Numerical values
    variable = DMax("[FieldName]", "TableName", "[Criteria] = " &amp; Forms!FormName!ControlName)

    ' Strings
    variable = DMax("[FieldName]", "TableName", "[Criteria] = '" &amp; Forms!FormName!ControlName &amp; "'")

    ' Dates
    variable = DMax("[FieldName]", "TableName", "[Criteria] = #" &amp; Forms!FormName!ControlName &amp; "#")
    ' ***************************

    ' ***************************
    ' Combinations
    ' Multiple types of criteria
    variable = DMax("[FieldName]", "TableName", "[Criteria1] = " &amp; Forms![FormName]![Control1] _
             &amp; " AND [Criteria2] = '" &amp; Forms![FormName]![Control2] &amp; "'" _
            &amp; " AND [Criteria3] =#" &amp; Forms![FormName]![Control3] &amp; "#")
    
    ' Use two fields from a single record.
    variable = DMax("[LastName] &amp; ', ' &amp; [FirstName]", "tblPeople", "[PrimaryKey] = 7")
            
    ' Expressions
    variable = DMax("[Field1] + [Field2]", "tableName", "[PrimaryKey] = 7")
    
    ' Control Structures
    variable = DMax("IIf([LastName] Like 'Smith', 'True', 'False')", "tableName", "[PrimaryKey] = 7")
    ' ***************************
```


## About the Contributors
<a name="AboutContributors"> </a>

UtterAccess is the premier Microsoft Access wiki and help forum. Click here to join. 


## See also
<a name="AboutContributors"> </a>


#### Concepts


 [Application Object](aefb0713-97e6-e2c7-e530-8fd2e1316a55.md)
#### Other resources


 [Application Object Members](3ab5276c-d52a-72a9-244c-ec92ead48811.md)
