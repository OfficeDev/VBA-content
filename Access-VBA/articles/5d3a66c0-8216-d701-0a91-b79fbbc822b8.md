
# Field2.GetChunk Method (DAO)

 **Last modified:** March 09, 2015

 _ **Applies to:** Access 2013 | Access 2016_

 **In this article**
[Syntax](#sectionSection0)
[Remarks](#sectionSection1)
[Example](#sectionSection2)


Returns all or a portion of the contents of a  **Memo** or **Long Binary** **Field2** object in the **[Fields](4be3ba07-20c1-d958-c1b8-7dd8b4731f60.md)** collection of a **[Recordset](9774232c-e6da-175b-fc7f-ed2ab7908fa0.md)** object.

## Syntax
<a name="sectionSection0"> </a>

 _expression_. **GetChunk**( ** _Offset_**, ** _Bytes_** )

 _expression_ A variable that represents a **Field2** object.


### Parameters



|**Name**|**Required/Optional**|**Data Type**|**Description**|
|:-----|:-----|:-----|:-----|
| _Offset_|Required|**Long**|The number of bytes to skip before copying begins.|
| _Bytes_|Required|**Long**|The number of bytes you want to return.|

### Return Value

Variant


## Remarks
<a name="sectionSection1"> </a>

The bytes returned by  **GetChunk** are assigned to variable. Use **GetChunk** to return a portion of the total data value at a time. You can use the **[AppendChunk](f98c6862-fecf-06cb-a7c0-42b0d3150a06.md)** method to reassemble the pieces.

If  _offset_ is 0, **GetChunk** begins copying from the first byte of the field.

If  _numbytes_ is greater than the number of bytes in the field, **GetChunk** returns the actual number of remaining bytes in the field.




 **Note**  Use a  **Memo** field for text, and put binary data only in **Long Binary** fields. Doing otherwise will cause undesirable results.


## Example
<a name="sectionSection2"> </a>

This example uses the  **AppendChunk** and **GetChunk** methods to fill an OLE object field with data from another record, 32K at a time. In a real application, one might use a procedure like this to copy an employee record (including the employee's photo) from one table to another. In this example, the record is simply being copied back to same table. Note that all the chunk manipulation takes place within a single AddNew-Update sequence.


```vb
Sub AppendChunkX() 
 
 Dim dbsNorthwind As Database 
 Dim rstEmployees As Recordset 
 Dim rstEmployees2 As Recordset 
 
 Set dbsNorthwind = OpenDatabase("Northwind.mdb") 
 
 ' Open two recordsets from the Employees table. 
 Set rstEmployees = _ 
 dbsNorthwind.OpenRecordset("Employees", _ 
 dbOpenDynaset) 
 Set rstEmployees2 = rstEmployees.Clone 
 
 ' Add a new record to the first Recordset and copy the 
 ' data from a record in the second Recordset. 
 With rstEmployees 
 .AddNew 
 !FirstName = rstEmployees2!FirstName 
 !LastName = rstEmployees2!LastName 
 CopyLargeField rstEmployees2!Photo, !Photo 
 .Update 
 
 ' Delete new record because this is a demonstration. 
 .Bookmark = .LastModified 
 .Delete 
 .Close 
 End With 
 
 rstEmployees2.Close 
 dbsNorthwind.Close 
 
End Sub 
 
Function CopyLargeField(fldSource As Field2, _ 
 fldDestination As Field2) 
 
 ' Set size of chunk in bytes. 
 Const conChunkSize = 32768 
 
 Dim lngOffset As Long 
 Dim lngTotalSize As Long 
 Dim strChunk As String 
 
 ' Copy the photo from one Recordset to the other in 32K 
 ' chunks until the entire field is copied. 
 lngTotalSize = fldSource.FieldSize 
 Do While lngOffset < lngTotalSize 
 strChunk = fldSource.GetChunk(lngOffset, conChunkSize) 
 fldDestination.AppendChunk strChunk 
 lngOffset = lngOffset + conChunkSize 
 Loop 
 
End Function
```

