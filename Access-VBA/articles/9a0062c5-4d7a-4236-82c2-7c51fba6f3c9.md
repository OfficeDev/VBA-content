
# Animate a Sparkline

 **Last modified:** July 28, 2015

You can animate a sparkline by using the  [ModifySourceData](35c1c1ed-b61d-2412-961f-8eb74b5563a2.md) method of the [SparklineGroup](cc694d97-a3d3-3473-2e37-0ede67b97680.md) object to iterate over a range of data. This example takes 36 months of data and animates it by displaying the first year of data, then iterates through each subsequent month until it reaches the last month of data. A counter is used to slow the animation so it can be viewed more easily. This example requires a sparkline group that contains three sparklines in the range A2:A4 that represent data in the range B2:AK4.



```
Sub SparkAnimation()

    ' The group of sparklines to animate
    Dim oSparkGroup As SparklineGroup
    'variables for the loop
    Dim i As Integer, j As Integer
    
    ' Set up the sparkline group variable
    Set oSparkGroup = Sheet1.Range("A2").SparklineGroups(1)
    
    ' Set the data source to the first year of data
    oSparkGroup.ModifySourceData "B2:M4"
    
    ' Loop through the data points for the subsequent two years
    For i = 1 To 24
        ' Move the reference for the sparkline group over one cell
        oSparkGroup.ModifySourceData Range(oSparkGroup.SourceData).Offset(, 1).Address
        
        ' Slow the animation
        j = 1
        Do
            j = j + 1: DoEvents
        Loop Until j = 4000
    Next i
    
End Sub
```


## See also


#### Concepts


 [SparklineGroup Object](cc694d97-a3d3-3473-2e37-0ede67b97680.md)
#### Other resources


 [SparklineGroup Members](dad308ee-d69b-748d-d0c8-ad63c643808f.md)
 [Programming With Sparklines In Excel](http://msdn.microsoft.com/library/e26f3356-882e-44d5-94a5-c7e8d1026d78%28Office.15%29.aspx)
