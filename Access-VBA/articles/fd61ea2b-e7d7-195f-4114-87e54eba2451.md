
# Recordset2.EditMode Property (DAO)

 **Last modified:** June 29, 2011

 _ **Applies to:** Access 2013 | Access 2016_

 **In this article**
[Syntax](#sectionSection0)
[Remarks](#sectionSection1)
[Example](#sectionSection2)


Returns a value that indicates the state of editing for the current record.

## Syntax
<a name="sectionSection0"> </a>

 _expression_. **EditMode**

 _expression_ A variable that represents a **Recordset2** object.


## Remarks
<a name="sectionSection1"> </a>

The return value is a  **Long** that indicates the state of editing. The value can be one of the **[EditModeEnum](738ab5b7-75dc-2e45-45f2-eb64cc7e9527.md)** constants.

The  **EditMode** property is useful when an editing process is interrupted, for example, by an error during validation. You can use the value of the **EditMode** property to determine whether you should use the **[Update](1b47606a-e79c-23f1-b120-46d1429bc167.md)** or **[CancelUpdate](f741dec1-b9a4-506e-74ec-2bc309b0918e.md)** method.

You can also check to see if the  **[LockEdits](77055f44-f8e9-ac64-ecc3-144ddb4a4558.md)** property setting is **True** and the **EditMode** property setting is **dbEditInProgress** to determine whether the current page is locked.


## Example
<a name="sectionSection2"> </a>

This example shows the value of the  **EditMode** property under various conditions. The EditModeOutput function is required for this procedure to run.


```vb
Sub EditModeX() 
 
 Dim dbsNorthwind As Database 
 Dim rstEmployees As Recordset2 
 
 Set dbsNorthwind = OpenDatabase("Northwind.mdb") 
 Set rstEmployees = _ 
 dbsNorthwind.OpenRecordset("Employees", _ 
 dbOpenDynaset) 
 
 ' Show the EditMode property under different editing 
 ' states. 
 With rstEmployees 
 EditModeOutput "Before any Edit or AddNew:", .EditMode 
 .Edit 
 EditModeOutput "After Edit:", .EditMode 
 .Update 
 EditModeOutput "After Update:", .EditMode 
 .AddNew 
 EditModeOutput "After AddNew:", .EditMode 
 .CancelUpdate 
 EditModeOutput "After CancelUpdate:", .EditMode 
 .Close 
 End With 
 
 dbsNorthwind.Close 
 
End Sub 
 
Function EditModeOutput(strTemp As String, _ 
 intEditMode As Integer) 
 
 ' Print report based on the value of the EditMode 
 ' property. 
 Debug.Print strTemp 
 Debug.Print " EditMode = "; 
 
 Select Case intEditMode 
 Case dbEditNone 
 Debug.Print "dbEditNone" 
 Case dbEditInProgress 
 Debug.Print "dbEditInProgress" 
 Case dbEditAdd 
 Debug.Print "dbEditAdd" 
 End Select 
 
End Function
```

