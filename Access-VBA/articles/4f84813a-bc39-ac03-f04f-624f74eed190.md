
# Export Data, Schema, and Related Tables to XML

 **Last modified:** July 28, 2015

 _**Applies to:** Access 2013_

The  ** [ExportXML](47627677-D311-C2E1-7532-E8A8A9BEEF29.md)** method can be used to export the data and formatting contained in a table, along with any additonal data that you specify.

To specify the additional data to export, you must must use the  ** [CreateAdditionalData](D27DF827-1BCC-EB1E-00D2-46EEBD265440.md)** method to create an ** [AdditionalData](2677072B-C2CA-3BCD-FEF4-F6B1CADB0379.md)** object. Then, use the ** [Add](29D4DF5E-7646-B1E7-B179-A6588502179A.md)** method to add additonal tables to export along with the main table.
The following procedure illustrates how to include additional data when exporting a table to XML. The Orders table is exported along with several other tables. The schema and the formatting are also exported as separate .xsd and .xsl files, respectively.



```
Private Sub ExportRelTables() 
   ' Purpose: Exports the Orders table as well as  
   ' a number of related databases to an XML file. 
   ' XSD and XSL files are also created. 
 
   Dim objAD As AdditionalData 
 
   ' Create the AdditionalData object. 
   Set objAD = Application.CreateAdditionalData 
 
   ' Add the related tables to the object. 
   With objAD 
      .Add "Order Details" 
      objAD(Item:="Order Details").Add "Order Details Details" 
      .Add "Customers" 
      .Add "Shippers" 
      .Add "Employees" 
      .Add "Products" 
      objAD(Item:="Products").Add "Product Details" 
      objAD(Item:="Products")(Item:="Product Details").Add _ 
         "Product Details Details" 
      .Add "Suppliers" 
      .Add "Categories" 
   End With 
 
   ' Export the Orders table along with the addtional data. 
   Application.ExportXml acExportTable, "Orders", _ 
       "C:\Orders.xml", "C:\OrdersSchema.xsd", _ 
       "C:\OrdersStyle.xsl", AdditionalData:= objAD 
End Sub 

```

