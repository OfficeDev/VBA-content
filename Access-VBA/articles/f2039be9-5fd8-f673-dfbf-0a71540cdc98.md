
# Database.CreateProperty Method (DAO)

 **Last modified:** March 09, 2015

 _ **Applies to:** Access 2013 | Access 2016_

 **In this article**
[Syntax](#sectionSection0)
[Remarks](#sectionSection1)
[Example](#sectionSection2)


Creates a new user-defined  **[Property](a1ecb0db-bb93-a7b5-23c3-0b73f275dfe0.md)** object (Microsoft Access workspaces only). .

## Syntax
<a name="sectionSection0"> </a>

 _expression_. **CreateProperty**( ** _Name_**, ** _Type_**, ** _Value_**, ** _DDL_** )

 _expression_ A variable that represents a **Database** object.


### Parameters



|**Name**|**Required/Optional**|**Data Type**|**Description**|
|:-----|:-----|:-----|:-----|
| _Name_|Optional|**Variant**|A  **String** that uniquely names the new **Property** object. See the **Name** property for details on valid **Property** names.|
| _Type_|Optional|**Variant**| A constant that defines the data type of the new **Property** object. See the **[Type](1295ca40-78c1-bdd0-d407-e1b5be8adfd4.md)** property for valid data types.|
| _Value_|Optional|**Variant**|A  **Variant** containing the initial property value. See the **[Value](6c0f9a8d-f51a-b8cf-8830-f8d960a1d08c.md)** property for details.|
| _DDL_|Optional|**Variant**|A  **Variant** ( **Boolean** subtype) that indicates whether or not the **Property** is a DDL object. The default is _False_. If  _DDL_ is _True_, users can't change or delete this  **Property** object unless they have _dbSecWriteDef_ permission.|

### Return Value

Property


## Remarks
<a name="sectionSection1"> </a>

You can create a user-defined  **Property** object only in the **[Properties](cd07184a-a261-29c9-542f-bc2eff6f4af6.md)** collection of an object that is persistent.

If you omit one or more of the optional parts when you use  **CreateProperty**, you can use an appropriate assignment statement to set or reset the corresponding property before you append the new object to a collection. After you append the object, you can alter some but not all of its property settings. See the **Name**, **Type**, and **Value** property topics for more details.

If  _name_ refers to an object that is already a member of the collection, a run-time error occurs when you use the **[Append](a0e553ba-6a57-09af-3436-4f6ca3cbe561.md)** method.

To remove a user-defined  **Property** object from the collection, use the **[Delete](a8e249e7-7526-3eff-a5cf-70cab2081970.md)** method on the **Properties** collection. You can't delete built-in properties.




 **Note**  If you omit the  _DDL_ argument, it defaults to _False_ (non-DDL). Because no corresponding DDL property is exposed, you must delete and re-create a **Property** object you want to change from DDL to non-DDL.


## Example
<a name="sectionSection2"> </a>

This example tries to set the value of a user-defined property. If the property doesn't exist, it uses the  **CreateProperty** method to create and set the value of the new property. The SetProperty procedure is required for this procedure to run.


```vb
Sub CreatePropertyX() 
 
   Dim dbsNorthwind As Database 
   Dim prpLoop As Property 
 
   Set dbsNorthwind = OpenDatabase("Northwind.mdb") 
 
   ' Set the Archive property to True. 
   SetProperty dbsNorthwind, "Archive", True 
    
   With dbsNorthwind 
      Debug.Print "Properties of " &; .Name 
       
      ' Enumerate Properties collection of the Northwind  
      ' database. 
      For Each prpLoop In .Properties 
         If prpLoop <> "" Then Debug.Print "  " &; _ 
            prpLoop.Name &; " = " &; prpLoop 
      Next prpLoop 
 
      ' Delete the new property since this is a  
      ' demonstration. 
      .Properties.Delete "Archive" 
 
      .Close 
   End With 
 
End Sub 
 
Sub SetProperty(dbsTemp As Database, strName As String, _ 
   booTemp As Boolean) 
 
   Dim prpNew As Property 
   Dim errLoop As Error 
 
   ' Attempt to set the specified property. 
   On Error GoTo Err_Property 
   dbsTemp.Properties("strName") = booTemp 
   On Error GoTo 0 
 
   Exit Sub 
 
Err_Property: 
 
   ' Error 3270 means that the property was not found. 
   If DBEngine.Errors(0).Number = 3270 Then 
      ' Create property, set its value, and append it to the  
      ' Properties collection. 
      Set prpNew = dbsTemp.CreateProperty(strName, _ 
         dbBoolean, booTemp) 
      dbsTemp.Properties.Append prpNew 
      Resume Next 
   Else 
      ' If different error has occurred, display message. 
      For Each errLoop In DBEngine.Errors 
         MsgBox "Error number: " &; errLoop.Number &; vbCr &; _ 
            errLoop.Description 
      Next errLoop 
      End 
   End If 
 
End Sub
```

