
# Module.DeleteLines Method (Access)

 **Last modified:** July 28, 2015

 **In this article**
 [Syntax](#sectionSection0)
 [Remarks](#sectionSection1)
 [Example](#sectionSection2)


The  **DeleteLines** method deletes lines from a standard module or a class module.


## Syntax
<a name="sectionSection0"> </a>

 _expression_. **DeleteLines**( **_StartLine_**,  **_Count_**)

 _expression_A variable that represents a  **Module** object.


### Parameters



|**Name**|**Required/Optional**|**Data Type**|**Description**|
|:-----|:-----|:-----|:-----|
|StartLine|Required| **Long**| The number of the line from which to begin deleting.|
|Count|Required| **Long**|The number of lines to delete.|

### Return Value

Nothing


## Remarks
<a name="sectionSection1"> </a>

Lines in a module are numbered beginning with one. To determine the number of lines in a module, use the  ** [CountOfLines](6c3bb4c8-15a9-6365-155d-d28dc0c5de78.md)**property.

To replace one line with another line, use the  ** [ReplaceLine](9e267b4a-5c15-a1bc-e2e0-a528871c9268.md)**method.


## Example
<a name="sectionSection2"> </a>

The following example deletes a specified line from a module.


```
Function DeleteWholeLine(strModuleName, strText As String) _ 
 As Boolean 
 Dim mdl As Module, lngNumLines As Long 
 Dim lngSLine As Long, lngSCol As Long 
 Dim lngELine As Long, lngECol As Long 
 Dim strTemp As String 
 
 On Error GoTo Error_DeleteWholeLine 
 DoCmd.OpenModule strModuleName 
 Set mdl = Modules(strModuleName) 
 
 If mdl.Find(strText, lngSLine, lngSCol, lngELine, lngECol) Then 
 lngNumLines = Abs(lngELine - lngSLine) + 1 
 strTemp = LTrim$(mdl.Lines(lngSLine, lngNumLines)) 
 strTemp = RTrim$(strTemp) 
 If strTemp = strText Then 
 mdl.DeleteLines lngSLine, lngNumLines 
 Else 
 MsgBox "Line contains text in addition to '" _ 
 &amp; strText &amp; "'." 
 End If 
 Else 
 MsgBox "Text '" &amp; strText &amp; "' not found." 
 End If 
 DeleteWholeLine = True 
 
Exit_DeleteWholeLine: 
 Exit Function 
 
Error_DeleteWholeLine: 
 MsgBox Err &amp; " :" &amp; Err.Description 
 DeleteWholeLine = False 
 Resume Exit_DeleteWholeLine 
End Function 

```

You could call this function from a procedure such as the following, which searches the module Module1 for a constant declaration and deletes it.




```
Sub DeletePiConst() 
 If DeleteWholeLine("Module1", "Const conPi = 3.14") Then 
 Debug.Print "Constant declaration deleted successfully." 
 Else 
 Debug.Print "Constant declaration not deleted." 
 End If 
End Sub 

```


## See also
<a name="sectionSection2"> </a>


#### Concepts


 [Module Object](e04272fa-9c29-2567-bd15-1cea38906894.md)
#### Other resources


 [Module Object Members](c2e71012-645e-b818-1247-9775f221619e.md)
