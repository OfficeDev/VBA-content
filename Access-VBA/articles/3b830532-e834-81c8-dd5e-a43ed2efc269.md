
# PivotTables.Add Method (Excel)

 **Last modified:** July 28, 2015

Adds a new PivotTable report. Returns a  ** [PivotTable](a9c1d4a0-78a9-f9a6-6daf-91cb63e45842.md)** object.

## Syntax

 _expression_. **Add**( **_PivotCache_**,  **_TableDestination_**,  **_TableName_**,  **_ReadData_**,  **_DefaultVersion_**)

 _expression_A variable that represents a  **PivotTables** object.


### Parameters



|**Name**|**Required/Optional**|**Data Type**|**Description**|
|:-----|:-----|:-----|:-----|
|PivotCache|Required| ** [PivotCache](c3d84ef1-f9e6-b1bc-cbf0-3ba8dfe17439.md)**|The PivotTable cache on which the new PivotTable report is based. The cache provides data for the report.|
|TableDestination|Required| **Variant**|The cell in the upper-left corner of the PivotTable report's destination range (the range on the worksheet where the resulting report will be placed). You must specify a destination range on the worksheet that contains the  **PivotTables** object specified byexpression.|
|TableName|Optional| **Variant**|The name of the new PivotTable report.|
|ReadData|Optional| **Variant**| **True** to create a PivotTable cache that contains all records from the external database; this cache can be very large. **False** to enable setting some of the fields as server-based page fields before the data is actually read.|
|DefaultVersion|Optional| **Variant**|The version of Microsoft Excel the PivotTable was originally created in.|

### Return Value

A  ** [PivotTable](a9c1d4a0-78a9-f9a6-6daf-91cb63e45842.md)** object that represents the new PivotTable report.


## Example

This example creates a new PivotTable cache based on an OLAP provider, and then it creates a new PivotTable report based on the cache, at cell A1 on the first worksheet.


```
Dim cnnConn As ADODB.Connection 
Dim rstRecordset As ADODB.Recordset 
Dim cmdCommand As ADODB.Command 
 
' Open the connection. 
Set cnnConn = New ADODB.Connection 
With cnnConn 
 .ConnectionString = _ 
 "Provider=Microsoft.Jet.OLEDB.4.0" 
 .Open "C:\perfdate\record.mdb" 
End With 
 
' Set the command text. 
Set cmdCommand = New ADODB.Command 
Set cmdCommand.ActiveConnection = cnnConn 
With cmdCommand 
 .CommandText = "Select Speed, Pressure, Time From DynoRun" 
 .CommandType = adCmdText 
 .Execute 
End With 
 
' Open the recordset. 
Set rstRecordset = New ADODB.Recordset 
Set rstRecordset.ActiveConnection = cnnConn 
rstRecordset.Open cmdCommand 
 
' Create PivotTable cache and report. 
Set objPivotCache = ActiveWorkbook.PivotCaches.Add( _ 
 SourceType:=xlExternal) 
Set objPivotCache.Recordset = rstRecordset 
 
ActiveSheet.PivotTables.Add _ 
 PivotCache:=objPivotCache, _ 
 TableDestination:=Range("A3"), _ 
 TableName:="Performance" 
 
With ActiveSheet.PivotTables("Performance") 
 .SmallGrid = False 
 With .PivotFields("Pressure") 
 .Orientation = xlRowField 
 .Position = 1 
 End With 
 With .PivotFields("Speed") 
 .Orientation = xlColumnField 
 .Position = 1 
 End With 
 With .PivotFields("Time") 
 .Orientation = xlDataField 
 .Position = 1 
 End With 
End With 
 
' Close the connections and clean up. 
cnnConn.Close 
Set cmdCommand = Nothing 
Set rstRecordSet = Nothing 
Set cnnConn = Nothing
```


## See also


#### Concepts


 [PivotTables Object](5beb33ac-a0fb-3f78-8fdc-d05719512214.md)
#### Other resources


 [PivotTables Object Members](c5b75c05-61a6-d791-790d-ec2a4e5047ae.md)
