
# Recordset2.CacheSize Property (DAO)

 **Last modified:** June 29, 2011

 _ **Applies to:** Access 2013 | Access 2016_

 **In this article**
[Syntax](#sectionSection0)
[Remarks](#sectionSection1)
[Example](#sectionSection2)


Sets or returns the number of records retrieved from an ODBC data source that will be cached locally. Read/write  **Long**.

## Syntax
<a name="sectionSection0"> </a>

 _expression_. **CacheSize**

 _expression_ A variable that represents a **Recordset2** object.


## Remarks
<a name="sectionSection1"> </a>

The value of the  **CacheSize** property must be between 5 and 1200, but not greater than available memory will allow. A typical value is 100. A setting of 0 turns off caching.

Data caching improves performance if you use  **Recordset** objects to retrieve data from a remote server. A cache is a space in local memory that holds the data most recently retrieved from the server; this is useful if users request the data again while the application is running. When users request data, the Microsoft Access database engine checks the cache for the requested data first rather than retrieving it from the server, which takes more time. The cache only saves data that comes from an ODBC data source.

Any Microsoft Access database engine-connected ODBC data source, such as a linked table, can have a local cache. To create the cache, open a  **Recordset** object from the remote data source, set the **CacheSize** and **[CacheStart](2e9c2b0d-b382-e4d6-9406-ace0e538a7b7.md)** properties, and then use the **[FillCache](28a70997-a8d4-73e6-171a-61286e3d3485.md)** method, or step through the records by using the **Move** methods.

You can base the  **CacheSize** property setting on the number of records your application can handle at one time. For example, if you're using a **Recordset** object as the source of the data to be displayed on screen, you could set its **CacheSize** property to 20 to display 20 records at one time.

The Microsoft Access database engine requests records within the cache range from the cache, and it requests records outside the cache range from the server.

Records retrieved from the cache don't reflect concurrent changes that other users made to the source data.

To force an update of all the cached data, set the  **CacheSize** property of the **Recordset** object to 0, re-set it to the size of the cache you originally requested, and then use the **FillCache** method.


## Example
<a name="sectionSection2"> </a>

This example uses the  **CreateTableDef** and **FillCache** methods and the **CacheSize**, **CacheStart** and **SourceTableName** properties to enumerate the records in a linked table twice. Then it enumerates the records twice with a 50-record cache. The example then displays the performance statistics for the uncached and cached runs through the linked table.


```vb
Sub ClientServerX3() 
 
 Dim dbsCurrent As Database 
 Dim tdfRoyalties As TableDef 
 Dim rstRemote As Recordset2 
 Dim sngStart As Single 
 Dim sngEnd As Single 
 Dim sngNoCache As Single 
 Dim sngCache As Single 
 Dim intLoop As Integer 
 Dim strTemp As String 
 Dim intRecords As Integer 
 
 ' Open a database to which a linked table can be 
 ' appended. 
 Set dbsCurrent = OpenDatabase("DB1.mdb") 
 
 ' Create a linked table that connects to a Microsoft SQL 
 ' Server database. 
 Set tdfRoyalties = _ 
 dbsCurrent.CreateTableDef("Royalties") 
 ' Note: The DSN referenced below must be set to 
 ' use Microsoft Windows NT Authentication Mode to 
 ' authorize user access to the Microsoft SQL Server. 
 tdfRoyalties.Connect = _ 
 "ODBC;DATABASE=pubs;DSN=Publishers" 
 tdfRoyalties.SourceTableName = "roysched" 
 dbsCurrent.TableDefs.Append tdfRoyalties 
 Set rstRemote = _ 
 dbsCurrent.OpenRecordset("Royalties") 
 
 With rstRemote 
 ' Enumerate the Recordset object twice and record 
 ' the elapsed time. 
 sngStart = Timer 
 
 For intLoop = 1 To 2 
 .MoveFirst 
 Do While Not .EOF 
 ' Execute a simple operation for the 
 ' performance test. 
 strTemp = !title_id 
 .MoveNext 
 Loop 
 Next intLoop 
 
 sngEnd = Timer 
 sngNoCache = sngEnd - sngStart 
 
 ' Cache the first 50 records. 
 .MoveFirst 
 .CacheSize = 50 
 .FillCache 
 sngStart = Timer 
 
 ' Enumerate the Recordset object twice and record 
 ' the elapsed time. 
 For intLoop = 1 To 2 
 intRecords = 0 
 .MoveFirst 
 Do While Not .EOF 
 ' Execute a simple operation for the 
 ' performance test. 
 strTemp = !title_id 
 ' Count the records. If the end of the 
 ' cache is reached, reset the cache to the 
 ' next 50 records. 
 intRecords = intRecords + 1 
 .MoveNext 
 If intRecords Mod 50 = 0 Then 
 .CacheStart = .Bookmark 
 .FillCache 
 End If 
 Loop 
 Next intLoop 
 
 sngEnd = Timer 
 sngCache = sngEnd - sngStart 
 
 ' Display performance results. 
 MsgBox "Caching Performance Results:" &; vbCr &; _ 
 " No cache: " &; Format(sngNoCache, _ 
 "##0.000") &; " seconds" &; vbCr &; _ 
 " 50-record cache: " &; Format(sngCache, _ 
 "##0.000") &; " seconds" 
 .Close 
 End With 
 
 ' Delete linked table because this is a demonstration. 
 dbsCurrent.TableDefs.Delete tdfRoyalties.Name 
 dbsCurrent.Close 
 
End Sub
```

