

## Scripting.Dictionary Object

### Description

 Represents a data [collection](https://msdn.microsoft.com/EN-US/library/office/gg251465.aspx) object that stores data in pairs as a unique key and an associated item.

### Syntax
 **Scripting.Dictionary**

### Properties

|**Name**|**Read/Write**|**Data Type**|**Description**|
|:-----|:-----|:-----|:-----|
|[CompareMode](https://msdn.microsoft.com/en-us/library/a14xez73.aspx)|*read / write*|[CompareMethod constant](https://msdn.microsoft.com/en-us/library/zx5efzad%28v=vs.90%29.aspx)|Sets or retrieves the current [CompareMode](https://msdn.microsoft.com/en-us/library/6f38799a.aspx) state. Can only be set on an empty dictionary. Accepted values are 0 (vbBinaryCompare), 1 (vbTextCompare), 2 (vbDatabaseCompare). Values greater than 2 can be used to refer to comparisons using specific Locale IDs (LCID). |
|[Count](https://msdn.microsoft.com/en-us/library/5t9h9579.aspx)|*read only*|Unsigned Integer|The one-based index count of the key/item pairs in the scripting dictionary object.|
|[Key](https://msdn.microsoft.com/en-us/library/1ex01tte.aspx)|*read / write*|(non-array) Variant|Each individual unique key in the dictionary.|
|[Item(*key*)](https://msdn.microsoft.com/en-us/library/84k9x471.aspx)|*read / write*|Variant|Each individual item associated with a key in the dictionary.|

 
### Methods
 
|**Name**|**Description**|
|:-----|:-----|
|[Add(*key,&nbsp;item*)](https://msdn.microsoft.com/en-us/library/5h92h863.aspx)|Adds a new Key and Item to the dictionary. The new key must not exist in the dictionary's current [Keys](https://msdn.microsoft.com/en-us/library/etzd1tzc.aspx) collection but an item can be repeated among many unique keys.|
|[Exists(*key*)](https://msdn.microsoft.com/en-us/library/office/gg251562.aspx)|Boolean test to determine if a Key already exists in the dictionary.|
|[Keys](https://msdn.microsoft.com/en-us/library/etzd1tzc.aspx)|Returns the array or collection of unique keys.|
|[Items](https://msdn.microsoft.com/en-us/library/8aet97f2.aspx)|Returns the array or collection of associated items.|
|[Remove(*key*)](https://msdn.microsoft.com/en-us/library/ywyayk03.aspx)|Removes individual dictionary keys with their associated item.|
|[RemoveAll](https://msdn.microsoft.com/en-us/library/45731e2w.aspx)|Clears all of a dictionary object's keys and items.|

 
 ### Remarks
 
 A  **Scripting.Dictionary** object is the equivalent of a PERL associative array. Items, which can be any form of data, are stored in the array. Each item is associated with a unique key. The key is used to retrieve an individual item and is usually an integer or a string, but can be anything except an array. They can be compared to a rudimentary two field database table with one field governed by an unsorted but unique primary index.

 If [Early Binding](https://msdn.microsoft.com/en-us/library/0tcf61s1.aspx) is to be implemented, a reference must be added to [Microsoft Scripting Runtime](https://msdn.microsoft.com/en-us/library/hww8txat.aspx) in the [VBE's Tools, References](https://msdn.microsoft.com/en-us/library/aa210708.aspx).
 
  A **Scripting.Dictionary** is not to be confused with an [MS Word Dictionary Object](https://msdn.microsoft.com/en-us/library/office/ff191817.aspx) that represents custom dictionaries as members of the Language Dictionaries collection.

### Examples

 The following illustrates how to create and populate a **Scripting.Dictionary** object.

```
    'Early Binding - requires reference to Microsoft Scripting Runtime
    Dim d1 As New Scripting.Dictionary           'Declare and bind a variable
    
    d1.CompareMode = vbBinaryCompare             'set the CompareMode on an empty dictionary
    
    d1.Add "a", "Athens"                         'Add some keys and items
    d1.Add "b", "Belgrade"
    d1.Add "c", "Cairo"
    d1.Add "C", "Carthage"                       'no error on a binary compare mode (e.g. c <> C)
    d1.Key("C") = "d"                            'change the key 
    Debug.Print d1.Count                         'returns 4

    'Late Binding
    Dim d2 As Object                             'Declare a variable
    Set d2 = CreateObject("Scripting.Dictionary")  'Bind the variable
    
    d2.CompareMode = vbTextCompare               'non-case sensitive compare model
    
    d2.Add "a", "Athens"                         'Add some keys and items
    d2.Add "b", "Belgrade"
    d2.Add "c", "Cairo"
    Debug.Print d2.Item("c")                     'Cairo
    
    On Error Resume Next                         'attempt to duplicate a key
    d2.Add "C", "Carthage"                       'error 457 thrown in text compare mode (e.g. c = C)
    '457 - This key is already associated with an element of this collection
    Debug.Print Err.Number & " - " & Err.Description
    On Error GoTo 0
    
    If d2.Exists("C") Then _
        d2.Item("C") = "Carthage"                'overwrite the original 'c' key's item
    Debug.Print d2.Item("c")                     'Carthage
    Debug.Print d2.Count                         'returns 3
    
    d2.Item("d") = "Cairo"                       'shorthand method; adds if new or updates item of existing key
    Debug.Print d2.Item("d")                     'Cairo
    
    Debug.Print d2.Count                         'returns 4

```

Use a Scripting.Dictionary's Keys as criteria for the [Range.AutoFilter method (Excel)](https://msdn.microsoft.com/en-us/library/office/ff193884.aspx)

```
    Dim dict As Object
    Set dict = CreateObject("Scripting.Dictionary")
    
    dict.CompareMode = vbTextCompare
    dict.Add Key:="red", Item:="anything"
    dict.Add Key:="blue", Item:="anything"
    dict.Add Key:="green", Item:="anything"
    
    With Worksheets("Sheet1")
        If .AutoFilterMode Then .AutoFilterMode = False
        With .Cells(1, 1).CurrentRegion
            'use the dictionary's unique keys as the criteria array
            .AutoFilter Field:=2, Criteria1:=dict.Keys, Operator:=xlFilterValues
        End With
        'data originating at A1 is filtered on column B for red, blue or green
    End With
```

Access and enumerate the dictionary's keys and items.

```
    'enumerate by key
    Dim k As Variant
    For Each k In dict.Keys
        Debug.Print k & " - " & dict.Item(k)
    Next k
    
    'enumerate by index position (zero-based index)
    Dim i As Long
    For i = 0 To dict.Count - 1
        Debug.Print dict.Keys(i) & " - " & dict.Items(i)
    Next i

    'enumerate as elements of an array
    Dim a As Long
    For a = LBound(dict.Keys) To UBound(dict.Keys)
        Debug.Print dict.Keys(a) & " - " & dict.Items(a)
    Next a
```


Remove a specific key/item pair or all entries from the dictionary object

```
    dict.Remove "blue"                         'remove by key
    
	dict.Remove dict.Keys(0)                   'remove first key/item by index position
	dict.Remove dict.Keys(UBound(dict.Keys))   'remove last key/item by index position
    
    dict.RemoveAll                             'remove all
```
###Concepts

[Objects (Visual Basic for Applications)](https://msdn.microsoft.com/en-us/library/office/jj692784.aspx)

###See Also

[Scripting Run-Time Reference](https://msdn.microsoft.com/en-us/library/hww8txat.aspx)<br/>
[FileSystemObject Object](https://msdn.microsoft.com/en-us/library/z9ty6h50.aspx)<br/>
[TextStream Object](https://msdn.microsoft.com/en-us/library/312a5kbt.aspx)
