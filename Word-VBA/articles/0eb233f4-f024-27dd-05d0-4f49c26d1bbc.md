
# Filter the Building Block List in a Content Control

 **Last modified:** July 28, 2015

 _**Applies to:** Word 2013_

 **In this article**
 [Sample 1](#sectionSection0)
 [Sample 2](#sectionSection1)
 [Additional Resources](#sectionSection2)


When you use content controls together with building blocks, you can help users by indicating what content they can insert and by limiting their choices. You can do this is by filtering the list of building blocks that are shown for a content control. To filter a building block list for a content control, you use the  ** [BuildingBlockType](6fe28ef5-fe7d-792e-f73a-b6726c802790.md)** property for the content control. You can further filter the list of building blocks by setting the ** [BuildingBlockCategory](ca799bde-8556-381e-c9ca-74c5ac250d99.md)** property for the content control.
You can filter the list of building blocks for a content control when you create the content control. However, you can also programmatically filter the list of building blocks based on the position of the cursor or on the value of another content control. To start, you need a custom building block gallery. To learn how to create a custom building block gallery, see  [Create a Custom Building Block Gallery](472688b6-205c-c88d-5a7e-26334ec5eeeb.md).


-  ** [BuildingBlock](2558b89f-8552-bb71-fa40-101cab2635ba.md)**
    
-  ** [ContentControl](783dec26-9b63-11f8-6187-985f9c815f27.md)**
    

## Sample 1
<a name="sectionSection0"> </a>

The following example shows how to filter a building block list to a specific gallery.


 **Note**  Run the code in the "Create a Custom Building Block Gallery" topic before running the code in this topic.


```
Sub CreateFilteredContentControl_SampleOneA() 
 Dim objCC As ContentControl 
 
 Set objCC = ActiveDocument.ContentControls.Add( _ 
 wdContentControlBuildingBlockGallery, Selection.Range) 
 
 objCC.BuildingBlockType = wdTypeCustom1 
End Sub
```

You can filter a building block list even further by specifying a specific category within the gallery. The following example shows how to filter a building block list to include only the building blocks within a category of a specified gallery.




```
Sub CreateFilteredContentControl_SampleOneB() 
 Dim objCC As ContentControl 
 
 Set objCC = ActiveDocument.ContentControls.Add( _ 
 wdContentControlBuildingBlockGallery, Selection.Range) 
 
 objCC.BuildingBlockType = wdTypeCustom1 
 objCC.BuildingBlockCategory = "Tertiary Headings" 
End Sub
```


## Sample 2
<a name="sectionSection1"> </a>

To filter a building block list based on the position of the cursor, you need to use the  **ContentControlOnEnter** event. For example, if you have a content control named Report Type that can be set to "financial" or "marketing", you can have a building block content control that shows a list of possible disclaimers. The content control for the disclaimers would show all disclaimers if the Report Type is not set, and only the appropriate subset if the property is set. The following example filters the list of building blocks for a content control based on the value of another content control in the document.


```
Private Sub Document_ContentControlOnEnter(ByVal ContentControl As ContentControl) 
 Dim objCC As ContentControl 
 Dim objType As ContentControl 
 
 Set objCC = ContentControl 
 Set objType = ActiveDocument.ContentControls.Item("Report Type") 
 
 If objCC.Title = "Disclaimer" Then 
 Select Case objType.Range.Text 
 Case "Financial" 
 objCC.BuildingBlockType = wdTypeCustom1 
 objCC.BuildingBlockCategory = "Financial Disclaimers" 
 
 Case "Marketing" 
 objCC.BuildingBlockType = wdTypeCustom1 
 objCC.BuildingBlockCategory = "Marketing Disclaimers" 
 
 End Select 
 End If 
End Sub
```


## Additional Resources
<a name="sectionSection2"> </a>


-  [Working with Building Blocks](c32a8972-a6fc-bb66-b62a-039b88580b37.md)
    
-  [Working with Content Controls](b4092c71-a383-f1db-8d68-de69e8d8a86b.md)
    
