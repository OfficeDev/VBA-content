
# Storing Values When a Macro Ends

 **Last modified:** July 28, 2015

 _**Applies to:** Word 2013_

 **In this article**
 [Document variables](#sectionSection0)
 [Remarks](#sectionSection1)
 [Document properties](#sectionSection2)
 [Remarks](#sectionSection3)
 [AutoText entries](#sectionSection4)
 [Settings files](#sectionSection5)
 [Windows registry](#sectionSection6)


When a macro ends, the values stored in its variables are not automatically saved to disk. If a macro needs to preserve a value, it must store that value outside itself before the macro execution is completed. This topic describes five locations where macro values can be easily stored and retrieved.

## Document variables
<a name="sectionSection0"> </a>

Document variables allow you to store values as part of a document or a template. For example, you might store macro values in the document or template where the macro resides. You can add variables to a document or template using the  ** [Add](5c38d785-539b-7e6c-9cd0-cfa48e1aef33.md)**method of the  ** [Variables](9719d0d4-319d-c710-d243-12a9dee45880.md)**collection. The following example saves a document variable in the same location as the macro that is running (document or template) using the  **ActiveDocument** property.


```
Sub AddDocumentVariable() 
 ActiveDocument.Variables.Add Name:="Age", Value:=12 
End Sub
```

The following example uses the  ** [Value](6a687fff-062a-4e27-abc7-2f49d6f9c76b.md)**property with a  ** [Variable](e6a75f54-6f91-75b4-7ca0-9be302e8dbe0.md)**object to return the value of a document variable.




```
Sub UseDocumentVariable() 
 Dim intAge As Integer 
 intAge = ActiveDocument.Variables("Age").Value 
End Sub
```


## Remarks
<a name="sectionSection1"> </a>

You can use the DOCVARIABLE field to insert a document variable into a document.


## Document properties
<a name="sectionSection2"> </a>

Like document variables, document properties allow you to store values as part of a document or a template. Document properties can be viewed in the  **Properties** dialog box.

The Word object model breaks document properties into two groups: built-in and custom. Custom document properties include the properties shown on the  **Custom** tab in the **Properties** dialog box. Built-in document properties include the properties on all the tabs in the **Properties** dialog box, except the **Custom** tab.

To access built-in properties, use the  **BuiltInDocumentProperties** property to return a **DocumentProperties** collection that includes the built-in document properties. Use the **CustomDocumentProperties**property of a  ** [Document](8d83487a-2345-a036-a916-971c9db5b7fb.md)** object or a ** [Template](47d1d92d-bba9-3f2a-9c71-22ac43159bd3.md)** object to return a **DocumentProperties** collection that includes the custom document properties. The following example creates a custom document property named "YourName" in the same location as the macro that is running (document or template).




```
Sub AddCustomDocumentProperties() 
 ActiveDocument.CustomDocumentProperties.Add Name:="YourName", _ 
 LinkToContent:=False, Value:="Joe", Type:=msoPropertyTypeString 
End Sub
```

Built-in document properties cannot be added to the  **DocumentProperties** collection returned by the **BuiltInDocumentProperties** property of a ** [Document](8d83487a-2345-a036-a916-971c9db5b7fb.md)** object or ** [Template](47d1d92d-bba9-3f2a-9c71-22ac43159bd3.md)** object. You can, however, retrieve the contents of a built-in document property or change the value of a read/write built-in document property.


## Remarks
<a name="sectionSection3"> </a>

You can use the DOCPROPERTY field to insert document properties into a document.


## AutoText entries
<a name="sectionSection4"> </a>

AutoText entries can be used to store information in a template. Unlike a document variable or property, AutoText entries can include items beyond macro variables, such as formatted text or a graphic. Use the  ** [Add](7ffa87f9-a23c-1847-3907-84c95f2b7f73.md)**method with the  ** [AutoTextEntries](4e4d92b3-d259-84b7-061f-82065e177c29.md)**collection to create a new AutoText entry. The following example creates an AutoText entry named "MyText" that contains the contents of the selection. If the following instruction is part of a template macro, the new AutoText entry is stored in the template, otherwise, the AutoText entry is stored in the template attached to the document where the instruction resides.


```
Sub AddAutoTextEntry() 
 ActiveDocument.AttachedTemplate.AutoTextEntries.Add Name:="MyText", _ 
 Range:=Selection.Range 
End Sub
```

Use the  ** [Value](0b8b92d7-955b-3f4c-8bbd-924786b3aa9c.md)**property with an  ** [AutoTextEntry](37a2cf05-ae07-d411-9bd8-ab4726b303a9.md)**object to retrieve the contents of an AutoText entry object.


## Settings files
<a name="sectionSection5"> </a>

You can set and retrieve information from a settings file using the  ** [PrivateProfileString](737fb157-4665-5e31-240a-347bd7334005.md)**property of the  ** [System](db15d780-3bbc-9515-a988-ea798777496f.md)** object. The structure of a Windows settings file is the same as the Windows 3.1 WIN.INI file. The following example sets the DocNum key to 1 under the DocTracker section in the Macro.ini file.


```
Sub MacroSystemFile() 
 System.PrivateProfileString( _ 
 FileName:="C:\My Documents\Macro.ini", _ 
 Section:="DocTracker", Key:="DocNum") = 1 
End Sub
```

After running the above instruction, the Macro.ini file includes the following text.




```
[DocTracker] 
DocNum=1
```

The  **PrivateProfileString** property has three arguments: **_FileName_**,  **_Section_**, and  **_Key_**. The  **_FileName_** argument is used to specify a settings file path and file name. The **_Section_** argument specifies the section name that appears between brackets before the associated keys (do not include the brackets with the section name). The **_Key_** argument specifies the key name, which is followed by an equal sign (=) and the setting.

Use the same  **PrivateProfileString** property to retrieve a setting from a settings file. The following example retrieves the DocNum setting under the DocTracker section in the Macro.ini file.




```
Sub GetSystemFileInfo() 
 Dim intDocNum As Integer 
 intDocNum = System.PrivateProfileString( _ 
 FileName:="C:\My Documents\Macro.ini", _ 
 Section:="DocTracker", Key:="DocNum") 
 MsgBox "DocNum is " &amp; intDocNum 
End Sub
```


## Windows registry
<a name="sectionSection6"> </a>

You can set and retrieve information from the Windows registry using the  **PrivateProfileString**property. The following example retrieves the Word 2007 program directory from the Windows registry.


```
Sub GetRegistryInfo() 
 Dim strSection As String 
 Dim strPgmDir As String 
 strSection = "HKEY_CURRENT_USER\Software\Microsoft" _ 
 &amp; "\Office\12.0\Word\Options" 
 strPgmDir = System.PrivateProfileString(FileName:="", _ 
 Section:=strSection, Key:="PROGRAMDIR") 
 MsgBox "The directory for Word is - " &amp; strPgmDir 
End Sub
```

The  **PrivateProfileString** property has three arguments: **_FileName_**,  **_Section_**, and  **_Key_**. To return or set a value for a registry entry, specify an empty string ("") for the  **_FileName_** argument. The **_Section_** argument should be the complete path to the registry subkey. The **_Key_** argument should be the name of an entry in the subkey specified by **_Section_**.

You can also set information in the Windows registry using the following  **PrivateProfileString** syntax.

 **System.PrivateProfileString**( **_FileName_**,  **_Section_**,  **_Key_**)  **=**_value_

The following example sets the DOC-PATH entry to "C:\My Documents" in the Options subkey for Office Word 2007 in the Windows registry.




```
Sub SetDocumentDirectory() 
 Dim strDocDirectory As String 
 strDocDirectory = "HKEY_CURRENT_USER\Software\Microsoft" _ 
 &amp; "\Office\10.0\Word\Options" 
 System.PrivateProfileString(FileName:="", _ 
 Section:=strDocDirectory, Key:="DOC-PATH") = "C:\My Documents" 
End Sub
```

