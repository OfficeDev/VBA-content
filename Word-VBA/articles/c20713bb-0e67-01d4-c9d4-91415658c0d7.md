
# Applying Formatting to Text

 **Last modified:** July 28, 2015

 _**Applies to:** Word 2013_

 **In this article**
 [Applying formatting to the selection](#sectionSection0)
 [Applying formatting to a range](#sectionSection1)
 [Inserting text and applying character and paragraph formatting](#sectionSection2)
 [Switching the space before a paragraph between 12 points and none](#sectionSection3)
 [Switching bold formatting on and off](#sectionSection4)
 [Increasing the left margin by 0.5 inch](#sectionSection5)


This topic includes Visual Basic examples related to the following tasks:

-  [Applying formatting to the selection](#Selection)
    
-  [Applying formatting to a range](#Range)
    
-  [Inserting text and applying character and paragraph formatting](#Inserting)
    
-  [Switching the space before a paragraph between 12 points and none](#TogglingSpace)
    
-  [Switching bold formatting on and off](#TogglingBold)
    
-  [Increasing the left margin by 0.5 inch](#Increasing)
    

## Applying formatting to the selection
<a name="sectionSection0"> </a>

The following example uses the  ** [Selection](d2362378-06a1-3a1a-2bd0-358f190eb6f3.md)**property to apply character and paragraph formatting to the selected text. Use the  ** [Font](c2a24190-62fa-09c4-7c47-90a7ecf20d97.md)**property to access character formatting properties and methods and the  ** [ParagraphFormat](3a3a3b4e-396f-fbe5-dc30-649ef7a9a8f9.md)**property to access paragraph formatting properties and methods.


```
Sub FormatSelection() 
 With Selection.Font 
 .Name = "Times New Roman" 
 .Size = 14 
 .AllCaps = True 
 End With 
 With Selection.ParagraphFormat 
 .LeftIndent = InchesToPoints(0.5) 
 .Space1 
 End With 
End Sub
```


## Applying formatting to a range
<a name="sectionSection1"> </a>

The following example defines a  ** [Range](15a7a1c4-5f3f-5b6e-60e9-29688de3f274.md)**object that refers to the first three paragraphs in the active document. The  **Range** is formatted by applying properties of the ** [Font](bc97f4df-fc81-d6c8-e99a-d50dc793b7ae.md)**object and the  ** [ParagraphFormat](712d754a-dc92-f1a3-531d-dfae74a42c23.md)**object.


```
Sub FormatRange() 
 Dim rngFormat As Range 
 Set rngFormat = ActiveDocument.Range( _ 
 Start:=ActiveDocument.Paragraphs(1).Range.Start, _ 
 End:=ActiveDocument.Paragraphs(3).Range.End) 
 With rngFormat 
 .Font.Name = "Arial" 
 .ParagraphFormat.Alignment = wdAlignParagraphJustify 
 End With 
End Sub
```


## Inserting text and applying character and paragraph formatting
<a name="sectionSection2"> </a>

The following example adds the word "Title" at the top of the current document. The first paragraph is center-aligned and one half-inch space is added after the paragraph. The word "Title" is formatted with 24-point Arial font.


```
Sub InsertFormatText() 
 Dim rngFormat As Range 
 Set rngFormat = ActiveDocument.Range(Start:=0, End:=0) 
 With rngFormat 
 .InsertAfter Text:="Title" 
 .InsertParagraphAfter 
 With .Font 
 .Name = "Tahoma" 
 .Size = 24 
 .Bold = True 
 End With 
 End With 
 With ActiveDocument.Paragraphs(1) 
 .Alignment = wdAlignParagraphCenter 
 .SpaceAfter = InchesToPoints(0.5) 
 End With 
End Sub
```


## Switching the space before a paragraph between 12 points and none
<a name="sectionSection3"> </a>

The following example toggles the space-before formatting of the first paragraph in the selection. The macro retrieves the current space before value, and if the value is 12 points, the space-before formatting is removed (the  ** [SpaceBefore](3e9cf50f-5e63-ea24-fe39-7fc9d8690bb4.md)**property is set to zero). If the space-before value is anything other than 12, the  **SpaceBefore** property is set to 12 points.


```
Sub ToggleParagraphSpace() 
 With Selection.Paragraphs(1) 
 If .SpaceBefore <> 0 Then 
 .SpaceBefore = 0 
 Else 
 .SpaceBefore = 6 
 End If 
 End With 
End Sub
```


## Switching bold formatting on and off
<a name="sectionSection4"> </a>

The following example toggles bold formatting of the selected text.


```
Sub ToggleBold() 
 Selection.Font.Bold = wdToggle 
End Sub
```


## Increasing the left margin by 0.5 inch
<a name="sectionSection5"> </a>

The following example increases the left and right margins by 0.5 inch. The  ** [PageSetup](1879d601-80ad-4fc0-1a87-92e999b59f88.md)** object contains all the page setup attributes of a document (such as left margin, bottom margin, and paper size) as properties. The ** [LeftMargin](873d6cf2-da9f-5d88-314f-9820284a54ee.md)**property is used to return and set the left margin setting. The  ** [RightMargin](abaabc8b-bb3f-fe68-ca35-d06f74d06791.md)**property is used to return and set the right margin setting.


```
Sub FormatMargins() 
 With ActiveDocument.PageSetup 
 .LeftMargin = .LeftMargin + InchesToPoints(0.5) 
 .RightMargin = .RightMargin + InchesToPoints(0.5) 
 End With 
End Sub
```

