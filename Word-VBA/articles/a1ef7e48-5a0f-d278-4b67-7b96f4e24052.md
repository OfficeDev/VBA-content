
# Working with the Selection Object

 **Last modified:** July 28, 2015

 _**Applies to:** Word 2013_

When you work on a document in Word, you usually select text and then perform an action, such as formatting the text or typing text. In Visual Basic, it is usually not necessary to select text before modifying the text. Instead, you create a  ** [Range](15a7a1c4-5f3f-5b6e-60e9-29688de3f274.md)** object that refers to a specific portion of the document. For information about defining **Range** objects, see [Working with Range objects](9e240aa7-8608-9d70-aee3-2e202687459e.md). However, when you want your code to respond to or change a selection, you can do so by using the  ** [Selection](7b574a91-c33e-ecfd-6783-6b7528b2ed8f.md)** object.

If text is not already selected, use the  **Select** method to select the text that is associated with a specific object and create a **Selection** object. For example, the following instruction selects the first word in the active document.



```
Sub SelectFirstWord() 
 ActiveDocument.Words(1).Select 
End Sub
```

For more information, see  [Selecting text in a document](1684b81f-caed-ea76-1378-580f6e34a1db.md).
If text is already selected, use the  ** [Selection](71938a78-36ae-07ba-496b-911bef746444.md)** property to return a **Selection** object that represents the current selection in a document. There can be only one **Selection** object per document, and it always accesses the current selection. The following example changes the formatting of the paragraphs in the current selection.



```
Sub FormatSelection() 
 Selection.Paragraphs.LeftIndent = InchesToPoints(0.5) 
End Sub
```

This example inserts the word "Hello" after the current selection.



```
Sub InsertTextAfterSelection() 
 Selection.InsertAfter Text:="Hello " 
End Sub
```

This example applies bold formatting to the selected text.



```
Sub BoldSelectedText() 
 Selection.Font.Bold = True 
End Sub
```

The macro recorder often creates a macro that uses the  **Selection** object. The following example was created using the macro recorder. This macro selects the first two words in the active document and applies bold formatting to them.



```
Sub Macro() 
 Selection.HomeKey Unit:=wdStory 
 Selection.MoveRight Unit:=wdWord, Count:=2, Extend:=wdExtend 
 Selection.Font.Bold = wdToggle 
End Sub
```

The following example accomplishes the same task without selecting the text or using the  **Selection** object.



```
Sub WorkingWithRanges() 
 ActiveDocument.Range(Start:=0, _ 
 End:=ActiveDocument.Words(2).End).Bold = True 
End Sub
```

