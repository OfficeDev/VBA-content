
# Displaying Built-in Word Dialog Boxes

 **Last modified:** July 28, 2015

 _**Applies to:** Word 2013_

 **In this article**
 [Showing a built-in dialog box](#sectionSection0)
 [Returning and changing dialog box settings](#sectionSection1)
 [Checking how a dialog box was closed](#sectionSection2)


This topic contains the following information and examples:

-  [Showing a built-in dialog box](#item1)
    
-  [Returning and changing dialog box settings](#item2)
    
-  [Checking how a dialog box was closed](#item4)
    

## Showing a built-in dialog box
<a name="sectionSection0"> </a>

You can display a built-in dialog box to get user input or to control Word by using Visual Basic for Applications (VBA). The  ** [Show](6b236493-342d-934b-f360-00b7846789e8.md)** method of the ** [Dialog](f90f6e6d-aaa0-c127-ab37-ca074144eff1.md)** object displays and executes any action taken in a built-in Word dialog box. To access a particular built-in Word dialog box, you specify a ** [WdWordDialog](af867214-8cd6-0477-6c88-2efe06589730.md)** constant with the ** [Dialogs](17acdfab-32d2-ddb8-04aa-692f9ffb20b8.md)** property. For example, the following macro instruction displays the **Open** dialog box ( **wdDialogFileOpen**).


```
Sub ShowOpenDialog() 
 Dialogs(wdDialogFileOpen).Show 
End Sub
```

If a file is selected and  **OK** is clicked, the file is opened (the action is executed). The following example displays the **Print** dialog box ( **wdDialogFilePrint**).




```
Sub ShowPrintDialog() 
 Dialogs(wdDialogFilePrint).Show 
End Sub
```

Set the  ** [DefaultTab](22de708e-fb23-b27a-00f0-dc43787c7eaf.md)** property to access a particular tab in a Word dialog box. The following example displays the **Page Border** tab in the **Borders and Shading** dialog box.




```
Sub ShowBorderDialog() 
 With Dialogs(wdDialogFormatBordersAndShading) 
 .DefaultTab = wdDialogFormatBordersAndShadingTabPageBorder 
 .Show 
 End With 
End Sub
```


 **Note**  You can also use the VBA properties in Word to display the user information without displaying the dialog box. The following example uses the  ** [UserName](96f5ffb6-a20d-96f0-e3a4-0ad2dd47bf99.md)** property for the ** [Application](d1cf6f8f-4e88-bf01-93b4-90a83f79cb44.md)** object to display the user name for the application without displaying the **User Information** dialog box.




```
Sub DisplayUserInfo() 
 MsgBox Application.UserName 
End Sub
```

If the user name is changed in the previous example, the change is not set in the dialog box. Use the  ** [Execute](7f7dce3a-40ef-988c-f5ea-06a25c0ccc4b.md)** method to execute the settings in a dialog box without displaying the dialog box. The following example displays the **User Information** dialog box, and if the name is not an empty string, the settings are set in the dialog box by using the **Execute** method.




```
Sub ShowAndSetUserInfoDialogBox() 
 With Dialogs(wdDialogToolsOptionsUserInfo) 
 .Display 
 If .Name <> "" Then .Execute 
 End With 
End Sub
```


 **Note**  Use the VBA properties and methods in Word to set the user information without displaying the dialog box. The following code example changes the user name through the  **UserName** property of the **Application** object, and then it displays the **User Information** dialog box to show that the change has been made. Note that displaying the dialog box is not necessary to change the value of a dialog box.




```
Sub SetUserName() 
 Application.UserName = "Jeff Smith" 
 Dialogs(wdDialogToolsOptionsUserInfo).Display 
End Sub
```


## Returning and changing dialog box settings
<a name="sectionSection1"> </a>

It is not very efficient to use a  **Dialog** object to return or change a value for a dialog box when you can return or change it using a property or method. Also, in most, if not all, cases, when VBA code is used in place of accessing the **Dialog** object, code is simpler and shorter. Therefore, the following examples also include examples that use corresponding VBA properties to perform the same tasks.

Prior to returning or changing a dialog box setting using the  ** [Dialog](f90f6e6d-aaa0-c127-ab37-ca074144eff1.md)** object, you need to identify the individual dialog box. This is done by using the ** [Dialogs](1c734758-d00a-31d7-d4f7-bf8c406ab6b5.md)** property with a ** [WdWordDialog](af867214-8cd6-0477-6c88-2efe06589730.md)** constant. After you have instantiated a **Dialog** object, you can return or set options in the dialog box. The following example displays the right indent from the **Paragraphs** dialog box.




```
Sub ShowRightIndent() 
 Dim dlgParagraph As Dialog 
 Set dlgParagraph = Dialogs(wdDialogFormatParagraph) 
 MsgBox "Right indent = " &amp; dlgParagraph.RightIndent 
End Sub
```


 **Note**  You can use the VBA properties and methods of Word to display the right indent setting for the paragraph. The following example uses the  ** [RightIndent](de69209e-d88d-d367-9d84-94faa07a30bd.md)** property of the ** [ParagraphFormat](712d754a-dc92-f1a3-531d-dfae74a42c23.md)** object to display the right indent for the paragraph at the insertion point position.




```
Sub ShowRightIndexForSelectedParagraph() 
 MsgBox Selection.ParagraphFormat.RightIndent 
End Sub
```

Just as you can return dialog box settings, you can also set dialog box settings. The following example marks the  **Keep with next** check box in the **Paragraph** dialog box.




```
Sub SetKeepWithNext() 
 With Dialogs(wdDialogFormatParagraph) 
 .KeepWithNext = 1 
 .Execute 
 End With 
End Sub
```


 **Note**  You can also use the VBA properties and methods to change the right indent for the paragraph. The following example uses the  ** [KeepWithNext](5fc8ad97-d839-7837-04c7-dac2efe1d1c2.md)** property of the **ParagraphFormat** object to keep the selected paragraph with the following paragraph.




```
Sub SetKeepWithNextForSelectedParagraph() 
 Selection.ParagraphFormat.KeepWithNext = True 
End Sub
```


 **Note**  Use the  ** [Update](7adf7403-77eb-85da-8a5a-092d1c8c548f.md)** method to ensure that the dialog box values reflect the current values. It may be necessary to use the **Update** method if you define a dialog box variable early in your macro and later want to return or change the current settings.


## Checking how a dialog box was closed
<a name="sectionSection2"> </a>

The value returned by the  **Show** and **Display** methods indicates which button was clicked to close the dialog box. The following example displays the **Break** dialog box, and if **OK** is clicked, a message is displayed on the status bar.


```
Sub DialogBoxButtons() 
 If Dialogs(wdDialogInsertBreak).Show = -1 Then 
 StatusBar = "Break inserted" 
 End If 
End Sub
```

The following table describes the return values associated with buttons in dialogs boxes.



|**Return value**|**Description**|
|:-----|:-----|
|-2|The  **Close** button.|
|-1|The  **OK** button.|
|0 (zero)|The  **Cancel** button.|
|> 0 (zero)|A command button: 1 is the first button, 2 is the second button, and so on.|
