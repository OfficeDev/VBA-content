
# Finding and Replacing Text or Formatting

 **Last modified:** July 28, 2015

 _**Applies to:** Word 2013_

 **In this article**
 [Finding text and selecting it](#sectionSection0)
 [Finding text without changing the selection](#sectionSection1)
 [Using the Replacement object](#sectionSection2)


Finding and replacing is exposed by the  ** [Find](da822788-cad5-992a-a835-18cc574cc324.md)**and  ** [Replacement](5d9615e4-f6ef-af5f-6e45-c382a88395c9.md)**objects. The  **Find** object is available from the ** [Selection](7b574a91-c33e-ecfd-6783-6b7528b2ed8f.md)**object and the  ** [Range](15a7a1c4-5f3f-5b6e-60e9-29688de3f274.md)**object. The find action differs slightly depending upon whether you access the  **Find** object from the **Selection** object or the **Range** object.

## Finding text and selecting it
<a name="sectionSection0"> </a>

If the  **Find** object is accessed from the **Selection** object, the selection is changed when the find criteria is found. The following example selects the next occurrence of the word "Hello." If the end of the document is reached before the word "Hello" is found, the search is stopped.


```
With Selection.Find 
 .Forward = True 
 .Wrap = wdFindStop 
 .Text = "Hello" 
 .Execute 
End With
```

The  **Find** object includes properties that relate to the options in the **Find and Replace** dialog box. You can set the individual properties of the **Find** object or use arguments with the ** [Execute](3b607955-0e82-aa13-dad1-7a5069a57b9d.md)**method, as shown in the following example.




```
Selection.Find.Execute FindText:="Hello", _ 
 Forward:=True, Wrap:=wdFindStop
```


## Finding text without changing the selection
<a name="sectionSection1"> </a>

If the  **Find** object is accessed from a **Range** object, the selection is not changed but the **Range** is redefined when the find criteria is found. The following example locates the first occurrence of the word "blue" in the active document. If the find operation is successful, the range is redefined and bold formatting is applied to the word "blue."


```
With ActiveDocument.Content.Find 
 .Text = "blue" 
 .Forward = True 
 .Execute 
 If .Found = True Then .Parent.Bold = True 
End With
```

The following example performs the same result as the previous example, using arguments of the  **Execute** method.




```
Set myRange = ActiveDocument.Content 
myRange.Find.Execute FindText:="blue", Forward:=True 
If myRange.Find.Found = True Then myRange.Bold = True
```


## Using the Replacement object
<a name="sectionSection2"> </a>

The  **Replacement** object represents the replace criteria for a find and replace operation. The properties and methods of the **Replacement** object correspond to the options in the **Find and Replace** dialog box ( **Edit** menu).

The  **Replacement** object is available from the **Find** object. The following example replaces all occurrences of the word "hi" with "hello". The selection changes when the find criteria is found because the **Find** object is accessed from the **Selection** object.




```
With Selection.Find 
 .ClearFormatting 
 .Text = "hi" 
 .Replacement.ClearFormatting 
 .Replacement.Text = "hello" 
 .Execute Replace:=wdReplaceAll, Forward:=True, _ 
 Wrap:=wdFindContinue 
End With
```

The following example removes bold formatting in the active document. The  ** [Bold](84e8d6b7-1be4-e1c5-c246-a6370011bc8b.md)**property is  **True** for the **Find** object and **False** for the **Replacement** object. To find and replace formatting, set the find and replace text to empty strings ("") and set the **_Format_** argument of the **Execute** method to **True**. The selection remains unchanged because the  **Find** object is accessed from a **Range** object (the ** [Content](80578329-a648-1d4b-f83d-4b2d289813fb.md)**property returns a  **Range** object).




```
With ActiveDocument.Content.Find 
 .ClearFormatting 
 .Font.Bold = True 
 With .Replacement 
 .ClearFormatting 
 .Font.Bold = False 
 End With 
 .Execute FindText:="", ReplaceWith:="", _ 
 Format:=True, Replace:=wdReplaceAll 
End With
```

