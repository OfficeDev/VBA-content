
# Working with Charts

 **Last modified:** July 28, 2015

 _**Applies to:** Word 2013_

In Word 2007 Service Pack 2 (SP2) and later, you can programmatically access and manipulate charts using the VBA object model in Word. The chart object in Word is drawn by the same shared Office drawing layer implementation used by Excel, so if you are familiar with the charting object model in Excel, you can easily migrate Excel VBA code that manipulates charts into Word VBA code.


## Using the Chart Object

In Word, a chart is represented by a  [Chart](366a825e-0daf-dbb7-b6f2-e7ce1a5ee2ef.md) object. The [Chart](366a825e-0daf-dbb7-b6f2-e7ce1a5ee2ef.md) object is contained by an InlineShape or Shape. You can use either the [InlineShapes](88c632b2-80de-c96a-8879-a98461b38bd0.md) collection or the [Shapes](0907eed3-886e-8e73-0e5e-71f4b37ddd5b.md) collection of the [Document](8d83487a-2345-a036-a916-971c9db5b7fb.md) object to add new or access existing charts. You use the [AddChart](http://msdn.microsoft.com/library/1b168e7b-543a-a817-51b0-8171beecc946%28Office.15%29.aspx) method for both collections, specifying the chart type and location within the document, to add a new chart.

You can use the  [HasChart](f8b88eef-ec41-fc03-f58b-e346d240a121.md) property to determine if an [InlineShape](a8fd110a-4aa7-c4b9-1559-32022787d955.md) object or [Shape](604029ce-9b2f-9748-5d4e-b458796fa2f0.md) object contains a chart. If [HasChart](f8b88eef-ec41-fc03-f58b-e346d240a121.md) returns True, you can then use the [Chart](33d577fe-58b0-8e2f-a859-5bd3b34ed892.md) property to get a reference to a [Chart](366a825e-0daf-dbb7-b6f2-e7ce1a5ee2ef.md) object that represents the chart. At this point, the implementation is virtually identical as that of Excel and VBA code can be transferred between the two programs in most cases.

For example, the following VBA code example adds a new 2-D stacked column chart to the active worksheet in Excel and sets the chart's source data to the range A1:C3 from the Sheet1 worksheet.




```
Sub AddChart_Excel()
    Dim objShape As Shape
    
    ' Create a chart and return a Shape object reference.
    ' The Shape object reference contains the chart.
    Set objShape = ActiveSheet.Shapes.AddChart(XlChartType.xlColumnStacked100)
    
    ' Ensure the Shape object contains a chart. If so,
    ' set the source data for the chart to the range A1:C3.
    If objShape.HasChart Then
        objShape.Chart.SetSourceData Source:=Range("'Sheet1'!$A$1:$C$3")
    End If
End Sub

```

By comparison, the following VBA code example adds a new 2-D stacked column chart to the active document in and sets the chart's source data to the range A1:C3 from the chart data associated with the chart. 




```
Sub AddChart_Word()
    Dim objShape As InlineShape
    
    ' Create a chart and return a Shape object reference.
    ' The Shape object reference contains the chart.
    Set objShape = ActiveDocument.InlineShapes.AddChart(XlChartType.xlColumnStacked100)
    
    ' Ensure the Shape object contains a chart. If so,
    ' set the source data for the chart to the range A1:C3.
    If objShape.HasChart Then
        objShape.Chart.SetSourceData Source:="'Sheet1'!$A$1:$C$3"
    End If
End Sub
```


## Key Differences Between the Chart object in Word and the ChartObject object in Excel

Even though how you work with charts between Excel and Word is nearly identical in most cases, it is helpful to identify important areas where the two implementations differ: 




- Programmatically creating or manipulating a  [ChartData](323ee62c-9b70-8280-d448-79cf4d2b6953.md) object in Word requires Excel to run.
    
- Chart properties and methods for manipulating the chart sheet are not implemented. The concept of a chart sheet is specific to Excel. Chart sheets are not used in Word, so methods and properties used to reference or manipulate a chart sheet have been disabled for those applications.
    
- Properties and methods that, in Excel normally take a  [Range](http://msdn.microsoft.com/library/b8207778-0dcc-4570-1234-f130532cc8cd%28Office.15%29.aspx) object reference now take a range address in Word. The [Range](15a7a1c4-5f3f-5b6e-60e9-29688de3f274.md) object in Word is different than the [Range](http://msdn.microsoft.com/library/b8207778-0dcc-4570-1234-f130532cc8cd%28Office.15%29.aspx) object in Excel. To prevent confusion, the charting object model in Word accepts range address strings, such as "='Sheet1'!$A$1:$D$5", in those properties and methods (such as the [SetSourceData](8c5b056a-6680-7e4e-ce67-a3b76b2d7d25.md) method of the Chart object) that accept Range objects in Excel.
    
- A new object,  [ChartData](323ee62c-9b70-8280-d448-79cf4d2b6953.md), has been added to the VBA object models for Word to provide access to the underlying linked or embedded data for a chart. Each chart has, associated with it, the data used to draw the chart in Word. The chart data can either be linked from an external Excel workbook, or embedded as part of the chart itself. The  [ChartData](323ee62c-9b70-8280-d448-79cf4d2b6953.md) object encapsulates access to the data for a given chart in Word. For example, the following VBA code example displays, and then minimizes, the chart data for each chart contained by the active document in Word.
    



```
Sub ShowWorkbook_Word() 
    Dim objShape As InlineShape 
     
    ' Iterates each inline shape in the active document. 
    ' If the inline shape contains a chart, then display the 
    ' data associated with that chart and minimize the application 
    ' used to display the data. 
    For Each objShape In ActiveDocument.InlineShapes 
        If objShape.HasChart Then 
 
            ' Activate the topmost window of the application used to 
            ' display the data for the chart. 
            objShape.Chart.ChartData.Activate 
             
            ' Minimize the application used to display the data for 
            ' the chart. 
            objShape.Chart.ChartData.Workbook.Application.WindowState = -4140 
        End If 
    Next 
End Sub 
```

