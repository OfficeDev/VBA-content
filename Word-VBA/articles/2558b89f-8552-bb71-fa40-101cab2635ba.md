
# BuildingBlock Object (Word)

 **Last modified:** July 28, 2015

Represents a building block in a template. A building block is pre-built content, similar to autotext, that may contain text, images, and formatting.

## Remarks

Each  **BuildingBlock** object is a member of the ** [BuildingBlocks](be5bba4a-b06c-0074-20bd-bbeb40e03d1c.md)** and ** [BuildingBlockEntries](9c5946e9-947d-7284-ab16-b570bf7f0ff3.md)** collections. Building blocks are stored in Microsoft Word templates. Therefore, to access the building blocks available for a document, you need to access an attached template. Built-in building blocks are stored in the template named "Building Blocks.dotx".

 Use the ** [Item](8380b6d9-fefa-dd3e-fc30-156b3a8be47a.md)** method of the collection or the **BuildingBlocks** collection to return an individual building block. The following example accesses the first building block in the first template in the ** [Templates](de62f768-011a-7446-48c3-1c4512da5f7c.md)** collection.




```
Dim objTemplate As Template 
Dim objBB As BuildingBlock 
 
Set objTemplate = Templates(1) 
Set objBB = objTemplate.BuildingBlockEntries.Item(1)
```


 **Note**  Depending on how you access the collection, the collection returned may change. For example, if you access a collection of building blocks with a type of  **wdTypeAutoText** with a category of "General", the returned collection may be different from the collection returned if you access a collection of building blocks with a type of **wdTypeAutoText** with a category of "Custom". It is also different from the collection returned if you access the collection of building blocks with a type of **wdTypeCustomAutoText** with a category of "General". Therefore, the first item in a collection accessed from the **BuildingBlockEntries** collection may be different from the first item in the collection accessed from the **BuildingBlocks** collection.

To create a new building block, you can use the  **Add** method for either the **BuildingBlockEntries** collection or the **BuildingBlocks** collection. However, the recommended way to create a new building block is by using the ** [Add](09578906-ea6d-9475-e026-b9dc437f451b.md)** method for the **BuildingBlockEntries** collection. The following example adds the selected text to the watermarks building block gallery of the first template in the ** [Templates](de62f768-011a-7446-48c3-1c4512da5f7c.md)** collection.




```
Dim objTemplate As Template 
Dim objBB As BuildingBlock 
 
Set objTemplate = Templates(1) 
 
Set objBB = objTemplate.BuildingBlockEntries _ 
 .Add(Name:="New Building Block Entry", _ 
 Type:=wdTypeWatermarks, _ 
 Category:="General", _ 
 Range:=Selection.Range)
```

Use the  ** [Insert](e2f3fd61-624b-fd18-3b5a-2c9f16fa6bd2.md)** method to insert a new building block into a document. The following example inserts the first building block in the first template into the active document at the Insertion Point.




```
Dim objTemplate As Template 
Dim objBB As BuildingBlock 
 
Set objTemplate = Templates(1) 
Set objBB = objTemplate.BuildingBlockEntries.Item(1) 
 
objBB.Insert Selection.Range
```

Use the  ** [Delete](8aac9cbb-67d6-e915-82ac-e3abd6f15985.md)** method to remove a building block from a template. The following example deletes the first building block from the first template in the **Templates** collection.




```
Dim objTemplate As Template 
 
Set objTemplate = Templates(1) 
 
objTemplate.BuildingBlockEntries(1).Delete
```

 Building blocks are organized by category and type. Use the ** [BuildingBlockTypes](fb179437-b736-dd99-3aea-125346aa7a3d.md)** collection to access individual ** [BuildingBlockType](e4f971da-f052-b6a2-db40-2f4cd0cfd6be.md)** objects. Use the ** [Categories](f5f5081d-4309-6617-28da-c369c1fe690c.md)** collection to access individual ** [Category](43e54bff-85ba-af64-eebe-033adbcc0873.md)** objects. Then use the **BuildingBlocks** propery to access the **BuildingBlocks** collection for a **Category** object. The following example prints the type and category names of all the building blocks in the first template to the **Immediate Window**. (This example assumes that the  **Immediate Window** is visible.)




```
Dim objTemplate As Template 
Dim objBBT As BuildingBlockType 
Dim objCat As Category 
Dim intCount As Integer 
Dim intCountCat As Integer 
 
Set objTemplate = Templates(1) 
 
For intCount = 1 To objTemplate.BuildingBlockTypes.Count 
 Set objBBT = objTemplate.BuildingBlockTypes(intCount) 
 If objBBT.Categories.Count > 0 Then 
 Debug.Print objBBT.Name 
 For intCountCat = 1 To objBBT.Categories.Count 
 Set objCat = objBBT.Categories(intCountCat) 
 Debug.Print vbTab &amp; objCat.Name 
 Next 
 End If 
Next
```

Each building block has properties that contain information that applies uniquely to it, such as  ** [Name](088e3d70-8fbb-2d3a-5b2a-09e7a2f79cc9.md)**,  ** [Description](af05afab-c9fd-7758-b54c-52d0ad51fc52.md)**,  ** [Type](bcb4b2ea-e2e8-3198-0ad2-18849dd16cc8.md)**, and  ** [Value](7b6c7ebf-62fe-3215-9b34-85c32ae3ff8e.md)**.

For more information about building blocks, see  [Working with Building Blocks](c32a8972-a6fc-bb66-b62a-039b88580b37.md).


## See also


#### Concepts


 [Word Object Model Reference](be452561-b436-bb9b-6f94-3faa9a74a6fd.md)
#### Other resources


 [BuildingBlock Object Members](56fb71a6-3ee7-77ee-eaa3-17f16692aa18.md)
