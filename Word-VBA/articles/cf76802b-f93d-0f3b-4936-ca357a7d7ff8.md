
# XMLMapping Object (Word)

 **Last modified:** July 28, 2015

Represents the XML mapping on a  ** [ContentControl](783dec26-9b63-11f8-6187-985f9c815f27.md)** object between custom XML and a content control. An XML mapping is a link between the text in a content control and an XML element in the custom XML data store for this document.

## Remarks

Use the  ** [SetMapping](0d33be39-f355-7a59-802c-33d031485a0e.md)** method to add or change the XML mapping for a content control using an XPath string. The following example sets the built-in document property for the document author, inserts a new content control into the active document, and then sets the XML mapping for the control to the built-in document property.


```
Dim objcc As ContentControl 
Dim objMap As XMLMapping 
Dim blnMap As Boolean 
 
ActiveDocument.BuiltInDocumentProperties("Author").Value = "David Jaffe" 
 
Set objcc = ActiveDocument.ContentControls.Add _ 
 (wdContentControlDate, ActiveDocument.Paragraphs(1).Range) 
 
Set objMap = objcc.XMLMapping 
blnMap = objMap.SetMapping(XPath:="/ns1:coreProperties[1]/ns0:createdate[1]") 
 
If blnMap = False Then 
 MsgBox "Unable to map the content control." 
End If
```

Use the  ** [SetMappingByNode](8eab3471-e1dc-f7ec-9b45-9fb459088190.md)** method to add or change the XML mapping for a content control using a **CustomXMLNode** object. The following example does the same thing as the previous example, but uses the **SetMappingByNode** method.




```
Dim objcc As ContentControl 
Dim objNode As CustomXMLNode 
Dim objMap As XMLMapping 
Dim blnMap As Boolean 
 
ActiveDocument.BuiltInDocumentProperties("Author").Value = "David Jaffe" 
 
Set objcc = ActiveDocument.ContentControls.Add _ 
 (wdContentControlDate, ActiveDocument.Paragraphs(1).Range) 
 
Set objNode = ActiveDocument.CustomXMLParts.SelectByNamespace _ 
 ("http://schemas.openxmlformats.org/package/2006/metadata/core-properties") _ 
 (1).DocumentElement.ChildNodes(1) 
 
Set objMap = objcc.XMLMapping 
blnMap = objMap.SetMappingByNode(objNode)
```

The following example creates a new  **CustomXMLPart** object, loads custom XML into it, and then creates two new content controls and maps each to a different XML element within the custom XML.




```
Dim objRange As Range 
Dim objCustomPart As CustomXMLPart 
Dim objCustomControl As ContentControl 
Dim objCustomNode As CustomXMLNode 
 
Set objCustomPart = ActiveDocument.CustomXMLParts.Add 
objCustomPart.LoadXML ("<books><book><author>Matt Hink</author>" &amp; _ 
 "<title>Migration Paths of the Red Breasted Robin</title>" &amp; _ 
 "<genre>non-fiction</genre><price>29.95</price>" &amp; _ 
 "<pub_date>2/1/2007</pub_date><abstract>You see them in " &amp; _ 
 "the spring outside your windows. You hear their lovely " &amp; _ 
 "songs wafting in the warm spring air. Now follow the path " &amp; _ 
 "of the red breasted robin as it migrates to warmer climes " &amp; _ 
 "in the fall, and then back to your back yard in the spring." &amp; _ 
 "</abstract></book></books>") 
 
ActiveDocument.Range.InsertParagraphBefore 
Set objRange = ActiveDocument.Paragraphs(1).Range 
Set objCustomNode = objCustomPart.SelectSingleNode _ 
 ("/books/book/title") 
Set objCustomControl = ActiveDocument.ContentControls _ 
 .Add(wdContentControlText, objRange) 
objCustomControl.XMLMapping.SetMappingByNode objCustomNode 
 
objRange.InsertParagraphAfter 
Set objRange = ActiveDocument.Paragraphs(2).Range 
Set objCustomNode = objCustomPart.SelectSingleNode _ 
 ("/books/book/abstract") 
Set objCustomControl = ActiveDocument.ContentControls _ 
 .Add(wdContentControlText, objRange) 
objCustomControl.XMLMapping.SetMappingByNode objCustomNode 
 
MsgBox objCustomControl.XMLMapping.IsMapped
```

Use the  ** [Delete](72864b8d-5b59-66c3-b9e3-b970f8adf7aa.md)** method to remove the XML mapping for a content control. Deleting the XML mapping for a content control deletes only the connection between the content control and the XML data. Both the content control and the XML data remain in the document. The following example deletes the XML mapping for all content controls in the active document that are currently mapped.




```
Dim objCC As ContentControl 
 
For Each objCC In ActiveDocument.ContentControls 
 If objCC.XMLMapping.IsMapped Then 
 objCC.XMLMapping.Delete 
 End If 
Next
```

Use the  ** [IsMapped](e78ae752-1f8f-5f18-0755-97ec10ab68ec.md)** property to determine if a content control is mapped to an XML node in the document's data store. The following example deletes the XML mapping for all mapped content controls in the active document.




```
Dim objCC As ContentControl 
 
For Each objCC In ActiveDocument.ContentControls 
 If objCC.XMLMapping.IsMapped Then 
 objCC.XMLMapping.Delete 
 End If 
Next
```

Use the  ** [CustomXMLNode](c28e3a1e-1bc3-fbe7-7ff8-78adef326bbd.md)** property to access the XML node to which a content control maps. Use the ** [CustomXMLPart](a9eac7d6-0088-7251-e0b2-fef529fee278.md)** property to access the XML part to which a content control maps. For more information about working with **CustomXMLNode** and **CustomXMLPart** objects, see the respective object topics.


## See also


#### Concepts


 [Word Object Model Reference](be452561-b436-bb9b-6f94-3faa9a74a6fd.md)
#### Other resources


 [XMLMapping Object Members](8fb27e7a-1d02-4754-87ca-f117cc67cdff.md)
