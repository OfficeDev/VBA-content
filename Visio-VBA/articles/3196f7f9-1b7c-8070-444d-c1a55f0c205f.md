
# Page.GetShapesLinkedToData Method (Visio)

 **Last modified:** July 28, 2015

 _**Applies to:** Visio 2013 Preview_

 **In this article**
 [Syntax](#sectionSection1)
 [Remarks](#sectionSection2)
 [Example](#sectionSection3)


Returns an array of all shapes on the active page linked to data in the specified data recordset.

 **Note**  This Visio object or member is available only to licensed users of Visio Professional 2013.


## Syntax
<a name="sectionSection1"> </a>

 _expression_. **GetShapesLinkedToData**( **_DataRecordsetID_**,  **_ShapeIDs()_**)

 _expression_An expression that returns a  **Page** object.


### Parameters



|**Name**|**Required/Optional**|**Data Type**|**Description**|
|:-----|:-----|:-----|:-----|
|DataRecordsetID|Required| **Long**|The ID of a data recordset contained in the current document.|
|ShapeIDs()|Required| **Long**|Out parameter. An array of type  **Long** that the method will return filled with the shape IDs of shapes on the page linked to the data recordset specified in DataRecordsetID.|

### Return Value

Nothing


## Remarks
<a name="sectionSection2"> </a>

For the ShapeIDs() parameter, pass an empty, dimensionless array of type  **Long**. If there are no linked shapes on the page,  **GetShapesLinkedToData** will return an empty array.

To determine the specific data row in the data recordset shapes are linked to, use the  ** [Page.GetShapesLinkedToDataRow ](d305eccc-4121-be3a-a389-f50234e526f1.md)**method.


## Example
<a name="sectionSection3"> </a>

The following Microsoft Visual Basic for Applications (VBA) macro shows how to use the  **GetShapesLinkedToData** method to determine the shape IDs of the shapes on the page linked to data in the data recordset most recently added to the **DataRecordsets** collection of the current document. It prints the shape IDs in the **Immediate** window.

Before running this macro, use the  ** [DataRecordsets.Add](9eb136ce-d543-75c3-3a72-cb23dfc8df78.md)** method or another means to add at least one data recordset to the **DataRecordsets** collection, and make sure there is at least one shape on the active page linked to data in the data recordset.




```
Public Sub GetShapesLinkedToData_Example() 
 
    Dim vsoDataRecordset As Visio.DataRecordset 
    Dim intRecordsetCount As Integer 
    Dim alngShapeIDs() As Long 
    Dim intArrayCounter As Integer 
     
    intRecordsetCount = Visio.ActiveDocument.DataRecordsets.Count 
    Set vsoDataRecordset = Visio.ActiveDocument.DataRecordsets(intRecordsetCount) 
     
    ActivePage.GetShapesLinkedToData vsoDataRecordset.ID, alngShapeIDs 
     
    For intArrayCounter = LBound(alngShapeIDs) To UBound(alngShapeIDs) 
        Debug.Print alngShapeIDs(intArrayCounter) 
    Next 
     
End Sub
```

