
# Rules.Create Method (Outlook)

 **Last modified:** July 28, 2015

 **In this article**
 [Syntax](#sectionSection0)
 [Remarks](#sectionSection1)
 [Example](#sectionSection2)


Creates a  ** [Rule](ea2ddbcc-fd65-a636-c6da-79950033f385.md)** object with the name specified by _Name_ and the type of rule specified by _RuleType_.


## Syntax
<a name="sectionSection0"> </a>

 _expression_. **Create**( **_Name_**,  **_RuleType_**)

 _expression_A variable that represents a  **Rules** object.


### Parameters



|**Name**|**Required/Optional**|**Data Type**|**Description**|
|:-----|:-----|:-----|:-----|
|Name|Required| **String**|A string identifier for the rule, which will be represented by  ** [Rule.Name](6c559ffe-b25c-ff49-31d1-1fd44935a8f3.md)** after rule creation. Names of rules in a collection are not unique.|
|RuleType|Required| ** [OlRuleType](a9ef16ad-78cf-8c26-0897-39a0bf1a25b1.md)**|A constant in the  **OlRuleType** enumeration that determines whether the rule is applied on sending or receiving a message.|

### Return Value

A  **Rule** object that represents the newly created rule.


## Remarks
<a name="sectionSection1"> </a>

The  _RuleType_ parameter of the added rule determines valid rule actions, rule conditions, and rule exception conditions that can be associated with the **Rule** object.

When a rule is added to the collection, the  ** [Rule.ExecutionOrder](070d50ca-4b0b-5629-1609-81ab8a3620d1.md)** of the new rule is 1. The **ExecutionOrder** of other rules in the collection is incremented by 1.


## Example
<a name="sectionSection2"> </a>

The following code sample in Visual Basic for Applicatons (VBA) uses the Rules object model to create a rule. The code sample uses the  ** [RuleAction](6451788f-e5ed-239c-a34d-b564b52d8955.md)** and ** [RuleCondition](e03f91c2-2c08-b036-104a-d6246f28bc2d.md)** objects to specify a rule that forwards messages from a specific sender to a specific folder, unless the message contains certain terms in the subject. Note that the code sample assumes that there already exists a folder "Dan" under the Inbox.


```
Sub CreateRule() 
 
 Dim colRules As Outlook.Rules 
 
 Dim oRule As Outlook.Rule 
 
 Dim colRuleActions As Outlook.RuleActions 
 
 Dim oMoveRuleAction As Outlook.MoveOrCopyRuleAction 
 
 Dim oFromCondition As Outlook.ToOrFromRuleCondition 
 
 Dim oExceptSubject As Outlook.TextRuleCondition 
 
 Dim oInbox As Outlook.Folder 
 
 Dim oMoveTarget As Outlook.Folder 
 
 
 
 'Specify target folder for rule move action 
 
 Set oInbox = Application.Session.GetDefaultFolder(olFolderInbox) 
 
 'Assume that target folder already exists 
 
 Set oMoveTarget = oInbox.Folders("Dan") 
 
 
 
 'Get Rules from Session.DefaultStore object 
 
 Set colRules = Application.Session.DefaultStore.GetRules() 
 
 
 
 'Create the rule by adding a Receive Rule to Rules collection 
 
 Set oRule = colRules.Create("Dan's rule", olRuleReceive) 
 
 
 
 'Specify the condition in a ToOrFromRuleCondition object 
 
 'Condition is if the message is sent by "DanWilson" 
 
 Set oFromCondition = oRule.Conditions.From 
 
 With oFromCondition 
 
 .Enabled = True 
 
 .Recipients.Add ("DanWilson") 
 
 .Recipients.ResolveAll 
 
 End With 
 
 
 
 'Specify the action in a MoveOrCopyRuleAction object 
 
 'Action is to move the message to the target folder 
 
 Set oMoveRuleAction = oRule.Actions.MoveToFolder 
 
 With oMoveRuleAction 
 
 .Enabled = True 
 
 .Folder = oMoveTarget 
 
 End With 
 
 
 
 'Specify the exception condition for the subject in a TextRuleCondition object 
 
 'Exception condition is if the subject contains "fun" or "chat" 
 
 Set oExceptSubject = _ 
 
 oRule.Exceptions.Subject 
 
 With oExceptSubject 
 
 .Enabled = True 
 
 .Text = Array("fun", "chat") 
 
 End With 
 
 
 
 'Update the server and display progress dialog 
 
 colRules.Save 
 
End Sub 
 

```


## See also
<a name="sectionSection2"> </a>


#### Concepts


 [Rules Object](dd41b4de-bf5f-5532-46c9-394a5d078bec.md)
#### Other resources


 [Rules Object Members](39fb5418-ff5a-1714-d3b5-07cc28893821.md)
