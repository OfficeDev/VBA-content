
# Display Specific Modules in the Navigation Pane

 **Last modified:** July 28, 2015

 _**Applies to:** Outlook 2013_

The  ** [CurrentModule](df7086b3-4174-839f-0756-a5201379ed92.md)** property of the ** [NavigationPane](b6538c72-6115-99fc-c926-e0532a747823.md)** object, in Microsoft Outlook, determines which navigation module is currently displayed in the Navigation Pane. You can retrieve a reference to a given ** [NavigationModule](76565eaf-1e64-f5d4-b90f-ba156863802c.md)** object from a **NavigationPane** object by either enumerating the ** [NavigationModules](4b0743d3-0a21-488c-27b2-31ae07129a61.md)** collection or by using the ** [GetNavigationModule](7c1a1313-94a4-fa68-7e70-66d85496fec0.md)** method of the **NavigationModules** collection.

The following sample sets the  **Mail** navigation module as the currently selected navigation module if any navigation module is selected, either programmatically or by user action, in the Navigation Pane. The sample performs the following actions:

1. The sample first obtains a reference to the  **NavigationPane** object for the active explorer when the ** [Startup](d4724d96-2572-b1e3-e202-0bfffb5cf7d5.md)** event of the ** [Application](797003e7-ecd1-eccb-eaaf-32d6ddde8348.md)** object is raised and assigns it to `objPane`, so the  ** [ModuleSwitch](63ecb01e-56e2-cfa8-0481-b81761f6ab5c.md)** event of the **NavigationPane** object can be detected.
    
2. When the  **ModuleSwitch** event of the **NavigationPane** occurs, the sample then checks the ** [NavigationModuleType](ee1fc78a-9720-c8d0-964c-0178ddbe8af6.md)** property of the **NavigationModule** object reference in the _CurrentModule_ parameter of the **ModuleSwitch** event.
    
3. If the  **NavigationModuleType** property of the currently selected **NavigationModule** object is set to **olModuleMail**, the sample uses the  **GetNavigationModule** method of the **NavigationModules** collection for the **NavigationPane** object to attempt to retrieve a ** [MailModule](df20efe5-be5c-952d-c6b7-20c20a83fda0.md)** object. If successful, the sample finally sets the **CurrentModule** property of the **NavigationPane** object to the retrieved **MailModule** object reference.
    



```
Dim WithEvents objPane As NavigationPane 
 
Private Sub Application_Startup() 
 ' Get the NavigationPane object for the 
 ' currently displayed Explorer object. 
 Set objPane = Application.ActiveExplorer.NavigationPane 
End Sub 
 
Private Sub objPane_ModuleSwitch(ByVal CurrentModule As NavigationModule) 
 Dim objModule As MailModule 
 
 If CurrentModule.NavigationModuleType <> olModuleMail Then 
 ' Use the GetModule method to obtain a MailModule from 
 ' the current NavigationPane object. 
 Set objModule = objPane.Modules.GetNavigationModule(olModuleMail) 
 
 ' Set the CurrentModule property to the MailModule. 
 Set objPane.CurrentModule = objModule 
 End If 
End Sub 

```

