
# Using Outlook Visual Basic for Applications to Respond to Outlook Events

You write an event procedure (also known as an event handler) to respond to events that occur in Microsoft Outlook. For example, you can write an event procedure that automatically maximizes the explorer window when Outlook starts.

Events are associated with particular objects. The  [Application](797003e7-ecd1-eccb-eaaf-32d6ddde8348.md) object is the topmost object, and is always available (that is, it does not have to be created). You can add an **Application** event procedure in the **ThisOutlookSession** module window simply by selecting **Application** in the left list and then selecting the event in the right list.

Adding an event handler for objects other than the  **Application** object requires a few additional steps.

First, you must declare a variable using the  **WithEvents** keyword to identify the object whose event you want to handle. For example, to declare a variable representing the [OutlookBarPane](f8e6aa05-7a66-64f2-5a6a-ea639b6bbc59.md) object, you would add the following to a code module.



```vb
Dim WithEvents myOlBar as Outlook.OutlookBarPane
```

You can then select  `myOlBar` in the Objects list of the module window and then select the event in the procedure list. The Visual Basic Editor will then add the template for the event procedure to the module window. You can then type the code you want to run when the event occurs. The following example shows code added to the [BeforeNavigate](f632928b-01a9-b467-1cee-0a86e0023f4d.md) event procedure for the **OutlookBarPane** object.



```vb
Private Sub myOlBar_BeforeNavigate(ByVal Shortcut As OutlookBarShortcut, Cancel As Boolean) 
 If Shortcut.Name = "Notes" Then 
 MsgBox "You cannot open the Notes folder." 
 Cancel = True 
 End If 
End Sub
```

The final step is to add code to set the object variable to the object whose event you want to handle. This code can exist in a macro, or if you want the event to be handled whenever Outlook runs, you can put it in the  [Startup](d4724d96-2572-b1e3-e202-0bfffb5cf7d5.md) event procedure, as in the following example.



```vb
Private Sub Application_Startup() 
 Set myOlBar = Application.ActiveExplorer.Panes(1) 
End Sub
```

