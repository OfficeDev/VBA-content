
# Table.EndOfTable Property (Outlook)

 **Last modified:** July 28, 2015

 **In this article**
 [Syntax](#sectionSection0)
 [Remarks](#sectionSection1)
 [Example](#sectionSection2)


Returns a  **Boolean** that indicates whether the current row is positioned after the last row in the ** [Table](0affaafd-93fe-227a-acee-e09a86cadc20.md)** object. Read-only.


## Syntax
<a name="sectionSection0"> </a>

 _expression_. **EndOfTable**

 _expression_A variable that represents a  **Table** object.


## Remarks
<a name="sectionSection1"> </a>

When you open a  **Table** using ** [Folder.GetTable](08d184cb-0c41-01b1-abc5-305476380f8b.md)**,  ** [Search.GetTable](3aba6b77-73a3-9620-9c18-b2e03c7b63bc.md)**, or  ** [Table.Restrict](ecdd30f6-e12c-8025-3ded-592d2fad2bb8.md)**, the value of  **EndOfTable** depends on the number of rows in the **Table**. If there are no rows,  **EndOfTable** returns **True**. If there are rows,  **EndOfTable** returns **False** until the cursor moves beyond the last row in the **Table**. 

The  **Table** has two virtual **Null** rows, one before the first row and one after the last row in the **Table**. Each call to  ** [Table.GetNextRow](e01ddaa0-a869-2f52-5e46-84d4d4090e61.md)** moves the current row to the next row in the **Table** and returns a ** [Row](06db3fa4-1649-48bf-3b86-ffdf99a47305.md)** object that represents the current row.

The  **EndOfTable** property returns **True** if the current row is after the last row and **False** if the current row is on or before the last row.


## Example
<a name="sectionSection2"> </a>

The following code sample applies a Jet filter on items in the Inbox to obtain a  **Table** of items that have a **LastModificationTime** greater than November 1, 2005. It then uses the **EndOfTable** property to enumerate the items in the **Table** and print the values of the default properties for each item in the **Table**.


```
Sub DemoTable() 
 'Declarations 
 Dim Filter As String 
 Dim oRow As Outlook.Row 
 Dim oTable As Outlook.Table 
 Dim oFolder As Outlook.Folder 
 
 'Get a Folder object for the Inbox 
 Set oFolder = Application.Session.GetDefaultFolder(olFolderInbox) 
 
 'Define Filter to obtain items last modified after November 1, 2005 
 Filter = "[LastModificationTime] > '11/1/2005'" 
 'Restrict with Filter 
 Set oTable = oFolder.GetTable(Filter) 
 
 'Enumerate the table using test for EndOfTable 
 Do Until (oTable.EndOfTable) 
 Set oRow = oTable.GetNextRow() 
 Debug.Print (oRow("EntryID")) 
 Debug.Print (oRow("Subject")) 
 Debug.Print (oRow("CreationTime")) 
 Debug.Print (oRow("LastModificationTime")) 
 Debug.Print (oRow("MessageClass")) 
 Loop 
End Sub
```


## See also
<a name="sectionSection2"> </a>


#### Concepts


 [Table Object](0affaafd-93fe-227a-acee-e09a86cadc20.md)
#### Other resources


 [Table Object Members](bd9db35d-0738-22cf-a936-425d5a0ead87.md)
