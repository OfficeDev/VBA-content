
# Enumerate Active Folders in the Calendar View

 **Last modified:** July 28, 2015

 _**Applies to:** Outlook 2013_

In Microsoft Outlook, you can traverse the group and folder hierarchy of a module in the Navigation Pane by using the  ** [NavigationGroups](07206203-36a9-7467-3a89-24fa2a7c2b1f.md)** and ** [NavigationFolders](ecff93b8-0c3f-5f31-5b61-c46d2622d2af.md)** collections. The **NavigationGroups** collection of the ** [NavigationModule](76565eaf-1e64-f5d4-b90f-ba156863802c.md)** object contains each navigation group displayed in a navigation module, while the **NavigationFolders** collection of the ** [NavigationGroup](a96eb2b1-af1f-71b2-6a0b-dcb5078beb1f.md)** object contains each navigation folder displayed in a navigation group.

By using these collections in combination, you can enumerate each navigation folder for a navigation module displayed in the Navigation Pane. 
The following sample counts the number of navigation folders selected for display in the  **Calendar** navigation module of the Navigation Pane. The sample performs the following actions:

1. The sample first obtains a reference to the  ** [NavigationPane](b6538c72-6115-99fc-c926-e0532a747823.md)** object for the active explorer.
    
2. It then uses the  ** [GetNavigationModule](7c1a1313-94a4-fa68-7e70-66d85496fec0.md)** method of the ** [NavigationModules](4b0743d3-0a21-488c-27b2-31ae07129a61.md)** collection to obtain a reference to the ** [CalendarModule](9203024d-9cef-75e0-600f-f3899e24761a.md)** object from the **NavigationPane** object.
    
3. The sample then enumerates through the  ** [NavigationGroups](2f19eceb-24e6-a55c-7013-c840bd0c9fbb.md)** collection of the **CalendarModule** object. For each **NavigationGroup** in the collection, the sample then enumerates the ** [NavigationFolders](06e58adc-99d7-dd84-4d23-7f845850ff98.md)** collection.
    
4. If the  ** [IsSelected](a8fb9430-0477-2417-0dba-e30e9f8ebe8d.md)** property of a **NavigationFolder** object contained in the **NavigationFolders** collection is set to **True**, the variable  `intCounter` is incremented.
    
5. Finally, the sample displays a dialog box containing the value of  `intCounter`.
    



```
Dim WithEvents objPane As NavigationPane 
 
Private Sub EnumerateActiveCalendarFolders() 
 Dim objModule As CalendarModule 
 Dim objGroup As NavigationGroup 
 Dim objFolder As NavigationFolder 
 Dim intCounter As Integer 
 
 On Error GoTo ErrRoutine 
 
 ' Get the NavigationPane object for the 
 ' currently displayed Explorer object. 
 Set objPane = Application.ActiveExplorer.NavigationPane 
 
 ' Get the CalendarModule object, if one exists, 
 ' for the current Navigation Pane. 
 Set objModule = objPane.Modules.GetNavigationModule(olModuleCalendar) 
 
 ' Iterate through each NavigationGroup contained 
 ' by the CalendarModule. 
 For Each objGroup In objModule.NavigationGroups 
 ' Iterate through each NavigationFolder contained 
 ' by the NavigationGroup. 
 For Each objFolder In objGroup.NavigationFolders 
 ' Check if the folder is selected. 
 If objFolder.IsSelected Then 
 intCounter = intCounter + 1 
 End If 
 Next 
 Next 
 
 ' Display the results. 
 MsgBox "There are " &amp; intCounter &amp; " selected calendars in the Calendar module." 
 
EndRoutine: 
 On Error GoTo 0 
 Set objFolder = Nothing 
 Set objGroup = Nothing 
 Set objModule = Nothing 
 Set objPane = Nothing 
 intCounter = 0 
 Exit Sub 
 
ErrRoutine: 
 MsgBox Err.Number &amp; " - " &amp; Err.Description, _ 
 vbOKOnly Or vbCritical, _ 
 "EnumerateActiveCalendarFolders" 
End Sub 

```

