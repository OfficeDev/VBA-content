
# Application.AdvancedSearch Method (Outlook)

 **Last modified:** July 28, 2015

 **In this article**
 [Syntax](#sectionSection0)
 [Remarks](#sectionSection1)
 [Example](#sectionSection2)


Performs a search based on a specified DAV Searching and Locating (DASL) search string.


## Syntax
<a name="sectionSection0"> </a>

 _expression_. **AdvancedSearch**( **_Scope_**,  **_Filter_**,  **_SearchSubFolders_**,  **_Tag_**)

 _expression_A variable that represents an  ** [Application](797003e7-ecd1-eccb-eaaf-32d6ddde8348.md)** object.


### Parameters



|**Name**|**Required/Optional**|**Data Type**|**Description**|
|:-----|:-----|:-----|:-----|
|Scope|Required| **String**|The scope of the search. For example, the folder path of a folder. It is recommended that the folder path is enclosed within single quotes. Otherwise, the search might not return correct results if the folder path contains special characters including Unicode characters. To specify multiple folder paths, enclose each folder path in single quotes and separate the single quoted folder paths with a comma.|
|Filter|Optional| **Variant**|The DASL search filter that defines the parameters of the search.|
|SearchSubFolders|Optional| **Variant**|Determines if the search will include any of the folder's subfolders.|
|Tag|Optional| **Variant**|The name given as an identifier for the search.|

### Return Value

A  ** [Search](226a5d49-3caf-90dd-725c-265404d1939f.md)** object that represents the results of the search.


## Remarks
<a name="sectionSection1"> </a>

You can run multiple searches simultaneously by calling the  **AdvancedSearch** method in successive lines of code. However, you should be aware that programmatically creating a large number of search folders can result in significant simultaneous search activity that would affect the performance of Outlook, especially if Outlook conducts the search in online Exchange mode.

The  **AdvancedSearch** method and related features in the Outlook object model do not create a Search Folder that will appear in the Outlook user interface. However, you can use the ** [Save](a6dbec81-67fd-e337-b640-4f94ab36218f.md)** method of the **Search** object that is returned to create a Search Folder that will appear in the Search Folders list in the Outlook user interface.

Using the Scope parameter, you can specify one or more folders in the same store, but you may not specify multiple folders in multiple stores. To specify multiple folders in the same store for theScope parameter, use a comma character between each folder path and enclose each folder path in single quotes. For default folders such as Inbox or Sent Items, you can use the simple folder name instead of the full folder path. For example, the following two lines of code represent validScope parameters:




```
Scope = "'Inbox', 'Sent Items'"
```




```
Scope = "'" &amp; Application.Session.GetDefaultFolder(olFolderInbox).FolderPath _  
    &amp; "','" &amp; Application.Session.GetDefaultFolder(olFolderSentMail).FolderPath &amp; "'"
```

The Filter parameter can be any valid DASL query. For additional information on DASL queries, see [Filtering Items](4038e042-1b07-5d18-18b0-c2b58c9c42da.md) and [Referencing Properties by Namespace](c1c7bfa9-64d7-81d2-84e7-f0a4c57780b3.md). Note that you cannot use a JET query for the Filter parameter of Advanced Search. If Instant Search is enabled on a store that contains a folder specified in theScope parameter, you can use Instant Search keywords to improve the performance of your search. If you use Instant Search keywords and Instant Search is not enabled, Outlook will return an error and your search will fail.


## Example
<a name="sectionSection2"> </a>

The following Visual Basic for Applications (VBA) example searches the  **Inbox** for items with subject equal toTest and displays the names of the senders of the e-mail items returned by the search. The ** [AdvancedSearchComplete](4f33ad44-20a3-62cd-aa1b-db74581ebb3c.md)** event procedure sets the boolean `blnSearchComp` to **True** when the search is complete. This boolean variable is used by the `TestAdvancedSearchComplete()` procedure to determine when the search is complete. The sample code must be placed in a class module such as `ThisOutlookSession`, and the  `TestAdvancedSearchComplete()` procedure must be called before the event procedure can be called by Outlook.


```
Public blnSearchComp As Boolean  
  
Private Sub Application_AdvancedSearchComplete(ByVal SearchObject As Search)  
    Debug.Print "The AdvancedSearchComplete Event fired"  
    If SearchObject.Tag = "Test" Then  
        m_SearchComplete = True  
    End If  
  
End Sub  
  
Sub TestAdvancedSearchComplete()  
    Dim sch As Outlook.Search  
    Dim rsts As Outlook.Results  
    Dim i As Integer  
    blnSearchComp = False  
    Const strF As String = "urn:schemas:mailheader:subject = 'Test'"  
    Const strS As String = "Inbox"     
    Set sch = Application.AdvancedSearch(strS, strF, "Test")   
    While blnSearchComp = False  
        DoEvents  
    Wend   
    Set rsts = sch.Results  
    For i = 1 To rsts.Count  
        Debug.Print rsts.Item(i).SenderName  
    Next  
End Sub  

```

The following Microsoft Visual Basic for Applications example uses the  **AdvancedSearch** method to create a new search. The parameters of the search, as specified by the _Filter_ argument of the **AdvancedSearch** method, will return all items in the Inbox and Sent Items folders where the Subject phrase-matches or contains "Office". The user's Inbox and Sent Items folders are specified as the scope of the search and the ** [SearchSubFolders](26dd1970-ba59-9f6a-8cf6-3dba0f9668b2.md)** property is set to **True**. When the search is complete, the  ** [GetTable](3aba6b77-73a3-9620-9c18-b2e03c7b63bc.md)** method is called on the ** [Search](226a5d49-3caf-90dd-725c-265404d1939f.md)** object for performant enumeration of search results.




```
Public m_SearchComplete As Boolean  
  
Private Sub Application_AdvancedSearchComplete(ByVal SearchObject As Search)  
    If SearchObject.Tag = "MySearch" Then  
        m_SearchComplete = True  
    End If  
End Sub  
  
Sub TestSearchForMultipleFolders()  
    Dim Scope As String  
    Dim Filter As String  
    Dim MySearch As Outlook.Search  
    Dim MyTable As Outlook.Table  
    Dim nextRow As Outlook.Row  
    m_SearchComplete = False  
    'Establish scope for multiple folders  
    Scope = "'" &amp; Application.Session.GetDefaultFolder( _  
    olFolderInbox).FolderPath _  
    &amp; "','" &amp; Application.Session.GetDefaultFolder( _  
    olFolderSentMail).FolderPath &amp; "'"  
    'Establish filter  
    If Application.Session.DefaultStore.IsInstantSearchEnabled Then  
        Filter = Chr(34) &amp; "urn:schemas:httpmail:subject" _  
        &amp; Chr(34) &amp; " ci_phrasematch 'Office'"  
    Else  
        Filter = Chr(34) &amp; "urn:schemas:httpmail:subject" _  
        &amp; Chr(34) &amp; " like '%Office%'"  
    End If  
    Set MySearch = Application.AdvancedSearch( _  
    Scope, Filter, True, "MySearch")  
    While m_SearchComplete <> True  
        DoEvents  
    Wend  
    Set MyTable = MySearch.GetTable  
    Do Until MyTable.EndOfTable  
        Set nextRow = MyTable.GetNextRow()  
        Debug.Print nextRow("Subject")  
    Loop  
End Sub  

```


## See also
<a name="sectionSection2"> </a>


#### Concepts


 [Application Object](797003e7-ecd1-eccb-eaaf-32d6ddde8348.md)
#### Other resources


 [Application Object Members](3519c89c-2353-85ee-7ddc-62e5dd85a8e7.md)
