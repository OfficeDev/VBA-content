
# Add a Custom Folder to a Group and Display it in Overlay Mode by Default

 **Last modified:** July 28, 2015

 _**Applies to:** Outlook 2013_

You can add custom navigation folders to a navigation group in Microsoft Outlook by using the  ** [Add](f88fd69a-8684-bfc4-bc20-1cff5c44974e.md)** method of the ** [NavigationFolders](ecff93b8-0c3f-5f31-5b61-c46d2622d2af.md)** collection for a ** [NavigationGroup](a96eb2b1-af1f-71b2-6a0b-dcb5078beb1f.md)** object. The **Add** method accepts a ** [Folder](3cf6cda8-6d70-666e-2643-9d9c5b9cacfc.md)** object reference, to which the custom navigation folder is associated.

If the custom navigation folder is associated with a calendar folder, you can also use the  ** [IsSideBySide](00a49ce6-ad74-1f24-2aaa-e79a3409c9c9.md)** property of the ** [NavigationFolder](c8d7aabb-58ba-df5e-ccdc-06f73db7726c.md)** object to determine if the contents of the custom navigation folder are displayed in side-by-side or overlay mode.
This sample creates a new calendar folder for company events and adds a custom navigation folder for the new folder, configuring the custom navigation folder so that it is displayed by default in overlay mode.
The sample performs the following actions:

1. The sample obtains a  ** [Folder](3cf6cda8-6d70-666e-2643-9d9c5b9cacfc.md)** object reference for the **Calendar** default folder for the current user, by using the ** [GetDefaultFolder](761b8b53-dd4d-43e4-c8f0-69cefdf0c77a.md)** method of the ** [NameSpace](f0dcaa19-07f5-5d42-a3bf-2e42b7885644.md)** object.
    
2. It then creates a new  **Folder** object named "Company Events", representing the new calendar folder, in the ** [Folders](0c814c3c-74fc-414c-982d-a0097fcb35c2.md)** collection of the **Calendar** default folder.
    
3. The sample then obtains a reference to the  ** [NavigationPane](b6538c72-6115-99fc-c926-e0532a747823.md)** object for the active explorer and uses the ** [GetNavigationModule](7c1a1313-94a4-fa68-7e70-66d85496fec0.md)** method of the ** [NavigationModules](4b0743d3-0a21-488c-27b2-31ae07129a61.md)** collection to obtain a ** [CalendarModule](9203024d-9cef-75e0-600f-f3899e24761a.md)** object reference.
    
4. It then uses the  ** [GetDefaultNavigationGroup](accdd554-1aa1-b254-7489-67673b889757.md)** method of the ** [NavigationGroups](07206203-36a9-7467-3a89-24fa2a7c2b1f.md)** collection for the **CalendarModule** to obtains a **NavigationGroup** object reference to the **My Calendars** navigation group.
    
5. It then adds a new  **NavigationFolder** object, based on the **Folder** object created earlier by the sample, to the navigation group by using the **Add** method of the **NavigationGroups** collection for that navigation group.
    
6. The sample then sets the  ** [CurrentModule](df7086b3-4174-839f-0756-a5201379ed92.md)** property of the **NavigationPane** object to the **CalendarModule** object reference, to ensure that the **Calendar** navigation module is currently displayed in the Navigation Pane.
    
7. Finally, the sample then configures the navigation folder:
    
      - The sample sets the  ** [IsSelected](a8fb9430-0477-2417-0dba-e30e9f8ebe8d.md)** property to **True** to display it in the active explorer.
    
  - The sample then sets the  **IsSideBySide** property to **False** to display it by default in overlay mode.
    



```
Private Sub CreateCompanyEventsFolder() 
 Dim objNamespace As NameSpace 
 Dim objCalendar As Folder 
 Dim objFolder As Folder 
 
 Dim objPane As NavigationPane 
 Dim objModule As CalendarModule 
 Dim objGroup As NavigationGroup 
 Dim objNavFolder As NavigationFolder 
 
 On Error GoTo ErrRoutine 
 
 ' First, retrieve the default calendar folder. 
 Set objNamespace = Application.GetNamespace("MAPI") 
 Set objCalendar = objNamespace.GetDefaultFolder(olFolderCalendar) 
 
 ' Create a new calendar folder named "Company Events". 
 Set objFolder = objCalendar.Folders.Add("Company Events", olFolderCalendar) 
 
 ' Get the NavigationPane object for the 
 ' currently displayed Explorer object. 
 Set objPane = Application.ActiveExplorer.NavigationPane 
 
 ' Get the calendar module from the Navigation Pane. 
 Set objModule = objPane.Modules.GetNavigationModule(olModuleCalendar) 
 
 ' Get the "My Calendars" navigation group from the 
 ' calendar module. 
 With objModule.NavigationGroups 
 Set objGroup = .GetDefaultNavigationGroup(olMyFoldersGroup) 
 End With 
 
 ' Add a new navigation folder for the "Company Events" 
 ' folder in the "My Calendars" navigation group. 
 Set objNavFolder = objGroup.NavigationFolders.Add(objFolder) 
 
 ' Set the navigation folder to be displayed in overlay mode 
 ' by default. The IsSelected property can't be set to True 
 ' unless the CalendarModule object is the current module 
 ' displayed in the Navigation Pane. 
 Set objPane.CurrentModule = objModule 
 objNavFolder.IsSelected = True 
 objNavFolder.IsSideBySide = False 
 
EndRoutine: 
 On Error GoTo 0 
 
 Set objNavFolder = Nothing 
 Set objFolder = Nothing 
 Set objGroup = Nothing 
 Set objModule = Nothing 
 Set objPane = Nothing 
 Set objNamespace = Nothing 
 
 Exit Sub 
 
ErrRoutine: 
 MsgBox Err.Number &amp; " - " &amp; Err.Description, _ 
 vbOKOnly Or vbCritical, _ 
 "CreateCompanyEventsFolder" 
End Sub 

```

