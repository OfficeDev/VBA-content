
# Recipient.Type Property (Outlook)

 **Last modified:** July 28, 2015

 **In this article**
 [Syntax](#sectionSection0)
 [Remarks](#sectionSection1)
 [Example](#sectionSection2)


Returns or sets a  **Long** representing the type of recipient. Read/write.


## Syntax
<a name="sectionSection0"> </a>

 _expression_. **Type**

 _expression_A variable that represents a  **Recipient** object.


## Remarks
<a name="sectionSection1"> </a>

Depending on the type of recipient, this property returns or sets a  **Long** corresponding to the numeric equivalent of one of the following constants:


-  ** [JournalItem](6e850295-39f9-47b8-e866-9622e9958c69.md)** recipient: the ** [OlJournalRecipientType](d44d02bf-b3c6-64db-e1db-75190ae8f684.md)** constant **olAssociatedContact**.
    
-  ** [MailItem](14197346-05d2-0250-fa4c-4a6b07daf25f.md)** recipient: one of the following ** [OlMailRecipientType](5f3f584b-c188-5c93-20c3-4dde6f2cfc3b.md)** constants: **olBCC**,  **olCC**,  **olOriginator**, or  **olTo**.
    
-  ** [MeetingItem](b75730f5-b395-3d66-5acd-b64fd8fcd78f.md)** recipient: one of the following ** [OlMeetingRecipientType](f5636886-f1db-9f64-f36c-4314133a7d12.md)** constants: **olOptional**,  **olOrganizer**,  **olRequired**, or  **olResource**.
    
-  ** [TaskItem](5df8cfa5-5460-a5a1-a130-ba5bca1a0091.md)** recipient: either of the following ** [OlTaskRecipientType](8f1678ba-f11a-1e70-816f-57b8a348a190.md)** constants: **olFinalStatus**, or  **olUpdate**.
    


This property may not always return the appropriate recipient type for a conference room. For instance, a conference room may be specified as a required recipient in a meeting request, in which case this property will not return  **olResource** for that conference room.

To reliably determine if a recipient is a conference room, use the Messaging API (MAPI) property,  ** [PidTagDisplayTypeEx](http://msdn.microsoft.com/library/23074402-6ac1-47f1-8a49-b8909f98a26e%28Office.15%29.aspx)**, of the  ** [Recipient](8cee4d79-ec55-52a4-710b-6456944ca86d.md)** object. You can access this property using the ** [PropertyAccessor](2fc91e13-703c-3ec9-9066-ffee7144306c.md)** object in the Outlook object model. The **PidTagDisplayTypeEx** property is represented as "http://schemas.microsoft.com/mapi/proptag/0x39050003" in the MAPI proptag namespace. Note that the **PidTagDisplayTypeEx** property is not available in versions of Microsoft Exchange Server earlier than Microsoft Exchange Server 2007; in such earlier versions of Exchange Server, you can use the **Recipient.Type** property and assume that a recipient having a type other than **olResource** is not a conference room.


## Example
<a name="sectionSection2"> </a>

The following Visual Basic for Applications (VBA) example shows how to use the  **PropertyAccessor** on the **PidTagDisplayTypeEx** property for each of the **Recipient** objects in the ** [Recipients](774f56b7-4de8-9584-60cd-4fbf361f4c85.md)** collection of a meeting request. If the value of that property is 7 (the value for the MAPI constant **DT_ROOM** as defined in the MAPI header file, mapidefs.h), then that recipient is a conference room. This example assumes that there is a meeting request in the current inspector.


```
Sub DemoMeetingRecipients() 
 Dim myAppointment As Outlook.AppointmentItem 
 Dim myPA As Outlook.PropertyAccessor 
 Dim d As Long 
 Dim myInt As Long 
 
 Set myAppointment = Application.ActiveInspector.CurrentItem 
 
 For d = 1 To myAppointment.Recipients.count 
 Debug.Print myAppointment.Recipients.item(d).name 
 Debug.Print myAppointment.Recipients.item(d).Type 
 Set myPA = myAppointment.Recipients.item(d).PropertyAccessor 
 myInt = myPA.GetProperty("http://schemas.microsoft.com/mapi/proptag/0x39050003") 
 Debug.Print myInt 
 Debug.Print "---" 
 Next d 
End Sub 

```

The following VBA example uses  ** [CreateItem](e5fbf367-db16-5042-823e-68e6b805e612.md)** to create an appointment and uses ** [MeetingStatus](cfd970cd-df6c-4537-0a17-b5adab3b667f.md)** to set the meeting status to "Meeting" to turn it into a meeting request with both a required and an optional attendee. The recipient names should be replaced with valid names to avoid errors.




```
Sub ScheduleMeeting() 
 Dim myItem as Outlook.AppointmentItem 
 Dim myRequiredAttendee As Outlook.Recipient 
 Dim myOptionalAttendee As Outlook.Recipient 
 Dim myResourceAttendee As Outlook.Recipient 
 
 Set myItem = Application.CreateItem(olAppointmentItem) 
 myItem.MeetingStatus = olMeeting 
 myItem.Subject = "Strategy Meeting" 
 myItem.Location = "Conference Room B" 
 myItem.Start = #9/24/2003 1:30:00 PM# 
 myItem.Duration = 90 
 Set myRequiredAttendee = myItem.Recipients.Add ("Nate Sun") 
 myRequiredAttendee.Type = olRequired 
 Set myOptionalAttendee = myItem.Recipients.Add ("Kevin Kennedy") 
 myOptionalAttendee.Type = olOptional 
 Set myResourceAttendee = myItem.Recipients.Add("Conference Room B") 
 myResourceAttendee.Type = olResource 
 myItem.Display 
End Sub
```


## See also
<a name="sectionSection2"> </a>


#### Concepts


 [Referencing Properties by Namespace](c1c7bfa9-64d7-81d2-84e7-f0a4c57780b3.md)
 [Recipient Object](8cee4d79-ec55-52a4-710b-6456944ca86d.md)
#### Other resources


 [Recipient Object Members](70e34018-95de-7fcf-1331-9be61a8675a2.md)
