
# Create a Rule to Move Specific E-mails to a Folder

 **Last modified:** July 28, 2015

 _**Applies to:** Outlook 2013_

This topic shows a code sample in Visual Basic for Applicatons (VBA) that uses the  **Rules** object model to create a rule. The code sample uses the ** [RuleAction](6451788f-e5ed-239c-a34d-b564b52d8955.md)** and ** [RuleCondition](e03f91c2-2c08-b036-104a-d6246f28bc2d.md)** objects to specify a rule that moves messages from a specific sender to a specific folder, unless the message contains certain terms in the subject. Note that the code sample assumes that there already exists a folder named "Dan" under the Inbox.

The following describes the steps used to create the rule:

1. Specify the target folder  `oMoveTarget` to move specific messages as determined by the condition and exception condition. The target folder is a subfolder named "Dan" under the Inbox, and is assumed to already exist.
    
2. Use  ** [Store.GetRules](06048799-e162-68f9-17c2-d80c25e2c55e.md)** to obtain a set of all the rules in the current session.
    
3. Using the  ** [Rules](dd41b4de-bf5f-5532-46c9-394a5d078bec.md)** collection returned from the last step, use ** [Rules.Create](84789ccc-a6c2-9f79-5338-45b03b116dd5.md)** to add a new rule. The new rule specifies some action upon receiving a message, so it is of type **olRuleReceive**.
    
4. Using the  ** [Rule](ea2ddbcc-fd65-a636-c6da-79950033f385.md)** object returned from the last step, use the ** [RuleConditions.From](3ebda0d0-ba44-95c6-ed02-a9c6acbf1f1c.md)** property to obtain a ** [ToOrFromRuleCondition](ec5cae2a-cde8-5681-6a49-74e2f0226a4f.md)** object, `oFromCondition`.  `oFromCondition` specifies the condition for the rule: when a message is from `Dan Wilson`. 
    
5. Using the same  **Rule** object, use the ** [RuleActions.MoveToFolder](6d9c577d-e022-72fc-45f2-bdda7a8761de.md)** property to obtain a ** [MoveOrCopyRuleAction](db951ad8-0d05-1696-acf4-c1da4fbdee33.md)** object, `oMoveRuleAction`.  `oMoveRuleAction` specifies the action for the rule: move the message to the target folder "Dan".
    
6. Using the same  **Rule** object, use the ** [RuleConditions.Subject](d6d51efb-9eec-0c07-ca8f-616791822f91.md)** property to obtain a ** [TextRuleCondition](87e9ca00-7577-02c2-fb6f-a5dc2054ad8b.md)** object, `oExceptSubject`.  `oExceptSubject` specifies the exception condition: if the subject contains the terms "fun" or "chat", then do not apply the rule to move the message to the folder "Dan".
    
7. Use  ** [Rules.Save](d838eca0-4ec5-ab43-a031-fd65ab7d9f3c.md)** to save the new rule together with the rest of the rules for the current store.
    



```
Sub CreateRule() 
    Dim colRules As Outlook.Rules 
    Dim oRule As Outlook.Rule 
    Dim colRuleActions As Outlook.RuleActions 
    Dim oMoveRuleAction As Outlook.MoveOrCopyRuleAction 
    Dim oFromCondition As Outlook.ToOrFromRuleCondition 
    Dim oExceptSubject As Outlook.TextRuleCondition 
    Dim oInbox As Outlook.Folder 
    Dim oMoveTarget As Outlook.Folder 
 
    'Specify target folder for rule move action 
    Set oInbox = Application.Session.GetDefaultFolder(olFolderInbox) 
    'Assume that target folder already exists 
    Set oMoveTarget = oInbox.Folders("Dan") 
     
    'Get Rules from Session.DefaultStore object 
    Set colRules = Application.Session.DefaultStore.GetRules() 
     
    'Create the rule by adding a Receive Rule to Rules collection 
    Set oRule = colRules.Create("Dan's rule", olRuleReceive) 
 
    'Specify the condition in a ToOrFromRuleCondition object 
    'Condition is if the message is from "Dan Wilson" 
    Set oFromCondition = oRule.Conditions.From 
    With oFromCondition 
        .Enabled = True 
        .Recipients.Add ("Dan Wilson") 
        .Recipients.ResolveAll 
    End With 
 
    'Specify the action in a MoveOrCopyRuleAction object 
    'Action is to move the message to the target folder 
    Set oMoveRuleAction = oRule.Actions.MoveToFolder 
    With oMoveRuleAction 
        .Enabled = True 
        .Folder = oMoveTarget 
    End With 
 
    'Specify the exception condition for the subject in a TextRuleCondition object 
    'Exception condition is if the subject contains "fun" or "chat" 
    Set oExceptSubject = _ 
        oRule.Exceptions.Subject 
    With oExceptSubject 
        .Enabled = True 
        .Text = Array("fun", "chat") 
    End With 
 
    'Update the server and display progress dialog 
    colRules.Save 
End Sub 
```

