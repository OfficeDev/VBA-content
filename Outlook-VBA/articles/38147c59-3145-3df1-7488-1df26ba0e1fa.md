
# Delete Data to Reset Solution Storage

 **Last modified:** July 28, 2015

 _**Applies to:** Outlook 2013_

This topic describes how to delete existing solution data to reset the solution storage:


1. Use  ** [Folder.GetStorage](cc5ee63b-7d11-6340-8392-8b35a689a28c.md)** to obtain an existing ** [StorageItem](41776bc3-b838-2755-fd6b-3b5012fb9ae5.md)** object in a specific folder. This call will return a new **StorageItem** object if none already exists.
    
2. Use  ** [StorageItem.Delete](0ace6d9e-3dc7-52d5-ac20-97c2f3b109de.md)** to remove the object permanently from the folder.
    
3. Use  **Folder.GetStorage** to create a new instance of the **StorageItem** object with the same subject.
    
4. Use the  ** [Add](88b86622-2234-77be-41e7-b76b0b3a75ad.md)** method of ** [StorageItem.UserProperties](0a08e77c-1665-a612-2f47-ef1c3fc331d2.md)** to create a custom property **Order Number**.
    
5. Set the  **Order Number** property.
    
6. Use  ** [StorageItem.Save](9462a342-294a-175e-7e8f-d416f0959f69.md)** to save the **StorageItem** object to the folder.
    



```
Sub StoreData() 
 Dim oInbox As Folder 
 Dim myStorage As StorageItem 
 Dim myPrivateProperty As UserProperty 
 
 Set oInbox = Application.Session.GetDefaultFolder(olFolderInbox) 
 ' Get an existing instance of StorageItem by subject 
 Set myStorage = oInbox.GetStorage("My Private Storage", olIdentifyBySubject) 
 
 'Remove the storage permanently assuming it's old 
 myStorage.Delete 
 Set myStorage = Nothing 
 
 'Get a new instance of StorageItem in the Inbox 
 Set myStorage = oInbox.GetStorage("My Private Storage", olIdentifyBySubject) 
 
 'Create custom property for Order Number 
 Set myPrivateProperty = myStorage.UserProperties.Add("Order Number", olNumber) 
 
 'Store application data in the Order Number property 
 myPrivateProperty.Value = 1000 
 
 'Save the data to the Inbox 
 myStorage.Save 
End Sub
```

