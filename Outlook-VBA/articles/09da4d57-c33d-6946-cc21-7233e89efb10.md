
# Store.GetRootFolder Method (Outlook)

 **Last modified:** July 28, 2015

 **In this article**
 [Syntax](#sectionSection0)
 [Remarks](#sectionSection1)
 [Example](#sectionSection2)


Returns a  ** [Folder](3cf6cda8-6d70-666e-2643-9d9c5b9cacfc.md)** object representing the root-level folder of the ** [Store](1eb22fe9-8849-7476-5388-2515b48591b9.md)**. Read-only.


## Syntax
<a name="sectionSection0"> </a>

 _expression_. **GetRootFolder**

 _expression_A variable that represents a  **Store** object.


### Return Value

A  **Folder** object that represents the folder at the root of that **Store**.


## Remarks
<a name="sectionSection1"> </a>

You can use the  **GetRootFolder** method to enumerate the subfolders of the root folder of the **Store**. Unlike  ** [NameSpace.Folders](a732d338-c825-4d38-0107-345069da708c.md)** which contains all folders for all stores in the current profile, **Store.GetRootFolder.Folders** allows you to enumerate all folders for a given **Store** object in the current profile.

The  ** [Parent](0671c1d3-c25e-b9c7-3c07-bd83c9f01ae4.md)** property of the root folder of a store returns the string "Mapi".

The root folder for the Exchange Public Folder store is the folder  **Public Folders**. This folder is returned by the call to  `Application.Session.GetDefaultFolder(olPublicFoldersAllPublicFolders)`.

 **GetRootFolder** returns an error if the service provider does not support root folders.


## Example
<a name="sectionSection2"> </a>

The following code sample in Microsoft Visual Basic for Applications (VBA) starts at the root-level folder of each  **Store** in a ** [Stores](8915a8e4-9c22-21d5-c492-051d393ce5f7.md)** collection for a session, and enumerates all folders on all stores for that session.


```
Sub EnumerateFoldersInStores() 
 
 Dim colStores As Outlook.Stores 
 
 Dim oStore As Outlook.Store 
 
 Dim oRoot As Outlook.Folder 
 
 
 
 On Error Resume Next 
 
 Set colStores = Application.Session.Stores 
 
 For Each oStore In colStores 
 
 Set oRoot = oStore.GetRootFolder 
 
 Debug.Print (oRoot.FolderPath) 
 
 EnumerateFolders oRoot 
 
 Next 
 
End Sub 
 
 
 
Private Sub EnumerateFolders(ByVal oFolder As Outlook.Folder) 
 
 Dim folders As Outlook.folders 
 
 Dim Folder As Outlook.Folder 
 
 Dim foldercount As Integer 
 
 
 
 On Error Resume Next 
 
 Set folders = oFolder.folders 
 
 foldercount = folders.Count 
 
 'Check if there are any folders below oFolder 
 
 If foldercount Then 
 
 For Each Folder In folders 
 
 Debug.Print (Folder.FolderPath) 
 
 EnumerateFolders Folder 
 
 Next 
 
 End If 
 
End Sub
```


## See also
<a name="sectionSection2"> </a>


#### Concepts


 [Store Object](1eb22fe9-8849-7476-5388-2515b48591b9.md)
#### Other resources


 [Store Object Members](84c1d423-e507-0b3b-6570-33829b94be04.md)
