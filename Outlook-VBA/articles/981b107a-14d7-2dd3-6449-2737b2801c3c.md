
# Selection.Item Method (Outlook)

 **Last modified:** July 28, 2015

 **In this article**
 [Syntax](#sectionSection0)
 [Remarks](#sectionSection1)
 [Example](#sectionSection2)


Returns a Microsoft Outlook item or conversation header from the selection.


## Syntax
<a name="sectionSection0"> </a>

 _expression_. **Item**( **_Index_**)

 _expression_A variable that represents a  ** [Selection](0b06a3ce-0445-db8f-e6e8-bb7bd469c50f.md)** object.


### Parameters



|**Name**|**Required/Optional**|**Data Type**|**Description**|
|:-----|:-----|:-----|:-----|
|Index|Required| **Variant**|Either the index number of the object, or a value used to match the default property of an object in the collection.|

### Return Value

An  **Object** that represents the specified item or conversation header.


## Remarks
<a name="sectionSection1"> </a>

Do not make any assumptions about the  **Item** method return type; your code should be able to handle multiple item types or a ** [ConversationHeader](5142d5f7-55c1-4d9d-3a11-d25c8763fcb7.md)** object. For example, the **Item** method can return an ** [AppointmentItem](204a409d-654e-27aa-643a-8344c631b82d.md)**,  ** [MailItem](14197346-05d2-0250-fa4c-4a6b07daf25f.md)**,  ** [MeetingItem](b75730f5-b395-3d66-5acd-b64fd8fcd78f.md)**, or  ** [TaskItem](5df8cfa5-5460-a5a1-a130-ba5bca1a0091.md)** in the Inbox folder, depending on the value of the ** [Selection.Location](8a2db72a-8db0-840e-349e-5d9d22f3affb.md)** property.

The  ** [Selection](0b06a3ce-0445-db8f-e6e8-bb7bd469c50f.md)** collection contains **ConversationHeader** objects only if you specify **olConversationHeaders** in the ** [GetSelection](c6af6665-d97d-3833-1014-5b43282bafc2.md)** method of the **Selection** object.


## Example
<a name="sectionSection2"> </a>

The following Microsoft Visual Basic for Applications (VBA) example displays the sender of each selected item in the active explorer. It uses the  ** [Count](ea7a19d2-6261-ce07-97f3-ebe95489a265.md)** property and ** [Item](981b107a-14d7-2dd3-6449-2737b2801c3c.md)** method of the ** [Selection](0b06a3ce-0445-db8f-e6e8-bb7bd469c50f.md)** object, returned by the ** [Explorer.Selection](11002043-9dab-a5ad-b36e-52ddb04c1859.md)** property, to display the senders of all messages that are selected in the active explorer.


```
Sub GetSelectedItems() 
 Dim myOlExp As Outlook.Explorer 
 Dim myOlSel As Outlook.Selection 
 Dim mySender As Outlook.AddressEntry 
 Dim oMail As Outlook.MailItem 
 Dim oAppt As Outlook.AppointmentItem 
 Dim oPA As Outlook.PropertyAccessor 
 Dim strSenderID As String 
 Const PR_SENT_REPRESENTING_ENTRYID As String = _ 
 "http://schemas.microsoft.com/mapi/proptag/0x00410102" 
 Dim MsgTxt As String 
 Dim x As Long 
 
 MsgTxt = "Senders of selected items:" 
 Set myOlExp = Application.ActiveExplorer 
 Set myOlSel = myOlExp.Selection 
 For x = 1 To myOlSel.Count 
 If myOlSel.Item(x).Class = OlObjectClass.olMail Then 
 ' For mail item, use the SenderName property. 
 Set oMail = myOlSel.Item(x) 
 MsgTxt = MsgTxt &amp; oMail.SenderName &amp; ";" 
 ElseIf myOlSel.Item(x).Class = OlObjectClass.olAppointment Then 
 ' For appointment item, use the Organizer property. 
 Set oAppt = myOlSel.Item(x) 
 MsgTxt = MsgTxt &amp; oAppt.Organizer &amp; ";" 
 Else 
 ' For other items, use the property accessor to get sender ID, 
 ' then get the address entry to display the sender name. 
 Set oPA = myOlSel.Item(x).PropertyAccessor 
 strSenderID = oPA.GetProperty(PR_SENT_REPRESENTING_ENTRYID) 
 Set mySender = Application.Session.GetAddressEntryFromID(strSenderID) 
 MsgTxt = MsgTxt &amp; mySender.Name &amp; ";" 
 End If 
 Next x 
 Debug.Print MsgTxt 
End Sub 

```


## See also
<a name="sectionSection2"> </a>


#### Concepts


 [Selection Object](0b06a3ce-0445-db8f-e6e8-bb7bd469c50f.md)
#### Other resources


 [Selection Object Members](c79922d4-aa76-ff48-f163-8161fa1ae0a8.md)
