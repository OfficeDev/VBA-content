
# Display a Dialog Box for Selecting Entries from the Contacts Folder

 **Last modified:** July 28, 2015

 _**Applies to:** Outlook 2013_

This topic describes how to use the  ** [SelectNamesDialog](1522736a-3cad-9f1c-4da9-b52a3a01731c.md)** object to display entries from the Contacts folder in a dialog box that resembles the **Select Names** dialog box in the Outlook user interface.



1. Look for the address list that corresponds with the Contacts folder.The  **SelectNamesDialog** object displays entires in a dialog box based on an ** [AddressList](84611afe-48b1-185b-df4b-0f004e7436ff.md)**. To display entries in the Contacts folder, look for the  **AddressList** that corresponds with the Contacts folder. Iterate through all the address lists defined for the current session, and for each address list, use ** [AddressList.GetContactsFolder](9ea91624-bd7d-af64-7220-a2d9b659787a.md)**to match the corresponding folder with the Contacts folder. 
    
2. Initialize the dialog box with the address list of the Contacts folder.
    
3. Use  ** [SelectNamesDialog.Display](a689dfca-e4f7-f1c0-03a1-71e7d7e310b7.md)** to display the dialog box. If **SelectNamesDialog.Display** returns True, then selected entries will be available in ** [SelectNamesDialog.Recipients](8b939af1-b266-55ad-f9ad-8802ac2e0930.md)**.
    




```
Sub ShowContactsInDialog() 
 Dim oDialog As SelectNamesDialog 
 Dim oAL As AddressList 
 Dim oContacts As Folder 
 
 Set oDialog = Application.Session.GetSelectNamesDialog 
 Set oContacts = _ 
 Application.Session.GetDefaultFolder(olFolderContacts) 
 
 'Look for the address list that corresponds with the Contacts folder 
 For Each oAL In Application.Session.AddressLists 
 If oAL.GetContactsFolder = oContacts Then 
 Exit For 
 End If 
 Next 
 With oDialog 
 'Initialize the dialog box with the address list representing the Contacts folder 
 .InitialAddressList = oAL 
 .ShowOnlyInitialAddressList = True 
 If .Display Then 
 'Recipients Resolved 
 'Access Recipients using oDialog.Recipients 
 End If 
 End With 
End Sub
```

