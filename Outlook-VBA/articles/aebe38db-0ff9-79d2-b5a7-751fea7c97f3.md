
# UserDefinedProperty Object (Outlook)

 **Last modified:** July 28, 2015

Represents the definition of a user-defined property for a  ** [Folder](3cf6cda8-6d70-666e-2643-9d9c5b9cacfc.md)** object.

## Remarks

Use  ** [UserDefinedProperties](4293bcb8-855e-4c6d-9718-ba8c5862b3bd.md)**(index), where index is a name or index number, to return a single **UserDefinedProperty** object.

Use the  ** [Add](e033b27e-101d-4ef8-ed84-790fd9e6107a.md)** method of the ** [UserDefinedProperties](4293bcb8-855e-4c6d-9718-ba8c5862b3bd.md)** collection for a **Folder** object to define a user-defined property for that folder.

Use the  ** [Type](94895d2b-7b3e-e455-3b58-58abd8279c10.md)** property to return the user-defined property type and the ** [DisplayFormat](f891aa8d-a769-275d-c027-7c5260eafc97.md)** property to return the display format for the user-defined property. If the **Type** property is set to **olCombination** or **olFormula**, use the  ** [Formula](5cbf6147-4f54-dce1-7c25-52a3f18896db.md)** property to return the formula used to generate values for the user-defined property.

The  **UserDefinedProperty** object represents only the definition of a user-defined property, which is applicable to all Outlook items contained by the folder. To retrieve or change user-defined property values for an Outlook item in that folder, use the ** [UserProperties](702ae502-d427-eeaf-ddd0-ff9749e7148c.md)** property of the Outlook item, such as a ** [MailItem](14197346-05d2-0250-fa4c-4a6b07daf25f.md)** object, to retrieve the ** [UserProperties](20b49c86-d74f-9bda-382c-559af278c148.md)** collection for that item. You can then use the ** [UserProperty](c94f642f-4368-d775-a79f-ce6c39bfe1fd.md)** object for the appropriate user-defined property to retrieve or change the value of that user-defined property for the Outlook item.


## Example

The following Visual Basic for Applications (VBA) example displays the name of a specified  **Folder** object, as well as the name and type of every **UserDefinedProperty** object contained in the **UserDefinedProperties** collection of the specified **Folder** object, to the **Immediate** window.


```
Sub DisplayUserProperties(ByRef FolderToCheck As Folder) 
 Dim objProperty As UserDefinedProperty 
 
 ' Print the name of the specified Folder object 
 ' reference to the Immediate window. 
 Debug.Print "--- Folder: " &amp; FolderToCheck.Name 
 
 ' Check if there are any user-defined properties 
 ' associated with the Folder object reference. 
 If FolderToCheck.UserDefinedProperties.Count = 0 Then 
 ' No user-defined properties are present. 
 Debug.Print " No user-defined properties." 
 Else 
 ' Iterate through every user-defined property in 
 ' the folder. 
 For Each objProperty In FolderToCheck.UserDefinedProperties 
 ' Retrieve the name of the user-defined property. 
 strPropertyInfo = objProperty.Name 
 ' Retrieve the type of the user-defined property. 
 Select Case objProperty.Type 
 Case OlUserPropertyType.olCombination 
 strPropertyInfo = strPropertyInfo &amp; " (Combination)" 
 Case OlUserPropertyType.olCurrency 
 strPropertyInfo = strPropertyInfo &amp; " (Currency)" 
 Case OlUserPropertyType.olDateTime 
 strPropertyInfo = strPropertyInfo &amp; " (Date/Time)" 
 Case OlUserPropertyType.olDuration 
 strPropertyInfo = strPropertyInfo &amp; " (Duration)" 
 Case OlUserPropertyType.olEnumeration 
 strPropertyInfo = strPropertyInfo &amp; " (Enumeration)" 
 Case OlUserPropertyType.olFormula 
 strPropertyInfo = strPropertyInfo &amp; " (Formula)" 
 Case OlUserPropertyType.olInteger 
 strPropertyInfo = strPropertyInfo &amp; " (Integer)" 
 Case OlUserPropertyType.olKeywords 
 strPropertyInfo = strPropertyInfo &amp; " (Keywords)" 
 Case OlUserPropertyType.olNumber 
 strPropertyInfo = strPropertyInfo &amp; " (Number)" 
 Case OlUserPropertyType.olOutlookInternal 
 strPropertyInfo = strPropertyInfo &amp; " (Outlook Internal)" 
 Case OlUserPropertyType.olPercent 
 strPropertyInfo = strPropertyInfo &amp; " (Percent)" 
 Case OlUserPropertyType.olSmartFrom 
 strPropertyInfo = strPropertyInfo &amp; " (Smart From)" 
 Case OlUserPropertyType.olText 
 strPropertyInfo = strPropertyInfo &amp; " (Text)" 
 Case OlUserPropertyType.olYesNo 
 strPropertyInfo = strPropertyInfo &amp; " (Yes/No)" 
 Case Else 
 strPropertyInfo = strPropertyInfo &amp; " (Unknown)" 
 End Select 
 
 ' Print the name and type of the user-defined property 
 ' to the Immediate window. 
 Debug.Print strPropertyInfo 
 Next 
 End If 
End Sub 

```


## See also


#### Concepts


 [Outlook Object Model Reference](73221b13-d8d8-99b8-3394-b95dbbfd5ddc.md)
#### Other resources


 [UserDefinedProperty Object Members](9a4fd85d-a47c-8871-bbe6-3383b28cc738.md)
