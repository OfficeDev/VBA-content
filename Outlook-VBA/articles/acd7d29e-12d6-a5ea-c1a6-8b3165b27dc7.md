
# Export a Calendar using Payload Sharing

 **Last modified:** July 28, 2015

 _**Applies to:** Outlook 2013_

Microsoft Outlook includes the ability to share calendar information with other users by using an iCalendar (.ics) file attached to a  ** [MailItem](14197346-05d2-0250-fa4c-4a6b07daf25f.md)**. The  ** [CalendarSharing](37a8a15e-51c2-b1a0-7db6-cf2a1f4e8405.md)** object is used to both generate the iCalendar file from a folder containing calendar items and to generate the **MailItem** to which the iCalendar file is attached.

This sample uses the  **CalendarSharing** item to share free/busy information for the next seven days with a single recipient:

1. The sample obtains a  ** [Folder](3cf6cda8-6d70-666e-2643-9d9c5b9cacfc.md)** object reference for the **Calendar** default folder for the current user, by using the ** [GetDefaultFolder](761b8b53-dd4d-43e4-c8f0-69cefdf0c77a.md)** method of the ** [NameSpace](f0dcaa19-07f5-5d42-a3bf-2e42b7885644.md)** object.
    
2. It uses the  ** [GetCalendarExporter](7c67e208-65dd-8904-4b6f-8ec2df4e530d.md)** method of the **Folder** object to create a **CalendarSharing** object reference for the folder.
    
3. It then sets the following properties on the  **CalendarSharing** object to restrict the range and level of information exported by the object:
    
4. 
      - The  ** [CalendarDetail](f3f0ba8d-23db-505f-58c4-6e3a33a468e7.md)** property is set to limit the information for each calendar item to free/busy information only.
    
  - The  ** [StartDate](218654d9-ab4c-9f3b-cb47-3c006eed710d.md)** and ** [EndDate](89358c71-7805-7acc-5afb-2ba7b592f9f2.md)** properties are set to restrict the calendar items included in the file to the next seven days.
    
  - The  ** [RestrictToWorkingHours](2d655c66-fd3e-0b82-41b2-798d408f6531.md)** property is set to restrict the calendar items to those that fall within working hours.
    
  - The  ** [IncludeAttachments](504bba9e-009f-986f-070e-ff73ce82ea03.md)** property is set to exclude any attachments for calendar items exported by the object.
    
  - The  ** [IncludePrivateDetails](a7c52e33-fe2a-b89a-9102-da2baf937e37.md)** property is set to exclude the details of any private calendar items exported by the object.
    
5. It then calles the  ** [ForwardAsICal](b796a573-784b-6725-535e-fd156a3f233c.md)** method of the **CalendarSharing** object to export the calendar items to an iCalendar file and create a **MailItem** object with the iCalendar file as an attachment. The **olCalendarMailFormatDailySchedule** constant of the ** [olCalendarMailFormat](b4b77080-1c8b-cfa4-3b3a-e59fec698bb1.md)** enumeration is used with the **ForwardAsICal** method to indicate that the body of the **MailItem** should contain, in HTML format, free/busy information for the next seven days.
    
6. Finally, the  ** [Add](7c285291-0f92-ca8d-1c7b-a71ace83ac84.md)** method for the ** [Recipients](58897f66-8a6a-e1a9-7e3b-5a84624f899d.md)** collection of the newly created **MailItem** object is called to add the specified recipient and the ** [Send](78c85013-523e-447b-c47d-2da0705f1fe0.md)** method is used to send the **MailItem**.
    



```
Public Sub ShareWorkCalendarByPayload() 
 
 Dim oNamespace As NameSpace 
 Dim oFolder As Folder 
 Dim oCalendarSharing As CalendarSharing 
 Dim oMailItem As MailItem 
 
 On Error GoTo ErrRoutine 
 ' Get a reference to the Calendar default folder 
 Set oNamespace = Application.GetNamespace("MAPI") 
 Set oFolder = oNamespace.GetDefaultFolder(olFolderCalendar) 
 
 ' Get a reference to a CalendarSharing object for that 
 ' folder. 
 Set oCalendarSharing = oFolder.GetCalendarExporter 
 
 ' Set the CalendarSharing object to restrict 
 ' the information shared in the iCalendar file. 
 With oCalendarSharing 
 ' Send free/busy information only. 
 .CalendarDetail = olFreeBusyOnly 
 
 ' Send information for the next seven days. 
 .startDate = Now 
 .endDate = DateAdd("d", 7, Now) 
 
 ' Restrict information to working hours only. 
 .RestrictToWorkingHours = True 
 
 ' Exclude attachments and private information. 
 .IncludeAttachments = False 
 .IncludePrivateDetails = False 
 End With 
 
 ' Get the mail item containing the iCalendar file 
 ' and calendar information. 
 Set oMailItem = oCalendarSharing.ForwardAsICal( _ 
 olCalendarMailFormatDailySchedule) 
 
 ' Send the mail item to the specified recipient. 
 With oMailItem 
 .Recipients.Add "someone@example.com" 
 .Send 
 End With 
 
EndRoutine: 
 On Error GoTo 0 
 Set oMailItem = Nothing 
 Set oCalendarSharing = Nothing 
 Set oFolder = Nothing 
 Set oNamespace = Nothing 
Exit Sub 
 
ErrRoutine: 
 Select Case Err.Number 
 Case 287 ' &amp;H0000011F 
 ' The user denied access to the Address Book. 
 ' This error occurs if the code is run by an 
 ' untrusted application, and the user chose not to 
 ' allow access. 
 MsgBox "Access to Outlook was denied by the user.", _ 
 vbOKOnly, _ 
 Err.Number &amp; " - " &amp; Err.Source 
 Case -2147467259 ' &amp;H80004005 
 ' Export failed. 
 ' This error typically occurs if the CalendarSharing 
 ' method cannot export the calendar information because 
 ' of conflicting property settings. 
 MsgBox Err.Description, _ 
 vbOKOnly, _ 
 Err.Number &amp; " - " &amp; Err.Source 
 Case -2147221233 ' &amp;H8004010F 
 ' Operation failed. 
 ' This error typically occurs if the GetCalendarExporter method 
 ' is called on a folder that doesn't contain calendar items. 
 MsgBox Err.Description, _ 
 vbOKOnly, _ 
 Err.Number &amp; " - " &amp; Err.Source 
 Case Else 
 ' Any other error that may occur. 
 MsgBox Err.Description, _ 
 vbOKOnly, _ 
 Err.Number &amp; " - " &amp; Err.Source 
 End Select 
 
 GoTo EndRoutine 
End Sub 

```

