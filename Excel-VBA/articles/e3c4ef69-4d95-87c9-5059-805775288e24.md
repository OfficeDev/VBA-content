
# IconSetCondition Object (Excel)

 **Last modified:** July 28, 2015

Represents an icon set conditional formatting rule.

## Remarks

All conditional formatting objects are contained within a  ** [FormatConditions](2486d4b4-605c-76d8-132a-694c0c600a81.md)** collection object, which is a child of a ** [Range](b8207778-0dcc-4570-1234-f130532cc8cd.md)** collection. You can create an icon set formatting rule by using either the ** [Add](705f9ad4-2500-6607-19c0-6abd3f214d3e.md)** method or ** [AddIconSetCondition](eb23b1b1-da7f-013e-aef3-5f690f5cb4ff.md)** method of the **FormatConditions** collection.

Each icon set contains three, four, or five icons. You use the  ** [IconSets](c837d2a8-d21d-7432-a409-f49426368556.md)** property of the ** [Workbook](8c00aa60-c974-eed3-0812-3c9625eb0d4c.md)** object to return an ** [IconSets](2197741e-8139-0098-d194-610fa28fa6c8.md)** object to specify one of the built-in icon sets. Each individual icon in the icon set is then assigned to a subset of the values of the range by the members of the ** [IconCriteria](c3b0480a-6def-c315-32ed-137b64708810.md)** object. The type of threshold is also specified by this object.


## Example

The following code example creates a range of numbers representing test scores and then applies an icon set conditional formatting rule to that range. The type of icon set is then changed from the default icons to a five-arrow icon set. Finally, the threshold type is modified from percentile to a hard-coded number.


```
Sub CreateIconSetCF() 
 
    Dim cfIconSet As IconSetCondition 
     
    'Fill cells with sample data from 1 to 10 
    With ActiveSheet 
        .Range("C1") = 55 
        .Range("C2") = 92 
        .Range("C3") = 88 
        .Range("C4") = 77 
        .Range("C5") = 66 
        .Range("C6") = 93 
        .Range("C7") = 76 
        .Range("C8") = 80 
        .Range("C9") = 79 
        .Range("C10") = 83 
        .Range("C11") = 66 
        .Range("C12") = 74 
    End With 
     
    Range("C1:C12").Select 
         
    'Create an icon set conditional format for the created sample data range 
    Set cfIconSet = Selection.FormatConditions.AddIconSetCondition 
     
    'Change the icon set to a five-arrow icon set 
    cfIconSet.IconSet = ActiveWorkbook.IconSets(xl5Arrows) 
     
    'The IconCriterion collection contains all IconCriteria 
    'By indexing into the collection you can modify each criterion 
 
    With cfIconSet.IconCriteria(1) 
        .Type = xlConditionValueNumber 
        .Value = 0 
        .Operator = 7 
    End With 
    With cfIconSet.IconCriteria(2) 
        .Type = xlConditionValueNumber 
        .Value = 60 
        .Operator = 7 
    End With 
    With cfIconSet.IconCriteria(3) 
        .Type = xlConditionValueNumber 
        .Value = 70 
        .Operator = 7 
    End With 
    With cfIconSet.IconCriteria(4) 
        .Type = xlConditionValueNumber 
        .Value = 80 
        .Operator = 7 
    End With 
    With cfIconSet.IconCriteria(5) 
        .Type = xlConditionValueNumber 
        .Value = 90 
        .Operator = 7 
    End With 
         
End Sub
```


## See also


#### Concepts


 [Excel Object Model Reference](11ea8598-8a20-92d5-f98b-0da04263bf2c.md)
#### Other resources


 [IconSetCondition Object Members](5ea20648-be46-7b8b-be31-368fc98329ab.md)
