
# Range Object (Excel)

 **Last modified:** July 28, 2015

 **In this article**
 [Remarks](#sectionSection0)
 [Example](#sectionSection1)
 [About the Contributor](#AboutContributor)


Represents a cell, a row, a column, a selection of cells containing one or more contiguous blocks of cells, or a 3-D range.


## Remarks
<a name="sectionSection0"> </a>

The following properties and methods for returning a  **Range** object are described in the examples section:


-  ** [Range](9a323305-c822-ef9e-1cc8-ec077a976834.md)** property
    
-  ** [Cells](19c14e41-7d8e-b56f-fd60-717df64edee8.md)** property
    
-  **Range** and **Cells**
    
-  ** [Offset](dfbbd1a2-2f73-fd6a-6277-4584823f55a4.md)** property
    
-  ** [Union](7c70a5be-2696-5fc2-bd69-6c6ff4d3291e.md)** method
    

## Example
<a name="sectionSection1"> </a>

Use  **Range**( _arg_), where  _arg_ names the range, to return a **Range** object that represents a single cell or a range of cells. The following example places the value of cell A1 in cell A5.


```
Worksheets("Sheet1").Range("A5").Value = _ 
    Worksheets("Sheet1").Range("A1").Value
```

The following example fills the range A1:H8 with random numbers by setting the formula for each cell in the range. When it's used without an object qualifier (an object to the left of the period), the  **Range** property returns a range on the active sheet. If the active sheet isn't a worksheet, the method fails. Use the ** [Activate](b198dc36-99d0-42db-6cbb-7f68396fd2f5.md)** method to activate a worksheet before you use the **Range** property without an explicit object qualifier.




```
Worksheets("Sheet1").Activate 
Range("A1:H8").Formula = "=Rand()"    'Range is on the active sheet
```

The following example clears the contents of the range named  _Criteria_.


 **Note**  If you use a text argument for the range address, you must specify the address in A1-style notation (you cannot use R1C1-style notation).




```
Worksheets(1).Range("Criteria").ClearContents
```

Use  **Cells**( _row_,  _column_) where  _row_ is the row index and _column_ is the column index, to return a single cell. The following example sets the value of cell A1 to 24.




```
Worksheets(1).Cells(1, 1).Value = 24
```

The following example sets the formula for cell A2.




```
ActiveSheet.Cells(2, 1).Formula = "=Sum(B1:B5)"
```

Although you can also use  `Range("A1")` to return cell A1, there may be times when the **Cells** property is more convenient because you can use a variable for the row or column. The following example creates column and row headings on Sheet1. Be aware that after the worksheet has been activated, the **Cells** property can be used without an explicit sheet declaration (it returns a cell on the active sheet).


 **Note**  Although you could use Visual Basic string functions to alter A1-style references, it is easier (and better programming practice) to use the  `Cells(1, 1)` notation.




```
Sub SetUpTable() 
Worksheets("Sheet1").Activate 
For TheYear = 1 To 5 
    Cells(1, TheYear + 1).Value = 1990 + TheYear 
Next TheYear 
For TheQuarter = 1 To 4 
    Cells(TheQuarter + 1, 1).Value = "Q" &amp; TheQuarter 
Next TheQuarter 
End Sub
```

Use  _expression_. **Cells**( _row_,  _column_), where  _expression_ is an expression that returns a **Range** object, and _row_ and _column_ are relative to the upper-left corner of the range, to return part of a range. The following example sets the formula for cell C5.




```
Worksheets(1).Range("C5:C10").Cells(1, 1).Formula = "=Rand()"
```

Use  **Range**( _cell1, cell2_), where  _cell1_ and _cell2_ are **Range** objects that specify the start and end cells, to return a **Range** object. The following example sets the border line style for cells A1:J10.


 **Note**  Be aware that the period in front of each occurrence of the  **Cells** property. The period is required if the result of the preceding **With** statement is to be applied to the **Cells** propertyâ€”in this case, to indicate that the cells are on worksheet one (without the period, the **Cells** property would return cells on the active sheet).




```
With Worksheets(1) 
    .Range(.Cells(1, 1), _ 
        .Cells(10, 10)).Borders.LineStyle = xlThick 
End With
```

Use  **Offset**( _row, column_), where  _row_ and _column_ are the row and column offsets, to return a range at a specified offset to another range. The following example selects the cell three rows down from and one column to the right of the cell in the upper-left corner of the current selection. You cannot select a cell that is not on the active sheet, so you must first activate the worksheet.




```
Worksheets("Sheet1").Activate 
  'Can't select unless the sheet is active 
Selection.Offset(3, 1).Range("A1").Select
```

Use  **Union**( _range1, range2_, ...) to return multiple-area rangesâ€”that is, ranges composed of two or more contiguous blocks of cells. The following example creates an object defined as the union of ranges A1:B2 and C3:D4, and then selects the defined range.




```
Dim r1 As Range, r2 As Range, myMultiAreaRange As Range 
Worksheets("sheet1").Activate 
Set r1 = Range("A1:B2") 
Set r2 = Range("C3:D4") 
Set myMultiAreaRange = Union(r1, r2) 
myMultiAreaRange.Select
```

If you work with selections that contain more than one area, the  ** [Areas](31fc03b4-25b6-27ae-2350-b34c6c6ba255.md)** property is useful. It divides a multiple-area selection into individual **Range** objects and then returns the objects as a collection. You can use the ** [Count](080cbbe7-056f-b21c-9004-171a6acce664.md)** property on the returned collection to verify a selection that contains more than one area, as shown in the following example.




```
Sub NoMultiAreaSelection() 
    NumberOfSelectedAreas = Selection.Areas.Count 
    If NumberOfSelectedAreas > 1 Then 
        MsgBox "You cannot carry out this command " &amp; _ 
            "on multi-area selections" 
    End If 
End Sub
```

 **Sample code provided by:** Dennis Wallentin, [VSTO &amp; .NET &amp; Excel](http://xldennis.wordpress.com/)

This example uses the  **AdvancedFilter** method of the **Range** object to create a list of the unique values, and the number of times those unique values occur, in the range of column A.




```
Sub Create_Unique_List_Count()
    'Excel workbook, the source and target worksheets, and the source and target ranges.
    Dim wbBook As Workbook
    Dim wsSource As Worksheet
    Dim wsTarget As Worksheet
    Dim rnSource As Range
    Dim rnTarget As Range
    Dim rnUnique As Range
    'Variant to hold the unique data
    Dim vaUnique As Variant
    'Number of unique values in the data
    Dim lnCount As Long
    
    'Initialize the Excel objects
    Set wbBook = ThisWorkbook
    With wbBook
        Set wsSource = .Worksheets("Sheet1")
        Set wsTarget = .Worksheets("Sheet2")
    End With
    
    'On the source worksheet, set the range to the data stored in column A
    With wsSource
        Set rnSource = .Range(.Range("A1"), .Range("A100").End(xlUp))
    End With
    
    'On the target worksheet, set the range as column A.
    Set rnTarget = wsTarget.Range("A1")
    
    'Use AdvancedFilter to copy the data from the source to the target,
    'while filtering for duplicate values.
    rnSource.AdvancedFilter Action:=xlFilterCopy, _
                            CopyToRange:=rnTarget, _
                            Unique:=True
                            
    'On the target worksheet, set the unique range on Column A, excluding the first cell
    '(which will contain the "List" header for the column).
    With wsTarget
        Set rnUnique = .Range(.Range("A2"), .Range("A100").End(xlUp))
    End With
    
    'Assign all the values of the Unique range into the Unique variant.
    vaUnique = rnUnique.Value
    
    'Count the number of occurrences of every unique value in the source data,
    'and list it next to its relevant value.
    For lnCount = 1 To UBound(vaUnique)
        rnUnique(lnCount, 1).Offset(0, 1).Value = _
            Application.Evaluate("COUNTIF(" &amp; _
            rnSource.Address(External:=True) &amp; _
            ",""" &amp; rnUnique(lnCount, 1).Text &amp; """)")
    Next lnCount
    
    'Label the column of occurrences with "Occurrences"
    With rnTarget.Offset(0, 1)
        .Value = "Occurrences"
        .Font.Bold = True
    End With

End Sub
```


## About the Contributor
<a name="AboutContributor"> </a>

Dennis Wallentin is the author of VSTO &amp; .NET &amp; Excel, a blog that focuses on .NET Framework solutions for Excel and Excel Services. Dennis has been developing Excel solutions for over 20 years and is also the co-author of "Professional Excel Development: The Definitive Guide to Developing Applications Using Microsoft Excel, VBA and .NET (2nd Edition)." 

