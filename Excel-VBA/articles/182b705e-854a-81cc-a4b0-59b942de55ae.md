
# Worksheet Object (Excel)

 **Last modified:** July 28, 2015

 **In this article**
 [Remarks](#sectionSection0)
 [Example](#sectionSection1)
 [About the Contributor](#AboutContributor)


Represents a worksheet.


## Remarks
<a name="sectionSection0"> </a>

The  **Worksheet** object is a member of the ** [Worksheets](5ec467a6-97e3-98d7-0b14-845d20c15910.md)** collection. The **Worksheets** collection contains all the **Worksheet** objects in a workbook.

The  **Worksheet** object is also a member of the [Sheets](048fd93c-bc27-4b58-358f-56fcee1710f8.md) collection. The **Sheets** collection contains all the sheets in the workbook (both chart sheets and worksheets).


## Example
<a name="sectionSection1"> </a>

Use  ** [Worksheets](8b7d660d-ca49-0bd0-dc57-64defa47bd5e.md)**( _index_), where  _index_ is the worksheet index number or name, to return a single **Worksheet** object. The following example hides worksheet one in the active workbook.


```
Worksheets(1).Visible = False
```

The worksheet index number denotes the position of the worksheet on the workbook's tab bar.  `Worksheets(1)` is the first (leftmost) worksheet in the workbook, and `Worksheets(Worksheets.Count)` is the last one. All worksheets are included in the index count, even if they're hidden.



The worksheet name is shown on the tab for the worksheet. Use the  ** [Name](3d000cdf-5e81-8701-ca7f-bdcce006363b.md)** property to set or return the worksheet name. The following example protects the scenarios on Sheet1.




```
 
Dim strPassword As String 
strPassword = InputBox ("Enter the password for the worksheet") 
Worksheets("Sheet1").Protect password:=strPassword, scenarios:=True
```

When a worksheet is the active sheet, you can use the  ** [ActiveSheet](fb5578c3-64a7-edb7-4004-e608739d4c1e.md)** property to refer to it. The following example uses the ** [Activate](b198dc36-99d0-42db-6cbb-7f68396fd2f5.md)** method to activate Sheet1, sets the page orientation to landscape mode, and then prints the worksheet.




```
Worksheets("Sheet1").Activate 
ActiveSheet.PageSetup.Orientation = xlLandscape 
ActiveSheet.PrintOut
```

 **Sample code provided by:** Holy Macro! Books, [Holy Macro! It's 2,500 Excel VBA Examples](http://www.mrexcel.com/store/index.php?l=product_detail&amp;p=1)

This example uses the BeforeDoubleClick event to open a specified set of files in Notepad. To use this example your worksheet must contain the following data:


- Cell A1 must contain the names of the files to open, each separated by a comma and a space.
    
- Cell D1 must contain the path to where the Notepad files are located.
    
- Cell D2 must contain the path to where the Notepad program is located.
    
- Cell D3 must contain the file extension, without the period, for the Notepad files (txt).
    
When you double-click cell A1, the files specified in cell A1 are opened in Notepad.




```
Private Sub Worksheet_BeforeDoubleClick(ByVal Target As Range, Cancel As Boolean)
   'Define your variables.
   Dim sFile As String, sPath As String, sTxt As String, sExe As String, sSfx As String
   
   'If you did not double-click on A1, then exit the function.
   If Target.Address <> "$A$1" Then Exit Sub
   
   'If you did double-click on A1, then override the default double-click behaviour with this function.
   Cancel = True
   
   'Set the path to the files, the path to Notepad, the file extension of the files, and the names of the files,
   'based on the information in the worksheet.
   sPath = Range("D1").Value
   sExe = Range("D2").Value
   sSfx = Range("D3").Value
   sFile = Range("A1").Value
   
   'Remove the spaces between the file names.
   sFile = WorksheetFunction.Substitute(sFile, " ", "")
   
   'Go through each file in the list (separated by commas) and
   'create the path, call the executable, and move on to the next comma.
   Do While InStr(sFile, ",")
      sTxt = sPath &amp; "\" &amp; Left(sFile, InStr(sFile, ",") - 1) &amp; "." &amp; sSfx
      If Dir(sTxt) <> "" Then Shell sExe &amp; " " &amp; sTxt, vbNormalFocus
      sFile = Right(sFile, Len(sFile) - InStr(sFile, ","))
   Loop
   
   'Finish off the last file name in the list
   sTxt = sPath &amp; "\" &amp; sFile &amp; "." &amp; sSfx
   If Dir(sTxt) <> "" Then Shell sExe &amp; " " &amp; sTxt, vbNormalNoFocus
End Sub
```


## About the Contributor
<a name="AboutContributor"> </a>

Holy Macro! Books publishes entertaining books for people who use Microsoft Office. See the complete catalog at MrExcel.com. 


## See also
<a name="AboutContributor"> </a>


#### Concepts


 [Excel Object Model Reference](11ea8598-8a20-92d5-f98b-0da04263bf2c.md)
#### Other resources


 [Worksheet Object Members](f8c1afea-1a1c-f5e4-37e3-52c434c8c157.md)
